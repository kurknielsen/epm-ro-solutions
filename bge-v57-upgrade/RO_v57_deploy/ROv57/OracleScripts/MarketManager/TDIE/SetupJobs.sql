DECLARE
	v_PROG_DISABLED	NUMBER(1);
	v_JOB_ENABLED	NUMBER(1);
	v_COUNT		PLS_INTEGER;
BEGIN
	-- Cache state in case they are currently enabled - so we re-enable them after re-creating them

	-- If program is explicitly disabled, keep it disabled after it is re-created.
	-- Otherwise, default is to enable the program.
	SELECT COUNT(1)
	INTO v_PROG_DISABLED
	FROM USER_SCHEDULER_PROGRAMS
	WHERE PROGRAM_NAME = 'FETCH_TDIE_IMPORT_FILES'
		AND ENABLED = 'FALSE';

	-- If job is explicitly enabled, keep it enabled after it is re-created.
	-- Otherwise, default is to disable the job.
	SELECT COUNT(1)
	INTO v_JOB_ENABLED
	FROM USER_SCHEDULER_JOBS
	WHERE JOB_NAME = 'FETCH_TDIE_IMPORT_FILES_JOB'
		AND ENABLED = 'TRUE';

	-- Re-create the program		
	SELECT COUNT(1)
	INTO v_COUNT
	FROM USER_SCHEDULER_PROGRAMS
	WHERE PROGRAM_NAME = 'FETCH_TDIE_IMPORT_FILES';

	IF v_COUNT > 0 THEN
		DBMS_SCHEDULER.DROP_PROGRAM(PROGRAM_NAME => 'FETCH_TDIE_IMPORT_FILES', FORCE => TRUE);
	END IF;

	DBMS_SCHEDULER.CREATE_PROGRAM(PROGRAM_NAME => 'FETCH_TDIE_IMPORT_FILES',
								PROGRAM_TYPE => 'PLSQL_BLOCK',
								PROGRAM_ACTION =>
'DECLARE
	v_PROCESS_ID	VARCHAR2(12);
	v_PROCESS_STATUS NUMBER;
	v_MESSAGE		VARCHAR2(4000);
BEGIN
	--SESSION_START is required for every job, to set the current user name.
	SESSION_START;
	--Fetch the files
	MM_TDIE_IMPORTS.FETCH_IMPORT_FILES(p_TRACE_ON => 0, p_PROCESS_ID => v_PROCESS_ID, p_PROCESS_STATUS => v_PROCESS_STATUS, p_MESSAGE => v_MESSAGE);
END;',
								ENABLED => v_PROG_DISABLED = 0
								);

	-- Re-create the job
	SELECT COUNT(1)
	INTO v_COUNT
	FROM USER_SCHEDULER_JOBS
	WHERE JOB_NAME = 'FETCH_TDIE_IMPORT_FILES_JOB';

	IF v_COUNT > 0 THEN
		DBMS_SCHEDULER.DROP_JOB(JOB_NAME => 'FETCH_TDIE_IMPORT_FILES_JOB');
	END IF;

	DBMS_SCHEDULER.CREATE_JOB(JOB_NAME => 'FETCH_TDIE_IMPORT_FILES_JOB',
							PROGRAM_NAME => 'FETCH_TDIE_IMPORT_FILES',
							SCHEDULE_NAME => 'EVERY_5_MINUTES',
							ENABLED => v_JOB_ENABLED <> 0
							);
	-- Remove any previous JOB_DATA
	DELETE FROM JOB_DATA D WHERE D.JOB_NAME = 'FETCH_TDIE_IMPORT_FILES_JOB';
	IF SQL%ROWCOUNT > 0 THEN
		LOGS.LOG_WARN('Deleted JOB_DATA for JOB_NAME = FETCH_TDIE_IMPORT_FILES_JOB');
	END IF;

	--Insert the new JOB_DATA
	INSERT INTO JOB_DATA
	   (JOB_NAME, USER_ID, JOB_THREAD_ID, ACTION_CHAIN_NAME, ACTION_DISPLAY_NAME, NOTIFICATION_EMAIL_ADDRESS)
	VALUES
	   ('FETCH_TDIE_IMPORT_FILES_JOB', SECURITY_CONTROLS.c_SUSER_ID_SYSTEM, NULL, 'System Job', NULL, NULL);
END;
/
