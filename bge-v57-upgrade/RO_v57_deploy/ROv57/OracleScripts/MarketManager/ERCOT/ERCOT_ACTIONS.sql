SET DEFINE OFF	

-- Ignore &'s in data; otherwise it will assume word after & is a substitution variable and prompt for a value	
-- Uncomment the following line if you wish to completely eliminate previous entries and start from scratch w/ delivered entries

DECLARE
	v_USER_ROLE_ID NUMBER(9);
	v_PUSER_ROLE_ID NUMBER (9);
	v_STATUS NUMBER;
	
	TYPE MARKET_T IS RECORD (
		MARKET_NAME VARCHAR2(10), 
		PACKAGE_NAME VARCHAR2(32), 
		HAS_EXCHANGE_LIST_PROC NUMBER(1),
		HAS_SUBMIT_LIST_PROC NUMBER(1)
		);
	
	c_EXTRACT MARKET_T;
	c_LMP MARKET_T;
	c_SETTLEMENT MARKET_T;
	
	--Action Types
	c_DE CONSTANT VARCHAR2(20) := 'Data Exchange';
	c_BO CONSTANT VARCHAR2(20) := 'Bid Offer';
	c_IM CONSTANT VARCHAR2(20) := 'Import';
------------------------------------
	PROCEDURE INIT_MARKET
		(
		p_MARKET IN OUT MARKET_T,
		p_MARKET_NAME IN VARCHAR2,
		p_PACKAGE_NAME IN VARCHAR2,
		p_HAS_EXCHANGE_LIST_PROC IN NUMBER,
		p_HAS_SUBMIT_LIST_PROC IN NUMBER
		) AS
	BEGIN
		p_MARKET.MARKET_NAME := p_MARKET_NAME;
		p_MARKET.PACKAGE_NAME := p_PACKAGE_NAME;
		p_MARKET.HAS_EXCHANGE_LIST_PROC := p_HAS_EXCHANGE_LIST_PROC;
		p_MARKET.HAS_SUBMIT_LIST_PROC := p_HAS_SUBMIT_LIST_PROC;
	END INIT_MARKET;
------------------------------------
	PROCEDURE PUT_ACTION
		(
		p_MARKET IN MARKET_T,
		p_EXCHANGE_CATEGORY IN VARCHAR2,
		p_EXCHANGE_NAME IN VARCHAR2,
		p_DISABLE_ACTION IN BOOLEAN := FALSE
		) AS

		v_ACTION_NAME SYSTEM_ACTION.ACTION_NAME%TYPE;
		v_DISPLAY_NAME SYSTEM_ACTION.DISPLAY_NAME%TYPE;
		v_MKT VARCHAR2(10);
		v_ACTION_TYPE VARCHAR2(32);
		v_MAIN_PROC VARCHAR2(256);
		v_IMPORT_TYPE SYSTEM_ACTION.IMPORT_TYPE%TYPE;
		v_EXCHANGE_TYPE SYSTEM_ACTION.EXCHANGE_TYPE%TYPE;
		v_ID NUMBER;
		v_ACTION_ALREADY_EXISTED BOOLEAN := TRUE;
		v_WARNING_PROC VARCHAR2(100) := NULL;
		v_LIST_PROC VARCHAR2(100) := NULL;
	
	 BEGIN
	 	--Set the Name and Display Name for the Action
	 	v_ACTION_NAME := 'Sched:ERCOT:' || CASE WHEN p_MARKET.MARKET_NAME IS NULL THEN '' ELSE p_MARKET.MARKET_NAME || ':' END || p_EXCHANGE_NAME;
		v_DISPLAY_NAME := 'ERCOT: ' || CASE WHEN p_MARKET.MARKET_NAME IS NULL THEN '' ELSE p_MARKET.MARKET_NAME || ': ' END || p_EXCHANGE_NAME ;
		
		--Set the Action Type to determine whether it shows up on Bid/Offer or Data Exchange dialog.
		v_ACTION_TYPE := CASE p_EXCHANGE_CATEGORY WHEN c_IM THEN c_DE ELSE p_EXCHANGE_CATEGORY END;

		--Set up the main Stored Procedure.
		v_MAIN_PROC := p_MARKET.PACKAGE_NAME || '.' || CASE p_EXCHANGE_CATEGORY
			WHEN c_DE THEN 'MARKET_EXCHANGE'
			WHEN c_IM THEN 'MARKET_IMPORT_CLOB'
			WHEN c_BO THEN 'MARKET_SUBMIT'
			END || ';EXCHANGE_TYPE="' || p_EXCHANGE_NAME || '"';
			
		IF p_EXCHANGE_CATEGORY = c_IM THEN
			v_IMPORT_TYPE := v_MAIN_PROC;
			v_EXCHANGE_TYPE := NULL;
		ELSE
			v_IMPORT_TYPE := NULL;
			v_EXCHANGE_TYPE := v_MAIN_PROC;
			IF p_EXCHANGE_CATEGORY = c_BO THEN
				v_WARNING_PROC := 'MM_MISO.MARKET_SUBMIT_WARNING';
				v_LIST_PROC := CASE WHEN p_MARKET.HAS_SUBMIT_LIST_PROC= 1 THEN
						p_MARKET.PACKAGE_NAME || '.' || 'MARKET_SUBMIT_TRANSACTION_LIST' || ';EXCHANGE_TYPE="' || p_EXCHANGE_NAME || '"'
					ELSE NULL END;
			ELSE
				v_LIST_PROC := CASE WHEN p_MARKET.HAS_EXCHANGE_LIST_PROC = 1 THEN 
						p_MARKET.PACKAGE_NAME || '.' || 'MARKET_EXCHANGE_ENTITY_LIST' || ';EXCHANGE_TYPE="' || p_EXCHANGE_NAME || '"'
					ELSE NULL END;
			END IF;	
		END IF;
			
		
		--Get the ID for the Action, and set a flag to remember whether it already existed.
		v_ID := ID.ID_FOR_SYSTEM_ACTION(v_ACTION_NAME);
		IF v_ID <= 0 THEN
			v_ACTION_ALREADY_EXISTED := FALSE;
			v_ID := 0;
		ELSE
			v_ACTION_ALREADY_EXISTED := TRUE;
		END IF;
				
		--Insert/Update the System Action.
		IO.PUT_SYSTEM_ACTION(v_ID,	-- o_OID
			v_ACTION_NAME,      -- p_EXCHANGE_NAME
			NULL,               -- p_ACTION_ALIAS
			v_ACTION_NAME,      -- p_ACTION_DESC
			v_ID,               -- p_ACTION_ID
			0,                  -- p_ENTITY_DOMAIN_ID
			'Scheduling',       -- p_MODULE
			v_ACTION_TYPE,      -- p_ACTION_TYPE
			v_DISPLAY_NAME,     -- p_DISPLAY_NAME
			v_IMPORT_TYPE,      -- p_IMPORT_TYPE
			NULL,               -- p_EXPORT_TYPE
			v_EXCHANGE_TYPE,    -- p_EXCHANGE_TYPE
			NULL,               -- p_IMPORT_FILE
			NULL,               -- p_EXPORT_FILE
			v_WARNING_PROC,     -- p_WARNING_PROC
			v_LIST_PROC);       -- p_ENTITY_LIST
			
		IF v_ID < 0 THEN
			ROLLBACK;
			RAISE_APPLICATION_ERROR(-20010, 'Failed to create action ' || v_ACTION_NAME || ' with status = ' || v_ID);
		END IF;

					
		--Add the 'User' and 'Power-User' roles to it if it did not already exist.
		--If it did already exist, we want to leave the roles alone.
		IF NOT v_ACTION_ALREADY_EXISTED AND NOT p_DISABLE_ACTION THEN
			EM.PUT_SYSTEM_ACTION_ROLE(v_ID, v_USER_ROLE_ID, 1, 0, 0, 0, 0, v_STATUS);
			EM.PUT_SYSTEM_ACTION_ROLE(v_ID, v_PUSER_ROLE_ID, 1, 0, 0, 0, 0, v_STATUS);
		END IF;
				
	END PUT_ACTION;
	------------------------------------	
BEGIN
	--Initialize PJM Market and Package names
	INIT_MARKET(c_EXTRACT, 'EXTRACT', 'MM_ERCOT_EXTRACT', 1, 1);
	INIT_MARKET(c_LMP, 'LMP', 'MM_ERCOT_LMP', 0, 0);
	INIT_MARKET(c_SETTLEMENT, 'SETTLEMENT', 'MM_ERCOT_SETTLEMENT', 0, 0);


	SELECT ROLE_ID INTO v_USER_ROLE_ID FROM APPLICATION_ROLE WHERE ROLE_NAME = 'User';
	SELECT ROLE_ID INTO v_PUSER_ROLE_ID FROM APPLICATION_ROLE WHERE ROLE_NAME = 'Power-User';


	--ERCOT EXTRACT MARKET EXCHANGE
	PUT_ACTION(c_EXTRACT, c_DE, MM_ERCOT_EXTRACT.g_ET_IMPORT_LOAD_EXTRACT);
	PUT_ACTION(c_EXTRACT, c_DE, MM_ERCOT_EXTRACT.g_ET_IMPORT_ESIID_EXTRACT);
	
	--ERCOT LMP MARKET EXCHANGE
	PUT_ACTION(c_LMP, c_DE, MM_ERCOT_LMP.g_ET_QUERY_MRKT_CLRING_PRICES);
	PUT_ACTION(c_LMP, c_DE, MM_ERCOT_LMP.g_ET_QUERY_ANC_SERVICE_PRICES);
	
	--ERCOT SETTLEMENT MARKET EXCHANGE
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_ANNUAL_INITIAL);
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_MONTHLY_INITIAL);
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_ANNUAL_FINAL);
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_MONTHLY_FINAL);
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_ANNUAL_TRUE_UP);
	PUT_ACTION(c_SETTLEMENT, c_DE, MM_ERCOT_SETTLEMENT.g_ET_IMPORT_MONTHLY_TRUE_UP);

END;
/
-- save changes to database
COMMIT;
SET DEFINE ON	
--Reset
