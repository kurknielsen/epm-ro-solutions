CREATE OR REPLACE TRIGGER DR_EVENT_UPDATE
  AFTER UPDATE ON DR_EVENT  
  FOR EACH ROW
DECLARE
  --
BEGIN
  IF :NEW.EVENT_STATUS IN (DEMAND_RESPONSE_UTIL.c_EVENT_STATUS_STARTED,
  							DEMAND_RESPONSE_UTIL.c_EVENT_STATUS_SYSTEM_OVERRIDE,
							DEMAND_RESPONSE_UTIL.c_EVENT_STATUS_RESUMED,
							DEMAND_RESPONSE_UTIL.c_EVENT_STATUS_ENDED)
	 AND :NEW.EVENT_STATUS = :OLD.EVENT_STATUS THEN
	 -- IF OUR CURRENT STATUS IS STARTED, SYSTEM OVERRIDE, RESUMED OR ENDED AND WE DIDN'T JUST CHANGE IT TO
	 -- THIS, THEN CHECK KEY ATTRIBUTES.  WE LET THE USER CHANGE THE STATUS AND CHANGE THIS ATTIRBUTES AT THE 
	 -- SAME TIME
	 
	IF NVL(:NEW.START_TIME, CONSTANTS.LOW_DATE) <> NVL(:OLD.START_TIME, CONSTANTS.LOW_DATE) THEN
		LOGS.LOG_WARN('The start time for DR Event ' || :NEW.EVENT_NAME ||
			' has been changed to ' || TEXT_UTIL.TO_CHAR_TIME(:NEW.START_TIME) || ' but it has a ' ||
			' status of ' || :NEW.EVENT_STATUS || '.');
	END IF;
	
	IF NVL(:NEW.STOP_TIME, CONSTANTS.LOW_DATE) <> NVL(:OLD.STOP_TIME, CONSTANTS.LOW_DATE) THEN
		LOGS.LOG_WARN('The stop time for DR Event ' || :NEW.EVENT_NAME ||
			' has been changed to ' || TEXT_UTIL.TO_CHAR_TIME(:NEW.STOP_TIME) || ' but it has a ' ||
			' status of ' || :NEW.EVENT_STATUS || '.');
	END IF;
	
	IF NVL(:NEW.EVENT_TYPE, CONSTANTS.UNDEFINED_ATTRIBUTE) <> NVL(:OLD.EVENT_TYPE, CONSTANTS.UNDEFINED_ATTRIBUTE) THEN
		LOGS.LOG_WARN('The event type for DR Event ' || :NEW.EVENT_NAME ||
			' has been changed to ' || :NEW.EVENT_TYPE || ' but it has a ' ||
			' status of ' || :NEW.EVENT_STATUS || '.');
	END IF;
							
  END IF;
END DR_EVENT_UPDATE;
/
