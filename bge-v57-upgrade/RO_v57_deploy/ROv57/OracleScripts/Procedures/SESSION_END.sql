CREATE OR REPLACE PROCEDURE SESSION_END IS
-- Revision: $Revision: 1.5 $
v_CURRENT_USER 	APPLICATION_USER.USER_NAME%TYPE;
v_LOGGED_OFF	BOOLEAN := FALSE;
-- Added these local variables to avoid binding queries to SYS_CONTEXT.
v_SID V$SESSION.SID%TYPE := SYS_CONTEXT('USERENV', 'SID');
v_AUDSID V$SESSION.AUDSID%TYPE := SYS_CONTEXT('USERENV', 'SESSIONID');
BEGIN
	IF GA.EVENT_LOG_FOR_LOGON_LOGOFF THEN
		v_CURRENT_USER := NVL(SECURITY_CONTROLS.CURRENT_USER,'(Unauthenticated)');
		LOGS.LOG_INFO(v_CURRENT_USER||' has logged off');
	END IF;

	LOGS.CLEANUP_ABANDONED_PROCESSES(TRUE);

	SECURITY_CONTROLS.SET_CURRENT_USER(NULL);
	v_LOGGED_OFF := TRUE;

	DELETE CURRENT_SESSION_USER;
	
	DELETE SYSTEM_SESSION S
	WHERE S.SESSION_SID = v_SID
		AND S.SESSION_AUDSID = v_AUDSID;

EXCEPTION
	WHEN OTHERS THEN
		IF GA.EVENT_LOG_FOR_LOGON_LOGOFF AND NOT v_LOGGED_OFF THEN
			ERRS.LOG_AND_CONTINUE(v_CURRENT_USER||' could not be logged off');
		ELSE
			ERRS.LOG_AND_CONTINUE(v_CURRENT_USER||' has been logged off prior to exception');
		END IF;
END SESSION_END;
/
