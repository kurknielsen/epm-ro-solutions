CREATE OR REPLACE PROCEDURE RECOMPILE_INVALID_OBJECTS AS
	v_TYPE VARCHAR2(32);
	v_SUBTYPE VARCHAR2(32);
BEGIN
	FOR v_OBJ IN (SELECT OBJECT_TYPE, OBJECT_NAME
				  FROM USER_OBJECTS
				  WHERE STATUS <> 'VALID'
				  	AND OBJECT_TYPE IN ('TRIGGER','VIEW','PACKAGE','PACKAGE BODY','TYPE','TYPE BODY','FUNCTION','PROCEDURE')
				  ORDER BY CASE WHEN OBJECT_TYPE = 'TYPE' THEN 1
						WHEN OBJECT_TYPE = 'PACKAGE' THEN 2
						WHEN OBJECT_TYPE IN ('FUNCTION','PROCEDURE') THEN 3
						WHEN OBJECT_TYPE = 'TRIGGER' THEN 5
						ELSE 4
						END)
				LOOP
		BEGIN
			v_TYPE := CASE V_OBJ.OBJECT_TYPE
						WHEN 'PACKAGE BODY' THEN 'PACKAGE'
						WHEN 'TYPE BODY' THEN 'TYPE'
						ELSE V_OBJ.OBJECT_TYPE
						END;
			v_SUBTYPE := CASE V_OBJ.OBJECT_TYPE
						WHEN 'PACKAGE' THEN 'SPECIFICATION'
						WHEN 'TYPE' THEN 'SPECIFICATION'
						WHEN 'PACKAGE BODY' THEN 'BODY'
						WHEN 'TYPE BODY' THEN 'BODY'
						ELSE NULL
						END;
			EXECUTE IMMEDIATE 'ALTER '||v_TYPE||' '||v_OBJ.OBJECT_NAME||' COMPILE '||v_SUBTYPE;
		EXCEPTION
			WHEN OTHERS THEN
				DBMS_OUTPUT.PUT_LINE(v_OBJ.OBJECT_TYPE||' '||v_OBJ.OBJECT_NAME||': '||SQLERRM);
		END;
	END LOOP;
END;
/
