CREATE OR REPLACE PACKAGE BODY CS AS
----------------------------------------------------------------------------------------------------
FUNCTION WHAT_VERSION RETURN VARCHAR IS
BEGIN
    RETURN '$Revision: 1.4 $';
END WHAT_VERSION;
----------------------------------------------------------------------------------------------------
PROCEDURE CACHE_ESP_SUPPLY_TYPE AS

-- Cache the ESP Supply Type by ESP ID..

CURSOR c_ESP_SUPPLY_TYPE IS
	SELECT ESP_ID, UPPER(SUBSTR(ESP_TYPE,1,1)) "SUPPLY_TYPE"
	FROM ENERGY_SERVICE_PROVIDER
	WHERE UPPER(SUBSTR(ESP_STATUS,1,1)) = 'A';

BEGIN

	FOR v_ESP_SUPPLY_TYPE IN c_ESP_SUPPLY_TYPE LOOP
		g_ESP_SUPPLY_TYPE_CACHE(v_ESP_SUPPLY_TYPE.ESP_ID) := v_ESP_SUPPLY_TYPE.SUPPLY_TYPE;
	END LOOP;

END CACHE_ESP_SUPPLY_TYPE;
----------------------------------------------------------------------------------------------------
PROCEDURE CACHE_PSE_IS_BUG AS

-- Cache the PSE BUG attributre by PSE Id.

CURSOR c_PSE_IS_BUG IS
	SELECT PSE_ID, PSE_IS_BACKUP_GENERATION
	FROM PURCHASING_SELLING_ENTITY
	WHERE UPPER(SUBSTR(PSE_STATUS,1,1)) = 'A';

BEGIN

	FOR v_PSE_IS_BUG IN c_PSE_IS_BUG LOOP
		g_PSE_IS_BUG_CACHE(v_PSE_IS_BUG.PSE_ID) := v_PSE_IS_BUG.PSE_IS_BACKUP_GENERATION;
	END LOOP;

END CACHE_PSE_IS_BUG;
----------------------------------------------------------------------------------------------------
PROCEDURE CACHE_IS_WHOLESALE AS

-- Cache the ESP Supply Type by ESP ID..

CURSOR c_IS_WHOLESALE IS
	SELECT SERVICE_POINT_ID
	FROM SERVICE_POINT
	WHERE UPPER(SUBSTR(SERVICE_POINT_TYPE,1,1)) = 'W';

BEGIN

	FOR v_IS_WHOLESALE IN c_IS_WHOLESALE LOOP
		g_IS_WHOLESALE_CACHE(v_IS_WHOLESALE.SERVICE_POINT_ID) := 1;
	END LOOP;

END CACHE_IS_WHOLESALE;
----------------------------------------------------------------------------------------------------
PROCEDURE CACHE_IS_AGGREGATE_POOL AS

CURSOR c_IS_AGGREGATE_POOL IS
	SELECT POOL_ID
	FROM POOL
	WHERE UPPER(SUBSTR(POOL_STATUS,1,1)) = 'A'
		AND UPPER(POOL_CATEGORY) = 'AGGREGATE POOL';

BEGIN

	FOR v_IS_AGGREGATE_POOL IN c_IS_AGGREGATE_POOL LOOP
		g_IS_AGGREGATE_POOL_CACHE(v_IS_AGGREGATE_POOL.POOL_ID) := 1;
	END LOOP;

END CACHE_IS_AGGREGATE_POOL;
----------------------------------------------------------------------------------------------------
PROCEDURE INITIALIZE_CACHE AS

BEGIN

	CACHE_ESP_SUPPLY_TYPE;
	CACHE_PSE_IS_BUG;
	CACHE_IS_WHOLESALE;
	CACHE_IS_AGGREGATE_POOL;

END INITIALIZE_CACHE;
----------------------------------------------------------------------------------------------------
PROCEDURE RELEASE_CACHE AS

BEGIN

	g_ESP_SUPPLY_TYPE_CACHE.DELETE;
	g_PSE_IS_BUG_CACHE.DELETE;
	g_IS_WHOLESALE_CACHE.DELETE;
	g_IS_AGGREGATE_POOL_CACHE.DELETE;

END RELEASE_CACHE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_AS_OF_DATE
	(
	p_SERVICE_CODE IN CHAR,
	p_AS_OF_DATE IN DATE
	) RETURN DATE IS

BEGIN

	IF (p_SERVICE_CODE = GA.FORECAST_SERVICE AND GA.VERSION_FORECAST)
	OR (p_SERVICE_CODE = GA.BACKCAST_SERVICE AND GA.VERSION_BACKCAST)
	OR (p_SERVICE_CODE = GA.ACTUAL_SERVICE AND GA.VERSION_ACTUAL) THEN
		RETURN p_AS_OF_DATE;
	ELSE
		RETURN LOW_DATE;
	END IF;

END GET_SERVICE_AS_OF_DATE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ESP_SUPPLY_TYPE
	(
	p_ESP_ID IN NUMBER
	) RETURN CHAR IS

-- Answer the EDC Supply Type For the specified ESP Id.

BEGIN

	IF g_ESP_SUPPLY_TYPE_CACHE.COUNT = 0 THEN
		CACHE_ESP_SUPPLY_TYPE;
	END IF;

	IF g_ESP_SUPPLY_TYPE_CACHE.EXISTS(p_ESP_ID) THEN
		RETURN g_ESP_SUPPLY_TYPE_CACHE(p_ESP_ID);
	ELSE
		RETURN '?';
	END IF;

END GET_ESP_SUPPLY_TYPE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_PSE_IS_BUG
	(
	p_PSE_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the PSE Backup Generation for the specified PSE Id;

BEGIN

	IF g_PSE_IS_BUG_CACHE.COUNT = 0 THEN
		CACHE_PSE_IS_BUG;
	END IF;

	IF g_PSE_IS_BUG_CACHE.EXISTS(p_PSE_ID) THEN
		RETURN g_PSE_IS_BUG_CACHE(p_PSE_ID);
	ELSE
		RETURN CONSTANTS.NOT_ASSIGNED;
	END IF;

END GET_PSE_IS_BUG;
----------------------------------------------------------------------------------------------------
FUNCTION GET_IS_WHOLESALE
	(
	p_SERVICE_POINT_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the Is Wholsale attribute setting for the specified Service Point.

BEGIN

	IF g_IS_WHOLESALE_CACHE.COUNT = 0 THEN
		CACHE_IS_WHOLESALE;
	END IF;

	IF g_IS_WHOLESALE_CACHE.EXISTS(p_SERVICE_POINT_ID) THEN
		RETURN 1;
	ELSE
		RETURN 0;
	END IF;

END GET_IS_WHOLESALE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_IS_AGGREGATE_POOL
	(
	p_POOL_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the Is Wholsale attribute setting for the specified Service Point.

BEGIN

	IF g_IS_AGGREGATE_POOL_CACHE.COUNT = 0 THEN
		CACHE_IS_AGGREGATE_POOL;
	END IF;

	IF g_IS_AGGREGATE_POOL_CACHE.EXISTS(p_POOL_ID) THEN
		RETURN 1;
	ELSE
		RETURN 0;
	END IF;

END GET_IS_AGGREGATE_POOL;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_UFE_OVERRIDE
	(
	p_ACCOUNT_ID IN NUMBER,
	p_UFE_CODE IN CHAR,
	p_UFE_REQUESTOR IN VARCHAR
	) RETURN NUMBER IS

-- Answer the account ufe participation
-- Answer 0 when there is not an entry

v_IS_UFE_PARTICIPANT NUMBER := 0;

BEGIN

	SELECT IS_UFE_PARTICIPANT
	INTO v_IS_UFE_PARTICIPANT
	FROM ACCOUNT_UFE_PARTICIPATION
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
		AND UFE_CODE = UPPER(SUBSTR(p_UFE_CODE,1,1))
		AND UFE_REQUESTOR = UPPER(p_UFE_REQUESTOR);

	RETURN v_IS_UFE_PARTICIPANT;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ACCOUNT_UFE_OVERRIDE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_IS_UFE_PARTICIPANT
	(
	p_ACCOUNT_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the account ufe participation
-- Answer 0 when there is not an entry

v_IS_UFE_PARTICIPANT NUMBER := 0;

BEGIN

	SELECT IS_UFE_PARTICIPANT
	INTO v_IS_UFE_PARTICIPANT
	FROM ACCOUNT
	WHERE ACCOUNT_ID = p_ACCOUNT_ID;

	RETURN v_IS_UFE_PARTICIPANT;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ACCOUNT_IS_UFE_PARTICIPANT;
----------------------------------------------------------------------------------------------------
FUNCTION GET_USAGE_FACTOR
	(
	p_ACCOUNT_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the account or meter assigned usage factor for the specified day
-- Answer 0.0 when there is not an assigned usage factor

v_FACTOR_VAL NUMBER;

BEGIN

	IF p_METER_ID > 0 THEN
		SELECT FACTOR_VAL
		INTO v_FACTOR_VAL
		FROM METER_USAGE_FACTOR
		WHERE METER_ID = p_METER_ID
			AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
	ELSE
		SELECT FACTOR_VAL
		INTO v_FACTOR_VAL
		FROM ACCOUNT_USAGE_FACTOR
		WHERE ACCOUNT_ID = p_ACCOUNT_ID
			AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
	END IF;

	RETURN v_FACTOR_VAL;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		IF GA.USAGE_FACTOR_PER_UNIT_OPTION THEN
		    RETURN 0;
		ELSE
		    RETURN 1;
		END IF;

END GET_USAGE_FACTOR;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_SERVICE_ID
   (
   p_ACCOUNT_ID IN NUMBER,
   p_SERVICE_LOCATION_ID IN NUMBER,
   p_METER_ID IN NUMBER,
   p_AGGREGATE_ID IN NUMBER
   ) RETURN NUMBER IS
-- Answer the Service Id for the account service for the specified date.
v_ACCOUNT_SERVICE_ID NUMBER;
BEGIN
   SELECT ACCOUNT_SERVICE_ID
   INTO v_ACCOUNT_SERVICE_ID
   FROM ACCOUNT_SERVICE
   WHERE ACCOUNT_ID = p_ACCOUNT_ID
      AND SERVICE_LOCATION_ID = p_SERVICE_LOCATION_ID
      AND METER_ID = p_METER_ID
      AND AGGREGATE_ID = p_AGGREGATE_ID;
   RETURN v_ACCOUNT_SERVICE_ID;
EXCEPTION
   WHEN NO_DATA_FOUND THEN
      RETURN CONSTANTS.NOT_ASSIGNED;
END GET_ACCOUNT_SERVICE_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_EDC_SYSTEM_LOAD_ID
	(
	p_EDC_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the Service Id for the account service for the specified date.

v_EDC_SYSTEM_LOAD_ID NUMBER;

BEGIN

	SELECT NVL(EDC_SYSTEM_LOAD_ID,0)
	INTO v_EDC_SYSTEM_LOAD_ID
	FROM ENERGY_DISTRIBUTION_COMPANY
	WHERE EDC_ID = p_EDC_ID;

	RETURN v_EDC_SYSTEM_LOAD_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_EDC_SYSTEM_LOAD_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SYSTEM_LOAD_NAME
	(
	p_SYSTEM_LOAD_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the sytem load id for the system load.

v_SYSTEM_LOAD_NAME VARCHAR(32);

BEGIN

	SELECT SYSTEM_LOAD_NAME
	INTO v_SYSTEM_LOAD_NAME
	FROM SYSTEM_LOAD
	WHERE SYSTEM_LOAD_ID = p_SYSTEM_LOAD_ID;

	RETURN v_SYSTEM_LOAD_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN 'NA';

END GET_SYSTEM_LOAD_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_METER_TYPE
	(
	p_ACCOUNT_ID IN NUMBER,
	p_METER_ID IN NUMBER
	) RETURN CHAR IS

v_METER_TYPE CHAR(1);

BEGIN

	IF p_METER_ID = 0 THEN
		SELECT UPPER(SUBSTR(ACCOUNT_METER_TYPE,1,1))
		INTO v_METER_TYPE
		FROM ACCOUNT
		WHERE ACCOUNT_ID = p_ACCOUNT_ID;
	ELSE
		SELECT UPPER(SUBSTR(METER_TYPE,1,1))
		INTO v_METER_TYPE
		FROM METER
		WHERE METER_ID = p_METER_ID;
	END IF;

	RETURN v_METER_TYPE;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '?';

END GET_METER_TYPE;
----------------------------------------------------------------------------------------------------

FUNCTION GET_METER_MODEL_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_METER_ID IN NUMBER := NULL
	) RETURN NUMBER IS

v_METER_MODEL_ID NUMBER(1);

BEGIN
	-- 7/27/2010 - PBM - Removed the MODEL_ID from the METER table to prevent inconsistancy. MODEL_ID is determined by the METER's ACCOUNT.
	ASSERT(p_ACCOUNT_ID IS NOT NULL, 'ACCOUNT_ID is invalid. It must be non-null. The METER''s MODEL_ID is determined by the ACCOUNT.');
	ASSERT(p_ACCOUNT_ID > 0, 'ACCOUNT_ID is invalid. It must be > 0. The METER''s MODEL_ID is determined by the ACCOUNT.');

	SELECT MODEL_ID
    INTO v_METER_MODEL_ID
    FROM ACCOUNT
    WHERE ACCOUNT_ID = p_ACCOUNT_ID;

	RETURN v_METER_MODEL_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN 0;

END GET_METER_MODEL_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_POINT_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_LOCATION_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the account service point id assignment for the specified day
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an assigned service location

v_SERVICE_POINT_ID NUMBER;

BEGIN

	IF GA.ENABLE_SINGLE_TX_SERVICE_POINT THEN
		v_SERVICE_POINT_ID := GA.SINGLE_TX_SERVICE_POINT_ID;
	ELSE
		SELECT B.SERVICE_POINT_ID
		INTO v_SERVICE_POINT_ID
		FROM ACCOUNT_SERVICE_LOCATION A, SERVICE_LOCATION B
		WHERE A.ACCOUNT_ID = p_ACCOUNT_ID
			AND A.SERVICE_LOCATION_ID = p_SERVICE_LOCATION_ID
			AND p_SERVICE_DATE BETWEEN A.BEGIN_DATE AND NVL(A.END_DATE, p_SERVICE_DATE)
			AND B.SERVICE_LOCATION_ID = A.SERVICE_LOCATION_ID;
	END IF;

	RETURN v_SERVICE_POINT_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_POINT_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_ZONE_ID
	(
	p_SERVICE_POINT_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the service zone id assignment for the specified service point id.
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an assigned service zone

v_SERVICE_ZONE_ID NUMBER;

BEGIN

	SELECT NVL(SERVICE_ZONE_ID, CONSTANTS.NOT_ASSIGNED)
	INTO v_SERVICE_ZONE_ID
	FROM SERVICE_POINT
	WHERE SERVICE_POINT_ID = p_SERVICE_POINT_ID;

	RETURN v_SERVICE_ZONE_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_ZONE_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE JOBS_IN_QUEUE
	(
	p_REQUEST_TYPE IN VARCHAR,
	p_STATUS OUT NUMBER,
	p_CURSOR IN OUT GA.REFCURSOR
	) AS

-- Answer the jobs in the queue for cast requests

v_WHAT VARCHAR(64);

BEGIN

	p_STATUS := GA.SUCCESS;
	SELECT DECODE(UPPER(SUBSTR(p_REQUEST_TYPE,1,1)),
		'F',	'FS.CAST_SERVICE_REQUEST(''F''',
		'B',	'FS.CAST_SERVICE_REQUEST(''B''',
		'A',	'FS.CAST_SERVICE_REQUEST(''A''',
		'U',	'FS.CAST_SERVICE_REQUEST(''U''',
		'P',	'PF.USAGE_WRF_REQUEST(',
		UPPER(SUBSTR(p_REQUEST_TYPE,1,1)),	'FS.CAST_REQUEST(''' || UPPER(SUBSTR(p_REQUEST_TYPE,1,1)) || '''',
		NULL) INTO v_WHAT FROM DUAL;
	OPEN p_CURSOR FOR
		SELECT DISTINCT JOB, WHAT, NEXT_DATE
		FROM USER_JOBS
		WHERE SUBSTR(WHAT,1,LENGTH(v_WHAT)) = v_WHAT
		ORDER BY 1;

END JOBS_IN_QUEUE;
----------------------------------------------------------------------------------------------------
PROCEDURE REMOVE_JOB
	(
	p_JOB IN BINARY_INTEGER,
	p_STATUS OUT NUMBER
	) AS

-- Remove a job from the job queue

BEGIN

	p_STATUS := GA.SUCCESS;
	DBMS_JOB.REMOVE(p_JOB);

END REMOVE_JOB;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_AREA_LOAD
	(
	p_AREA_LOAD IN AREA_LOAD%ROWTYPE,
	p_STATUS OUT NUMBER
	) AS

-- Update or insert an Area Load.

v_AS_OF_DATE DATE := LOW_DATE;
v_INTERVAL_ABBR VARCHAR(2);
BEGIN


	p_STATUS := GA.SUCCESS;

	IF GA.VERSION_AREA_LOAD THEN
		SELECT MAX(AS_OF_DATE)
		INTO v_AS_OF_DATE
		FROM AREA_LOAD
		WHERE AREA_ID = p_AREA_LOAD.AREA_ID
			AND CASE_ID = p_AREA_LOAD.CASE_ID
			AND LOAD_CODE = p_AREA_LOAD.LOAD_CODE
			AND LOAD_DATE = p_AREA_LOAD.LOAD_DATE
			AND AS_OF_DATE <= p_AREA_LOAD.AS_OF_DATE;
	END IF;

	SELECT DECODE(AREA_INTERVAL,'Hour', 'HH',
			 								   'Day', 'DD', 'MI')
	INTO v_INTERVAL_ABBR
	FROM AREA
	WHERE AREA_ID = p_AREA_LOAD.AREA_ID;

	UPDATE AREA_LOAD
	SET LOAD_VAL = p_AREA_LOAD.LOAD_VAL
	WHERE CASE_ID = p_AREA_LOAD.CASE_ID
		AND AREA_ID = p_AREA_LOAD.AREA_ID
		AND LOAD_CODE = p_AREA_LOAD.LOAD_CODE
		AND LOAD_DATE = TRUNC(p_AREA_LOAD.LOAD_DATE, v_INTERVAL_ABBR)
		AND AS_OF_DATE = v_AS_OF_DATE;

	IF SQL%NOTFOUND THEN
		INSERT INTO AREA_LOAD (
			CASE_ID,
			AREA_ID,
			LOAD_CODE,
			LOAD_DATE,
			AS_OF_DATE,
			LOAD_VAL)
		VALUES (
			p_AREA_LOAD.CASE_ID,
			p_AREA_LOAD.AREA_ID,
			p_AREA_LOAD.LOAD_CODE,
			TRUNC(p_AREA_LOAD.LOAD_DATE, v_INTERVAL_ABBR),
			v_AS_OF_DATE,
			p_AREA_LOAD.LOAD_VAL);
	END IF;

END PUT_AREA_LOAD;
--------------------------------------------------------------------------------------------------------------
FUNCTION GET_PROVIDER_SERVICE
	(
	p_PROVIDER_SERVICE_ID IN NUMBER
	) RETURN PROVIDER_SERVICE%ROWTYPE IS

-- Answer the provider service associated with the id
-- Answer NULL when there is not an account.

v_PROVIDER_SERVICE PROVIDER_SERVICE%ROWTYPE;

BEGIN

	SELECT *
	INTO v_PROVIDER_SERVICE
	FROM PROVIDER_SERVICE
	WHERE PROVIDER_SERVICE_ID = p_PROVIDER_SERVICE_ID;

	RETURN v_PROVIDER_SERVICE;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN NULL;

END GET_PROVIDER_SERVICE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_SERVICE
	(
	p_ACCOUNT_SERVICE_ID IN NUMBER
	) RETURN ACCOUNT_SERVICE%ROWTYPE IS

-- Answer the account service associated with the id
-- Answer NULL when there is not an account.

v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE;

BEGIN

	SELECT *
	INTO v_ACCOUNT_SERVICE
	FROM ACCOUNT_SERVICE
	WHERE ACCOUNT_SERVICE_ID = p_ACCOUNT_SERVICE_ID;

	RETURN v_ACCOUNT_SERVICE;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN NULL;

END GET_ACCOUNT_SERVICE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ESP_ID
	(
	p_ESP_NAME IN VARCHAR,
	p_IDENT_TYPE IN CHAR DEFAULT 'N'
	) RETURN NUMBER IS

-- Answer the esp id assignment for the specified esp name
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an account.

v_ESP_ID NUMBER;

BEGIN

	IF p_IDENT_TYPE ='N' THEN
		SELECT ESP_ID INTO v_ESP_ID FROM ENERGY_SERVICE_PROVIDER
		WHERE ESP_NAME = p_ESP_NAME
			AND UPPER(SUBSTR(ESP_STATUS,1,1)) = 'A';
	ELSIF p_IDENT_TYPE = 'X' THEN
		SELECT ESP_ID INTO v_ESP_ID FROM ENERGY_SERVICE_PROVIDER
		WHERE ESP_EXTERNAL_IDENTIFIER = p_ESP_NAME
			AND UPPER(SUBSTR(ESP_STATUS,1,1)) = 'A';
	ELSIF p_IDENT_TYPE = 'D' THEN
		SELECT ESP_ID INTO v_ESP_ID FROM ENERGY_SERVICE_PROVIDER
		WHERE ESP_DUNS_NUMBER = p_ESP_NAME
			AND UPPER(SUBSTR(ESP_STATUS,1,1)) = 'A';
	END IF;

	RETURN v_ESP_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ESP_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ESP_OF_RECORD
	(
	p_ACCOUNT_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the ESP id assignment for the specified day and aggregate account.
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an assignment.

v_ESP_ID NUMBER;

BEGIN

	SELECT ESP_ID
	INTO v_ESP_ID
	FROM AGGREGATE_ACCOUNT_ESP
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
		AND AGGREGATE_ID = p_AGGREGATE_ID
		AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);

	RETURN v_ESP_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ESP_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ESP_OF_RECORD
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the ESP assignment of record for an account for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned ESP.
-- This function supports the "Account" model option.

v_ESP_ID NUMBER;
v_SERVICE_DATE DATE;

BEGIN

    v_SERVICE_DATE := p_SERVICE_DATE;
    IF GA.CSB_IS_SUBDAILY THEN
        v_SERVICE_DATE := v_SERVICE_DATE + 1/86400;
    END IF;

	SELECT ESP_ID
	INTO v_ESP_ID
	FROM ACCOUNT_ESP
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
      AND v_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, v_SERVICE_DATE);

	RETURN v_ESP_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ESP_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_EDC_OF_RECORD
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the EDC assignment of record for an account for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned EDC.

v_EDC_ID NUMBER;

BEGIN

	SELECT EDC_ID
	INTO v_EDC_ID
	FROM ACCOUNT_EDC
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
		AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);

	RETURN v_EDC_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_EDC_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_PSE_OF_RECORD
	(
	p_ESP_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the PSE assignment of record for an ESP for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned PSE.

v_PSE_ID NUMBER := CONSTANTS.NOT_ASSIGNED;

BEGIN

	IF GA.ENABLE_PSE_ESP_ASSIGNMENT THEN
		SELECT PSE_ID
		INTO v_PSE_ID
		FROM PSE_ESP
		WHERE ESP_ID = p_ESP_ID
			AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
	END IF;

	RETURN v_PSE_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_PSE_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_POOL_OF_RECORD
	(
	p_ACCOUNT_ID IN NUMBER,
	p_ESP_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_IS_AGGREGATE_ACCOUNT IN BOOLEAN DEFAULT FALSE
	) RETURN NUMBER IS

-- Answer the Pool assignment of record for an ESP for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned Pool.

v_POOL_ID NUMBER := CONSTANTS.NOT_ASSIGNED;
v_SERVICE_DATE DATE;

BEGIN

    v_SERVICE_DATE := p_SERVICE_DATE;
    IF GA.CSB_IS_SUBDAILY THEN
        v_SERVICE_DATE := v_SERVICE_DATE + 1/86400;
    END IF;

	IF GA.ENABLE_ESP_POOL_ASSIGNMENT THEN
		IF p_IS_AGGREGATE_ACCOUNT THEN
			SELECT POOL_ID
			INTO v_POOL_ID
			FROM AGGREGATE_ACCOUNT_ESP
			WHERE ACCOUNT_ID = p_ACCOUNT_ID
				AND ESP_ID = p_ESP_ID
				AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
		ELSE
			SELECT POOL_ID
			INTO v_POOL_ID
			FROM ACCOUNT_ESP
			WHERE ACCOUNT_ID = p_ACCOUNT_ID
				AND ESP_ID = p_ESP_ID
				AND v_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, v_SERVICE_DATE);
		END IF;
	END IF;

	RETURN v_POOL_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_POOL_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_AGGREGATE_POOL_OF_RECORD
	(
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the Pool assignment of record for an ESP for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned Pool.

v_POOL_ID NUMBER := CONSTANTS.NOT_ASSIGNED;

BEGIN

	IF GA.ENABLE_ESP_POOL_ASSIGNMENT THEN
		SELECT POOL_ID
		INTO v_POOL_ID
		FROM AGGREGATE_ACCOUNT_ESP
		WHERE AGGREGATE_ID = p_AGGREGATE_ID
			AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
	END IF;

	RETURN v_POOL_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_AGGREGATE_POOL_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_PSE_OF_RECORD
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the PSE assignment of record for an Account for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned PSE.

v_PSE_ID NUMBER;

BEGIN

	SELECT PSE_ID
	INTO v_PSE_ID
	FROM PSE_ESP
	WHERE ESP_ID =
		(SELECT ESP_ID
		FROM ACCOUNT_ESP
		WHERE ACCOUNT_ID = p_ACCOUNT_ID
			AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE))
		AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);

	RETURN v_PSE_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ACCOUNT_PSE_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_EDC_SC_OF_RECORD
	(
	p_EDC_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the SC assignment of record for an EDC for the service date;
-- otherwise answer the not_unassigned flag when there is not an assigned SC.

v_SC_ID NUMBER;

BEGIN

	SELECT EDC_SC_ID
	INTO v_SC_ID
	FROM ENERGY_DISTRIBUTION_COMPANY
	WHERE EDC_ID = p_EDC_ID;

	RETURN v_SC_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_EDC_SC_OF_RECORD;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_TYPE_ID
	(
	p_ACCOUNT_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the internal id of the specified account.

v_SERVICE_TYPE_ID NUMBER;

BEGIN

	SELECT NVL(TX_SERVICE_TYPE_ID, 0)
	INTO v_SERVICE_TYPE_ID
	FROM ACCOUNT
	WHERE ACCOUNT_ID = p_ACCOUNT_ID;

	RETURN v_SERVICE_TYPE_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_TYPE_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SCHEDULE_GROUP_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the internal id of the account or meter schedule group.

v_SCHEDULE_GROUP_ID NUMBER := CONSTANTS.NOT_ASSIGNED;

BEGIN

	IF GA.ENABLE_SCHED_GROUP_ASSIGNMENT THEN
		IF p_METER_ID = CONSTANTS.NOT_ASSIGNED THEN
			SELECT SCHEDULE_GROUP_ID
			INTO v_SCHEDULE_GROUP_ID
			FROM ACCOUNT_SCHEDULE_GROUP
			WHERE ACCOUNT_ID = p_ACCOUNT_ID
				AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
		ELSE
			SELECT SCHEDULE_GROUP_ID
			INTO v_SCHEDULE_GROUP_ID
			FROM METER_SCHEDULE_GROUP
			WHERE METER_ID = p_METER_ID
				AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE);
		END IF;
	END IF;

	RETURN v_SCHEDULE_GROUP_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SCHEDULE_GROUP_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SC_ID
	(
	p_EDC_ID IN NUMBER
	) RETURN NUMBER IS

-- Answer the SC assignment of record for an EDC;
-- otherwise answer the not_unassigned flag when there is not an assigned SC.

v_SC_ID NUMBER;

BEGIN

	SELECT EDC_SC_ID
	INTO v_SC_ID
	FROM ENERGY_DISTRIBUTION_COMPANY
	WHERE EDC_ID = p_EDC_ID;

	RETURN v_SC_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SC_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_ID
	(
	p_ACCOUNT_IDENT IN VARCHAR,
	p_IDENT_TYPE IN CHAR DEFAULT 'N'
	) RETURN NUMBER IS

-- Answer the account id assignment for the specified account name
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an account.

v_ACCOUNT_ID NUMBER;

BEGIN

	IF p_IDENT_TYPE ='N' THEN
		SELECT ACCOUNT_ID INTO v_ACCOUNT_ID FROM ACCOUNT
		WHERE ACCOUNT_NAME = LTRIM(RTRIM(p_ACCOUNT_IDENT));
	ELSIF p_IDENT_TYPE = 'X' THEN
		SELECT ACCOUNT_ID INTO v_ACCOUNT_ID FROM ACCOUNT
		WHERE ACCOUNT_EXTERNAL_IDENTIFIER = LTRIM(RTRIM(p_ACCOUNT_IDENT));
	ELSIF p_IDENT_TYPE = 'D' THEN
		SELECT ACCOUNT_ID INTO v_ACCOUNT_ID FROM ACCOUNT
		WHERE ACCOUNT_DUNS_NUMBER = LTRIM(RTRIM(p_ACCOUNT_IDENT));
	END IF;

	RETURN v_ACCOUNT_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_ACCOUNT_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_LOCATION_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

-- Answer the account service location id assignment for the specified day
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an assigned service location

v_SERVICE_LOCATION_ID NUMBER;

BEGIN

	SELECT SERVICE_LOCATION_ID
	INTO v_SERVICE_LOCATION_ID
	FROM ACCOUNT_SERVICE_LOCATION
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
		AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE)
		AND ROWNUM = 1;

	RETURN v_SERVICE_LOCATION_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_LOCATION_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_METER_NAME
	(
	p_METER_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the meter name of the associated meter id.

v_METER_NAME METER.METER_NAME%TYPE;

BEGIN

	SELECT METER_NAME
	INTO v_METER_NAME
	FROM METER
	WHERE METER_ID = p_METER_ID;

	RETURN v_METER_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_METER_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ACCOUNT_NAME
	(
	p_ACCOUNT_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the ACCOUNT name of the associated ACCOUNT id.

v_ACCOUNT_NAME ACCOUNT.ACCOUNT_NAME%TYPE;

BEGIN

	SELECT ACCOUNT_NAME
	INTO v_ACCOUNT_NAME
	FROM ACCOUNT
	WHERE ACCOUNT_ID = p_ACCOUNT_ID;

	RETURN v_ACCOUNT_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_ACCOUNT_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_AGGREGATE_ACCOUNT_NAME
	(
	p_AGGREGATE_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the ACCOUNT name of the associated AGGREGATE Id.

v_ACCOUNT_NAME ACCOUNT.ACCOUNT_NAME%TYPE;

BEGIN

	SELECT ACCOUNT_NAME
	INTO v_ACCOUNT_NAME
	FROM AGGREGATE_ACCOUNT_ESP A, ACCOUNT B
	WHERE A.AGGREGATE_ID = p_AGGREGATE_ID
		AND B.ACCOUNT_ID = A.ACCOUNT_ID;

	RETURN v_ACCOUNT_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_AGGREGATE_ACCOUNT_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_EDC_NAME
	(
	p_EDC_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the EDC name of the associated esp id.

v_EDC_NAME ENERGY_DISTRIBUTION_COMPANY.EDC_NAME%TYPE;

BEGIN

	SELECT EDC_NAME
	INTO v_EDC_NAME
	FROM ENERGY_DISTRIBUTION_COMPANY
	WHERE EDC_ID = p_EDC_ID;

	RETURN v_EDC_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_EDC_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_ESP_NAME
	(
	p_ESP_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the ESP name of the associated esp id.

v_ESP_NAME ENERGY_SERVICE_PROVIDER.ESP_NAME%TYPE;

BEGIN

	SELECT ESP_NAME
	INTO v_ESP_NAME
	FROM ENERGY_SERVICE_PROVIDER
	WHERE ESP_ID = p_ESP_ID;

	RETURN v_ESP_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_ESP_NAME;
----------------------------------------------------------------------------------------------------
FUNCTION GET_POOL_NAME
	(
	p_POOL_ID IN NUMBER
	) RETURN VARCHAR IS

-- Answer the POOL name of the associated pool id.

v_POOL_NAME POOL.POOL_NAME%TYPE;

BEGIN

	SELECT POOL_NAME
	INTO v_POOL_NAME
	FROM POOL
	WHERE POOL_ID = p_POOL_ID;

	RETURN v_POOL_NAME;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		RETURN '';

END GET_POOL_NAME;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_SERVICE_LOCATION_AND_POINT
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_SERVICE_LOCATION_ID OUT NUMBER,
	p_SERVICE_POINT_ID OUT NUMBER
	) AS

-- Answer the account service location id and service point id assignment for the specified day
-- Answer CONSTANTS.NOT_ASSIGNED when there is not an assigned service location

BEGIN

	SELECT A.SERVICE_LOCATION_ID, B.SERVICE_POINT_ID
	INTO p_SERVICE_LOCATION_ID, p_SERVICE_POINT_ID
	FROM ACCOUNT_SERVICE_LOCATION A,
		SERVICE_LOCATION B
	WHERE ACCOUNT_ID = p_ACCOUNT_ID
		AND p_SERVICE_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, p_SERVICE_DATE)
		AND B.SERVICE_LOCATION_ID = A.SERVICE_LOCATION_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		p_SERVICE_LOCATION_ID := CONSTANTS.NOT_ASSIGNED;
		p_SERVICE_POINT_ID := CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_LOCATION_AND_POINT;
----------------------------------------------------------------------------------------------------
FUNCTION PUT_ACCOUNT_SERVICE
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_LOCATION_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER
	) RETURN NUMBER AS
PRAGMA AUTONOMOUS_TRANSACTION;
v_ACCOUNT_SERVICE_ID ACCOUNT_SERVICE.ACCOUNT_SERVICE_ID%TYPE;
BEGIN
	SELECT OID.NEXTVAL INTO v_ACCOUNT_SERVICE_ID FROM DUAL;
	INSERT INTO ACCOUNT_SERVICE (
		ACCOUNT_ID,
		SERVICE_LOCATION_ID,
		METER_ID,
		AGGREGATE_ID,
		ACCOUNT_SERVICE_ID,
		ENTRY_DATE)
	VALUES (
		p_ACCOUNT_ID,
		p_SERVICE_LOCATION_ID,
		p_METER_ID,
		p_AGGREGATE_ID,
		v_ACCOUNT_SERVICE_ID,
		SYSDATE);
    COMMIT;
    RETURN v_ACCOUNT_SERVICE_ID;
END PUT_ACCOUNT_SERVICE;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_PROVIDER_SERVICE
	(
	p_EDC_ID IN NUMBER,
	p_ESP_ID IN NUMBER,
	p_PSE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID IN NUMBER
	) AS
PRAGMA AUTONOMOUS_TRANSACTION;
BEGIN

	INSERT INTO PROVIDER_SERVICE (
		EDC_ID,
		ESP_ID,
		PSE_ID,
		PROVIDER_SERVICE_ID,
		ENTRY_DATE)
	VALUES (
		p_EDC_ID,
		p_ESP_ID,
		p_PSE_ID,
		p_PROVIDER_SERVICE_ID,
		SYSDATE);
    COMMIT;
END PUT_PROVIDER_SERVICE;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_PROVIDER_SERVICE_ID
	(
	p_EDC_ID IN NUMBER,
	p_ESP_ID IN NUMBER,
	p_PSE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID OUT NUMBER
	) AS

BEGIN

	p_PROVIDER_SERVICE_ID := GET_PROVIDER_SERVICE_ID(p_EDC_ID, p_ESP_ID, p_PSE_ID);
	IF p_PROVIDER_SERVICE_ID = CONSTANTS.NOT_ASSIGNED THEN
     BEGIN
		    SELECT OID.NEXTVAL INTO p_PROVIDER_SERVICE_ID FROM DUAL;
		    PUT_PROVIDER_SERVICE(p_EDC_ID, p_ESP_ID, p_PSE_ID, p_PROVIDER_SERVICE_ID);
     EXCEPTION
        WHEN DUP_VAL_ON_INDEX THEN
        ERRS.LOG_AND_CONTINUE('Creation Failed. Retry.', p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
        p_PROVIDER_SERVICE_ID := GET_PROVIDER_SERVICE_ID(p_EDC_ID, p_ESP_ID, p_PSE_ID);
     END;
  END IF;

END GET_PROVIDER_SERVICE_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_PROVIDER_SERVICE_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_PROVIDER_SERVICE_ID OUT NUMBER
	) AS

v_EDC_ID NUMBER;
v_ESP_ID NUMBER;
v_PSE_ID NUMBER;

BEGIN

	v_EDC_ID := GET_EDC_OF_RECORD(p_ACCOUNT_ID, p_SERVICE_DATE);
	IF p_AGGREGATE_ID > 0 THEN
		v_ESP_ID := GET_ESP_OF_RECORD(p_ACCOUNT_ID, p_AGGREGATE_ID, p_SERVICE_DATE);
	ELSE
		v_ESP_ID := GET_ESP_OF_RECORD(p_ACCOUNT_ID, p_SERVICE_DATE);
	END IF;
	v_PSE_ID := GET_PSE_OF_RECORD(v_ESP_ID, p_SERVICE_DATE);

	GET_PROVIDER_SERVICE_ID(v_EDC_ID, v_ESP_ID, v_PSE_ID, p_PROVIDER_SERVICE_ID);

END GET_PROVIDER_SERVICE_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_PROVIDER_SERVICE_ID
	(
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

v_PROVIDER_SERVICE_ID NUMBER(9);
v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE := GET_ACCOUNT_SERVICE(p_ACCOUNT_SERVICE_ID);

BEGIN

	GET_PROVIDER_SERVICE_ID(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.AGGREGATE_ID, p_SERVICE_DATE, v_PROVIDER_SERVICE_ID);

	RETURN v_PROVIDER_SERVICE_ID;

EXCEPTION
	WHEN OTHERS THEN
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_PROVIDER_SERVICE_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_ESP_PROVIDER_SERVICE_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_ESP_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_PROVIDER_SERVICE_ID OUT NUMBER
	) AS

v_EDC_ID NUMBER;
v_PSE_ID NUMBER;

BEGIN

	v_EDC_ID := GET_EDC_OF_RECORD(p_ACCOUNT_ID, p_SERVICE_DATE);
	v_PSE_ID := GET_PSE_OF_RECORD(p_ESP_ID, p_SERVICE_DATE);

	GET_PROVIDER_SERVICE_ID(v_EDC_ID, p_ESP_ID, v_PSE_ID, p_PROVIDER_SERVICE_ID);

END GET_ESP_PROVIDER_SERVICE_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_ACCOUNT_SERVICE_ID
	(
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_LOCATION_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER,
	p_ACCOUNT_SERVICE_ID OUT NUMBER
	) AS
BEGIN
    SELECT ACCOUNT_SERVICE_ID
    INTO p_ACCOUNT_SERVICE_ID
    FROM ACCOUNT_SERVICE
    WHERE ACCOUNT_ID = p_ACCOUNT_ID
        AND SERVICE_LOCATION_ID = p_SERVICE_LOCATION_ID
        AND METER_ID = p_METER_ID
        AND AGGREGATE_ID = p_AGGREGATE_ID;
EXCEPTION
    WHEN NO_DATA_FOUND THEN
         BEGIN
            p_ACCOUNT_SERVICE_ID := PUT_ACCOUNT_SERVICE(p_ACCOUNT_ID, p_SERVICE_LOCATION_ID, p_METER_ID, p_AGGREGATE_ID);
         EXCEPTION
            WHEN DUP_VAL_ON_INDEX THEN
                ERRS.LOG_AND_CONTINUE('Creation Failed. Retry.', p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
                GET_ACCOUNT_SERVICE_ID(p_ACCOUNT_ID, p_SERVICE_LOCATION_ID, p_METER_ID, p_AGGREGATE_ID,p_ACCOUNT_SERVICE_ID);
         END;
END GET_ACCOUNT_SERVICE_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_SERVICE_DELIVERY
	(
	p_SERVICE_DELIVERY_ID IN NUMBER,
	p_POOL_ID IN NUMBER,
	p_SERVICE_POINT_ID IN NUMBER,
	p_SERVICE_ZONE_ID IN NUMBER,
	p_SCHEDULE_GROUP_ID IN NUMBER,
	p_SC_ID IN NUMBER,
	p_SUPPLY_TYPE IN CHAR,
	p_IS_BUG IN NUMBER,
	p_IS_WHOLESALE IN NUMBER,
	p_IS_AGGREGATE_POOL IN NUMBER
	) AS
PRAGMA AUTONOMOUS_TRANSACTION;
BEGIN

	IF LOGS.IS_DEBUG_ENABLED THEN
		LOGS.LOG_DEBUG('PUT_SERVICE_DELIVERY');
	END IF;

	INSERT INTO SERVICE_DELIVERY(
		SERVICE_DELIVERY_ID,
		POOL_ID,
		SERVICE_POINT_ID,
		SERVICE_ZONE_ID,
		SCHEDULE_GROUP_ID,
		SC_ID,
		SUPPLY_TYPE,
		IS_BUG,
		IS_WHOLESALE,
		IS_AGGREGATE_POOL,
		ENTRY_DATE)
	VALUES(
		p_SERVICE_DELIVERY_ID,
		p_POOL_ID,
		p_SERVICE_POINT_ID,
		p_SERVICE_ZONE_ID,
		p_SCHEDULE_GROUP_ID,
		p_SC_ID,
		p_SUPPLY_TYPE,
		p_IS_BUG,
		p_IS_WHOLESALE,
		p_IS_AGGREGATE_POOL,
		SYSDATE);
COMMIT;
END PUT_SERVICE_DELIVERY;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_DELIVERY_ID
	(
	p_POOL_ID IN NUMBER,
	p_SERVICE_POINT_ID IN NUMBER,
	p_SERVICE_ZONE_ID IN NUMBER,
	p_SCHEDULE_GROUP_ID IN NUMBER,
	p_SC_ID IN NUMBER,
	p_SUPPLY_TYPE IN CHAR,
	p_IS_BUG IN NUMBER,
	p_IS_WHOLESALE IN NUMBER,
	p_IS_AGGREGATE_POOL IN NUMBER
	) RETURN NUMBER IS

-- Answer the Service Delivery Id for the service for the specified date.

v_SERVICE_DELIVERY_ID NUMBER;

BEGIN

	SELECT SERVICE_DELIVERY_ID
	INTO v_SERVICE_DELIVERY_ID
	FROM SERVICE_DELIVERY
	WHERE POOL_ID = p_POOL_ID
		AND SERVICE_POINT_ID = p_SERVICE_POINT_ID
		AND SERVICE_ZONE_ID = p_SERVICE_ZONE_ID
		AND SCHEDULE_GROUP_ID = p_SCHEDULE_GROUP_ID
		AND SC_ID = p_SC_ID
		AND SUPPLY_TYPE = p_SUPPLY_TYPE
		AND IS_BUG = p_IS_BUG
		AND IS_WHOLESALE = p_IS_WHOLESALE
		AND IS_AGGREGATE_POOL = p_IS_AGGREGATE_POOL;

	RETURN v_SERVICE_DELIVERY_ID;

EXCEPTION
	WHEN OTHERS THEN
		RETURN CONSTANTS.NOT_ASSIGNED;

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE SET_SERVICE_DELIVERY_ID
	(
	p_POOL_ID IN NUMBER,
	p_SERVICE_POINT_ID IN NUMBER,
	p_SERVICE_ZONE_ID IN NUMBER,
	p_SCHEDULE_GROUP_ID IN NUMBER,
	p_SC_ID IN NUMBER,
	p_SUPPLY_TYPE IN CHAR,
	p_IS_BUG IN NUMBER,
	p_IS_WHOLESALE IN NUMBER,
	p_IS_AGGREGATE_POOL IN NUMBER,
	p_SERVICE_DELIVERY_ID IN OUT NUMBER
	) AS

-- Set/Answer the Service Delivery Id for the service for the specified date.

BEGIN

	IF LOGS.IS_DEBUG_ENABLED THEN
		LOGS.LOG_DEBUG('SET_SERVICE_DELIVERY');
		LOGS.LOG_DEBUG('POOL_ID=' || TO_CHAR(p_POOL_ID));
		LOGS.LOG_DEBUG('SERVICE_POINT_ID==' || TO_CHAR(p_SERVICE_POINT_ID));
		LOGS.LOG_DEBUG('SERVICE_ZONE_ID=' || TO_CHAR(p_SERVICE_ZONE_ID));
		LOGS.LOG_DEBUG('SCHEDULE_GROUP_ID=' || TO_CHAR(p_SCHEDULE_GROUP_ID));
		LOGS.LOG_DEBUG('SC_ID=' || TO_CHAR(p_SC_ID));
		LOGS.LOG_DEBUG('SUPPLY_TYPE=' || p_SUPPLY_TYPE);
		LOGS.LOG_DEBUG('IS_BUG=' || TO_CHAR(p_IS_BUG));
		LOGS.LOG_DEBUG('IS_WHOLESALE=' || TO_CHAR(p_IS_WHOLESALE));
		LOGS.LOG_DEBUG('IS_AGGREGATE_POOL=' || TO_CHAR(p_IS_AGGREGATE_POOL));
	END IF;

p_SERVICE_DELIVERY_ID := GET_SERVICE_DELIVERY_ID(p_POOL_ID, p_SERVICE_POINT_ID, p_SERVICE_ZONE_ID, p_SCHEDULE_GROUP_ID, p_SC_ID, p_SUPPLY_TYPE, p_IS_BUG, p_IS_WHOLESALE, p_IS_AGGREGATE_POOL);

IF LOGS.IS_DEBUG_ENABLED THEN
	LOGS.LOG_DEBUG('SERVICE_DELIVERY_ID=' || TO_CHAR(p_SERVICE_DELIVERY_ID));
END IF;

IF p_SERVICE_DELIVERY_ID = CONSTANTS.NOT_ASSIGNED THEN
	BEGIN
	SELECT OID.NEXTVAL INTO p_SERVICE_DELIVERY_ID FROM DUAL;
	PUT_SERVICE_DELIVERY(p_SERVICE_DELIVERY_ID, p_POOL_ID, p_SERVICE_POINT_ID, p_SERVICE_ZONE_ID, p_SCHEDULE_GROUP_ID, p_SC_ID, p_SUPPLY_TYPE, p_IS_BUG, p_IS_WHOLESALE, p_IS_AGGREGATE_POOL);
	EXCEPTION
		WHEN DUP_VAL_ON_INDEX THEN
			ERRS.LOG_AND_CONTINUE('Creation Failed. Retry.', p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
			p_SERVICE_DELIVERY_ID := GET_SERVICE_DELIVERY_ID(p_POOL_ID, p_SERVICE_POINT_ID, p_SERVICE_ZONE_ID, p_SCHEDULE_GROUP_ID, p_SC_ID, p_SUPPLY_TYPE, p_IS_BUG, p_IS_WHOLESALE, p_IS_AGGREGATE_POOL);
	END;
END IF;

END SET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_SERVICE_DELIVERY_ID
	(
	p_ACCOUNT_SERVICE IN ACCOUNT_SERVICE%ROWTYPE,
	p_PROVIDER_SERVICE IN PROVIDER_SERVICE%ROWTYPE,
	p_SERVICE_DATE IN DATE,
	p_SERVICE_DELIVERY_ID IN OUT NUMBER
	) AS

-- Set/Answer the Service Delivery Id for the service for the specified date.

v_POOL_ID NUMBER(9);
v_SERVICE_POINT_ID NUMBER(9);
v_SERVICE_ZONE_ID NUMBER(9);
v_SCHEDULE_GROUP_ID NUMBER(9);
v_SC_ID NUMBER(9);
v_SUPPLY_TYPE CHAR(1);
v_IS_BUG NUMBER(1);
v_IS_WHOLESALE NUMBER(1);
v_IS_AGGREGATE_POOL NUMBER(1);

BEGIN

	IF LOGS.IS_DEBUG_ENABLED THEN
		LOGS.LOG_DEBUG('GET_SERVICE_DELIVERY_ID');
	END IF;

	IF p_ACCOUNT_SERVICE.AGGREGATE_ID > 0 THEN
		v_POOL_ID := GET_AGGREGATE_POOL_OF_RECORD(p_ACCOUNT_SERVICE.AGGREGATE_ID, p_SERVICE_DATE);
	ELSE
		v_POOL_ID := GET_POOL_OF_RECORD(p_ACCOUNT_SERVICE.ACCOUNT_ID, p_PROVIDER_SERVICE.ESP_ID, p_SERVICE_DATE);
	END IF;

	v_SERVICE_POINT_ID := GET_SERVICE_POINT_ID(p_ACCOUNT_SERVICE.ACCOUNT_ID, p_ACCOUNT_SERVICE.SERVICE_LOCATION_ID, p_SERVICE_DATE);
	v_SERVICE_ZONE_ID := GET_SERVICE_ZONE_ID(v_SERVICE_POINT_ID);
	v_SCHEDULE_GROUP_ID := GET_SCHEDULE_GROUP_ID(p_ACCOUNT_SERVICE.ACCOUNT_ID, p_ACCOUNT_SERVICE.METER_ID, p_SERVICE_DATE);
	v_SC_ID := GET_SC_ID(p_PROVIDER_SERVICE.EDC_ID);
	v_SUPPLY_TYPE := GET_ESP_SUPPLY_TYPE(p_PROVIDER_SERVICE.ESP_ID);
	v_IS_BUG := GET_PSE_IS_BUG(p_PROVIDER_SERVICE.PSE_ID);
	v_IS_WHOLESALE := GET_IS_WHOLESALE(v_SERVICE_POINT_ID);
	v_IS_AGGREGATE_POOL := GET_IS_AGGREGATE_POOL(v_POOL_ID);

	SET_SERVICE_DELIVERY_ID(v_POOL_ID, v_SERVICE_POINT_ID, v_SERVICE_ZONE_ID, v_SCHEDULE_GROUP_ID, v_SC_ID, v_SUPPLY_TYPE, v_IS_BUG, v_IS_WHOLESALE, v_IS_AGGREGATE_POOL, p_SERVICE_DELIVERY_ID);

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_SERVICE_DELIVERY_ID
	(
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_SERVICE_DELIVERY_ID IN OUT NUMBER
	) AS

-- Set/Answer the Service Delivery Id for the service for the specified date.

v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE := GET_ACCOUNT_SERVICE(p_ACCOUNT_SERVICE_ID);
v_PROVIDER_SERVICE PROVIDER_SERVICE%ROWTYPE := GET_PROVIDER_SERVICE(p_PROVIDER_SERVICE_ID);

BEGIN

	GET_SERVICE_DELIVERY_ID(v_ACCOUNT_SERVICE, v_PROVIDER_SERVICE, p_SERVICE_DATE, p_SERVICE_DELIVERY_ID);

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_DELIVERY_ID
	(
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE
	) RETURN NUMBER IS

v_SERVICE_DELIVERY_ID NUMBER(9);

BEGIN

	GET_SERVICE_DELIVERY_ID(p_ACCOUNT_SERVICE_ID, p_PROVIDER_SERVICE_ID, p_SERVICE_DATE, v_SERVICE_DELIVERY_ID);

	RETURN v_SERVICE_DELIVERY_ID;

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_SERVICE_DELIVERY_ID
	(
	p_ACCOUNT_SERVICE IN ACCOUNT_SERVICE%ROWTYPE,
	p_PROVIDER_SERVICE IN PROVIDER_SERVICE%ROWTYPE,
	p_POOL_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_SERVICE_DELIVERY_ID IN OUT NUMBER
	) AS

-- Set/Answer the Service Delivery Id for the service for the specified date.

v_POOL_ID NUMBER(9);
v_SERVICE_POINT_ID NUMBER(9);
v_SERVICE_ZONE_ID NUMBER(9);
v_SCHEDULE_GROUP_ID NUMBER(9);
v_SC_ID NUMBER(9);
v_SUPPLY_TYPE CHAR(1);
v_IS_BUG NUMBER(1);
v_IS_WHOLESALE NUMBER(1);
v_IS_AGGREGATE_POOL NUMBER(1);

BEGIN

	IF LOGS.IS_DEBUG_ENABLED THEN
		LOGS.LOG_DEBUG('GET_SERVICE_DELIVERY_ID');
	END IF;

    v_POOL_ID := p_POOL_ID;
	v_SERVICE_POINT_ID := GET_SERVICE_POINT_ID
(p_ACCOUNT_SERVICE.ACCOUNT_ID, p_ACCOUNT_SERVICE.SERVICE_LOCATION_ID,
p_SERVICE_DATE);
	v_SERVICE_ZONE_ID := GET_SERVICE_ZONE_ID(v_SERVICE_POINT_ID);
	v_SCHEDULE_GROUP_ID := GET_SCHEDULE_GROUP_ID
(p_ACCOUNT_SERVICE.ACCOUNT_ID, p_ACCOUNT_SERVICE.METER_ID, p_SERVICE_DATE);
	v_SC_ID := GET_SC_ID(p_PROVIDER_SERVICE.EDC_ID);
	v_SUPPLY_TYPE := GET_ESP_SUPPLY_TYPE(p_PROVIDER_SERVICE.ESP_ID);
	v_IS_BUG := GET_PSE_IS_BUG(p_PROVIDER_SERVICE.PSE_ID);
	v_IS_WHOLESALE := GET_IS_WHOLESALE(v_SERVICE_POINT_ID);
	v_IS_AGGREGATE_POOL := GET_IS_AGGREGATE_POOL(v_POOL_ID);

	SET_SERVICE_DELIVERY_ID(v_POOL_ID, v_SERVICE_POINT_ID,
v_SERVICE_ZONE_ID, v_SCHEDULE_GROUP_ID, v_SC_ID, v_SUPPLY_TYPE, v_IS_BUG,
v_IS_WHOLESALE, v_IS_AGGREGATE_POOL, p_SERVICE_DELIVERY_ID);

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_SERVICE_DELIVERY_ID
	(
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID IN NUMBER,
	p_POOL_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_SERVICE_DELIVERY_ID IN OUT NUMBER
	) AS

-- Set/Answer the Service Delivery Id for the service for the specified date.

v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE := GET_ACCOUNT_SERVICE
(p_ACCOUNT_SERVICE_ID);
v_PROVIDER_SERVICE PROVIDER_SERVICE%ROWTYPE := GET_PROVIDER_SERVICE
(p_PROVIDER_SERVICE_ID);

BEGIN

	GET_SERVICE_DELIVERY_ID(v_ACCOUNT_SERVICE,
v_PROVIDER_SERVICE,p_POOL_ID, p_SERVICE_DATE, p_SERVICE_DELIVERY_ID);

END GET_SERVICE_DELIVERY_ID;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_SERVICE
	(
	p_SERVICE_CODE IN CHAR,
	p_SERVICE IN SERVICE%ROWTYPE,
	p_SERVICE_ID OUT NUMBER
	) AS

v_AS_OF_DATE DATE := GET_SERVICE_AS_OF_DATE(p_SERVICE_CODE, p_SERVICE.AS_OF_DATE);

BEGIN

	SELECT SERVICE_ID
	INTO p_SERVICE_ID
	FROM SERVICE
 	WHERE MODEL_ID = p_SERVICE.MODEL_ID
		AND SCENARIO_ID = p_SERVICE.SCENARIO_ID
		AND AS_OF_DATE = v_AS_OF_DATE
		AND PROVIDER_SERVICE_ID = p_SERVICE.PROVIDER_SERVICE_ID
		AND ACCOUNT_SERVICE_ID = p_SERVICE.ACCOUNT_SERVICE_ID
		AND SERVICE_DELIVERY_ID = p_SERVICE.SERVICE_DELIVERY_ID;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		SELECT SID.NEXTVAL INTO p_SERVICE_ID FROM DUAL;
		INSERT INTO SERVICE (
			SERVICE_ID,
			MODEL_ID,
			SCENARIO_ID,
			AS_OF_DATE,
			PROVIDER_SERVICE_ID,
			ACCOUNT_SERVICE_ID,
			SERVICE_DELIVERY_ID,
			ENTRY_DATE)
		VALUES (
			p_SERVICE_ID,
			p_SERVICE.MODEL_ID,
			p_SERVICE.SCENARIO_ID,
			v_AS_OF_DATE,
			p_SERVICE.PROVIDER_SERVICE_ID,
			p_SERVICE.ACCOUNT_SERVICE_ID,
			p_SERVICE.SERVICE_DELIVERY_ID,
			SYSDATE);

END PUT_SERVICE;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_SERVICE_STATE
	(
	p_SERVICE_STATE IN SERVICE_STATE%ROWTYPE
	) AS

BEGIN

	UPDATE SERVICE_STATE
	SET BASIS_AS_OF_DATE = p_SERVICE_STATE.BASIS_AS_OF_DATE,
		IS_UFE_PARTICIPANT = p_SERVICE_STATE.IS_UFE_PARTICIPANT,
		SERVICE_ACCOUNTS = p_SERVICE_STATE.SERVICE_ACCOUNTS,
		METER_TYPE = p_SERVICE_STATE.METER_TYPE,
		IS_AGGREGATE_ACCOUNT = p_SERVICE_STATE.IS_AGGREGATE_ACCOUNT,
		PROFILE_TYPE = p_SERVICE_STATE.PROFILE_TYPE,
		PROFILE_SOURCE_DATE = p_SERVICE_STATE.PROFILE_SOURCE_DATE,
		PROFILE_ZERO_COUNT = p_SERVICE_STATE.PROFILE_ZERO_COUNT,
		USAGE_FACTOR = p_SERVICE_STATE.USAGE_FACTOR
 	WHERE SERVICE_ID = p_SERVICE_STATE.SERVICE_ID
		AND SERVICE_CODE = p_SERVICE_STATE.SERVICE_CODE
		AND SERVICE_DATE = TRUNC(p_SERVICE_STATE.SERVICE_DATE);

	IF SQL%NOTFOUND THEN
		INSERT INTO SERVICE_STATE (
			SERVICE_ID,
			SERVICE_CODE,
			SERVICE_DATE,
			BASIS_AS_OF_DATE,
			IS_UFE_PARTICIPANT,
			SERVICE_ACCOUNTS,
			METER_TYPE,
			IS_EXTERNAL_FORECAST,
			IS_AGGREGATE_ACCOUNT,
			IS_AGGREGATE_POOL,
			PROFILE_TYPE,
			PROFILE_SOURCE_DATE,
			PROFILE_ZERO_COUNT,
			USAGE_FACTOR,
			SERVICE_INTERVALS)
		VALUES (
			p_SERVICE_STATE.SERVICE_ID,
			p_SERVICE_STATE.SERVICE_CODE,
			TRUNC(p_SERVICE_STATE.SERVICE_DATE),
			NVL(p_SERVICE_STATE.BASIS_AS_OF_DATE, SYSDATE),
			p_SERVICE_STATE.IS_UFE_PARTICIPANT,
			p_SERVICE_STATE.SERVICE_ACCOUNTS,
			p_SERVICE_STATE.METER_TYPE,
			p_SERVICE_STATE.IS_EXTERNAL_FORECAST,
			p_SERVICE_STATE.IS_AGGREGATE_ACCOUNT,
			NVL(p_SERVICE_STATE.IS_AGGREGATE_POOL, 0),
			p_SERVICE_STATE.PROFILE_TYPE,
			p_SERVICE_STATE.PROFILE_SOURCE_DATE,
			p_SERVICE_STATE.PROFILE_ZERO_COUNT,
			p_SERVICE_STATE.USAGE_FACTOR,
			NVL(p_SERVICE_STATE.SERVICE_INTERVALS,1));
	END IF;

END PUT_SERVICE_STATE;
----------------------------------------------------------------------------------------------------
PROCEDURE PUT_PROCESS_STATUS
	(
	p_PROCESS_NAME IN VARCHAR,
	p_PROCESS_DATE IN DATE,
	p_AS_OF_DATE IN DATE,
	p_PROCESS_STATE IN VARCHAR,
	p_PROCESS_SYSDATE IN DATE
	) AS

BEGIN

	UPDATE PROCESS_STATUS
	SET PROCESS_STATE = p_PROCESS_STATE,
		PROCESS_SYSDATE = p_PROCESS_SYSDATE
	WHERE PROCESS_NAME = p_PROCESS_NAME
		AND PROCESS_DATE = p_PROCESS_DATE
		AND PROCESS_AS_OF_DATE = p_AS_OF_DATE;

	IF SQL%NOTFOUND THEN
		INSERT INTO PROCESS_STATUS (
			PROCESS_NAME,
			PROCESS_DATE,
			PROCESS_AS_OF_DATE,
			PROCESS_STATE,
			PROCESS_SYSDATE)
		VALUES (
			p_PROCESS_NAME,
			p_PROCESS_DATE,
			p_AS_OF_DATE,
			p_PROCESS_STATE,
			p_PROCESS_SYSDATE);
	END IF;

END PUT_PROCESS_STATUS;
----------------------------------------------------------------------------------------------------
PROCEDURE GET_AGGREGATE_ACCOUNT_SERVICE
	(
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_AS_OF_DATE IN DATE,
	p_SERVICE_ACCOUNTS OUT NUMBER,
	p_USAGE_FACTOR OUT NUMBER
	) AS

-- Answer the number of accounts in service and the usage factor for the specified day for an aggregate account.

v_CASE_ID NUMBER;

BEGIN


	--Must include CASE_ID in all queries where it is part of Primary Key to avoid very slow queries against large tables
	v_CASE_ID := GA.BASE_CASE_ID;    --Base_Case Only for now

	SELECT NVL(SERVICE_ACCOUNTS,0), USAGE_FACTOR
	INTO p_SERVICE_ACCOUNTS, p_USAGE_FACTOR
	FROM AGGREGATE_ACCOUNT_SERVICE A
	WHERE AGGREGATE_ID = p_AGGREGATE_ID
		AND CASE_ID = v_CASE_ID        --Query is much faster with CASE_ID specified
		AND SERVICE_DATE = p_SERVICE_DATE
		AND AS_OF_DATE =
			(SELECT MAX(AS_OF_DATE)
			FROM AGGREGATE_ACCOUNT_SERVICE
			WHERE AGGREGATE_ID = p_AGGREGATE_ID    --Don't use A.AGGREGATE_ID here - it is much slower when table is large - wjc
				AND CASE_ID = v_CASE_ID        --Query is much faster with CASE_ID specified in subquery and outer query!!!!
				AND SERVICE_DATE = A.SERVICE_DATE
				AND AS_OF_DATE <= p_AS_OF_DATE);

	IF p_USAGE_FACTOR IS NULL THEN
		IF GA.USAGE_FACTOR_PER_UNIT_OPTION THEN
		    p_USAGE_FACTOR := 0;
		ELSE
		    p_USAGE_FACTOR := 1;
		END IF;
	END IF;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		p_SERVICE_ACCOUNTS := 0;
		p_USAGE_FACTOR := 0;

END GET_AGGREGATE_ACCOUNT_SERVICE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_ID
	(
	p_MODEL_ID IN NUMBER,
	p_SCENARIO_ID IN NUMBER,
	p_AS_OF_DATE IN DATE,
	p_PROVIDER_SERVICE_ID IN NUMBER,
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_SERVICE_DELIVERY_ID IN NUMBER
	) RETURN NUMBER IS

v_SERVICE_ID SERVICE.SERVICE_ID%TYPE;

	FUNCTION LOOKUP_SERVICE_RECORD RETURN NUMBER AS
		v_SERVICE_ID_LOCAL SERVICE.SERVICE_ID%TYPE;
	BEGIN
		SELECT SERVICE_ID
		INTO v_SERVICE_ID_LOCAL
		FROM SERVICE
		WHERE MODEL_ID = p_MODEL_ID
		AND SCENARIO_ID = p_SCENARIO_ID
		AND AS_OF_DATE = p_AS_OF_DATE
		AND PROVIDER_SERVICE_ID = p_PROVIDER_SERVICE_ID
		AND ACCOUNT_SERVICE_ID = p_ACCOUNT_SERVICE_ID
		AND SERVICE_DELIVERY_ID = p_SERVICE_DELIVERY_ID;
		RETURN v_SERVICE_ID_LOCAL;
	END;

	PROCEDURE INSERT_SERVICE_RECORD AS
		PRAGMA AUTONOMOUS_TRANSACTION;
	BEGIN
		INSERT INTO SERVICE
			(SERVICE_ID,
			 MODEL_ID,
			 SCENARIO_ID,
			 AS_OF_DATE,
			 PROVIDER_SERVICE_ID,
			 ACCOUNT_SERVICE_ID,
			 SERVICE_DELIVERY_ID,
			 ENTRY_DATE)
		VALUES
			(V_SERVICE_ID,
			 P_MODEL_ID,
			 P_SCENARIO_ID,
			 P_AS_OF_DATE,
			 P_PROVIDER_SERVICE_ID,
			 P_ACCOUNT_SERVICE_ID,
			 P_SERVICE_DELIVERY_ID,
			 SYSDATE);
		COMMIT;
	END;

BEGIN
	BEGIN
		v_SERVICE_ID := LOOKUP_SERVICE_RECORD;
	EXCEPTION
		WHEN NO_DATA_FOUND THEN
			BEGIN
				ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
				SELECT SID.NEXTVAL INTO v_SERVICE_ID FROM DUAL;
				INSERT_SERVICE_RECORD();
			EXCEPTION
			  WHEN DUP_VAL_ON_INDEX THEN
				ERRS.LOG_AND_CONTINUE('Creation Failed. Retry.', p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
				v_SERVICE_ID := LOOKUP_SERVICE_RECORD;
			END;
		WHEN OTHERS THEN
			v_SERVICE_ID := CONSTANTS.NOT_ASSIGNED;
	END;
	RETURN v_SERVICE_ID;
END GET_SERVICE_ID;
----------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE
	(
	p_SERVICE_ID IN NUMBER
	) RETURN SERVICE%ROWTYPE IS

v_SERVICE SERVICE%ROWTYPE;

BEGIN

	SELECT * INTO v_SERVICE FROM SERVICE WHERE SERVICE_ID = p_SERVICE_ID;
	RETURN v_SERVICE;

EXCEPTION
	WHEN OTHERS THEN
		RETURN NULL;

END GET_SERVICE;
--------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_STATE
	(
	p_MODEL_ID IN NUMBER,
	p_SCENARIO_ID IN NUMBER,
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_LOCATION_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_CODE IN CHAR,
	p_SERVICE_DATE IN DATE,
	p_AS_OF_DATE IN DATE,
	p_APPLY_UFE_CODE IN NUMBER DEFAULT 0,
	p_ESP_ID IN NUMBER DEFAULT 0,
	p_REQUESTOR IN VARCHAR DEFAULT USER
	) RETURN SERVICE_STATE%ROWTYPE IS
BEGIN
	RETURN GET_SERVICE_STATE(p_MODEL_ID, p_SCENARIO_ID, p_ACCOUNT_ID, p_SERVICE_LOCATION_ID, p_METER_ID,
							 p_AGGREGATE_ID, p_SERVICE_CODE, p_SERVICE_DATE, p_AS_OF_DATE,
							 NULL, p_ESP_ID, NULL, NULL,
							 p_APPLY_UFE_CODE, p_REQUESTOR);
END GET_SERVICE_STATE;
--------------------------------------------------------------------------------------------------
FUNCTION GET_SERVICE_STATE
	(
	p_MODEL_ID IN NUMBER,
	p_SCENARIO_ID IN NUMBER,
	p_ACCOUNT_ID IN NUMBER,
	p_SERVICE_LOCATION_ID IN NUMBER,
	p_METER_ID IN NUMBER,
	p_AGGREGATE_ID IN NUMBER,
	p_SERVICE_CODE IN CHAR,
	p_SERVICE_DATE IN DATE,
	p_AS_OF_DATE IN DATE,
	p_EDC_ID IN NUMBER,
	p_ESP_ID IN NUMBER,
	p_PSE_ID IN NUMBER,
	p_POOL_ID IN NUMBER,
	p_APPLY_UFE_CODE IN NUMBER := 0,
	p_REQUESTOR IN VARCHAR := USER
	) RETURN SERVICE_STATE%ROWTYPE IS

v_EDC_ID NUMBER(9);
v_ESP_ID NUMBER(9);
v_PSE_ID NUMBER(9);
v_SERVICE_LOCATION_ID NUMBER(9);
v_SERVICE_POINT_ID NUMBER(9);
v_SERVICE_ZONE_ID NUMBER(9);
v_POOL_ID NUMBER(9);
v_SCHEDULE_GROUP_ID NUMBER(9);
v_SC_ID NUMBER(9);
v_SUPPLY_TYPE CHAR(1);
v_IS_BUG NUMBER(1);
v_IS_WHOLESALE NUMBER(1);
v_IS_AGGREGATE_POOL NUMBER(1);
v_SERVICE_DATE DATE := TRUNC(p_SERVICE_DATE);
v_SERVICE SERVICE%ROWTYPE;
v_SERVICE_STATE SERVICE_STATE%ROWTYPE;

BEGIN

	IF NVL(p_SERVICE_LOCATION_ID, CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
		GET_SERVICE_LOCATION_AND_POINT(p_ACCOUNT_ID, v_SERVICE_DATE, v_SERVICE_LOCATION_ID, v_SERVICE_POINT_ID);
	ELSE
		v_SERVICE_LOCATION_ID := p_SERVICE_LOCATION_ID;
		v_SERVICE_POINT_ID := GET_SERVICE_POINT_ID(p_ACCOUNT_ID, p_SERVICE_LOCATION_ID, v_SERVICE_DATE);
	END IF;

	GET_ACCOUNT_SERVICE_ID(p_ACCOUNT_ID, v_SERVICE_LOCATION_ID, p_METER_ID, p_AGGREGATE_ID, v_SERVICE.ACCOUNT_SERVICE_ID);

	IF NVL(p_EDC_ID,CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
		v_EDC_ID := GET_EDC_OF_RECORD(p_ACCOUNT_ID, v_SERVICE_DATE);
	ELSE
		v_EDC_ID := p_EDC_ID;
	END IF;

	IF NVL(p_ESP_ID, CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
		IF p_AGGREGATE_ID = CONSTANTS.NOT_ASSIGNED THEN
			v_ESP_ID := GET_ESP_OF_RECORD(p_ACCOUNT_ID, v_SERVICE_DATE);
		ELSE
			v_ESP_ID := GET_ESP_OF_RECORD(p_ACCOUNT_ID, p_AGGREGATE_ID, v_SERVICE_DATE);
		END IF;
	ELSE
		v_ESP_ID := p_ESP_ID;
	END IF;

	IF NVL(p_PSE_ID,CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
		v_PSE_ID := GET_PSE_OF_RECORD(v_ESP_ID, v_SERVICE_DATE);
	ELSE
		v_PSE_ID := p_PSE_ID;
	END IF;

	GET_PROVIDER_SERVICE_ID(v_EDC_ID, v_ESP_ID, v_PSE_ID, v_SERVICE.PROVIDER_SERVICE_ID);

	v_SERVICE.MODEL_ID := p_MODEL_ID;
	v_SERVICE.SCENARIO_ID := p_SCENARIO_ID;
	v_SERVICE.AS_OF_DATE := p_AS_OF_DATE;

	v_SERVICE_STATE.SERVICE_CODE := p_SERVICE_CODE;
	v_SERVICE_STATE.SERVICE_DATE := v_SERVICE_DATE;
	v_SERVICE_STATE.BASIS_AS_OF_DATE := SYSDATE;
	v_SERVICE_STATE.METER_TYPE := GET_METER_TYPE(p_ACCOUNT_ID, p_METER_ID);
	v_SERVICE_STATE.IS_EXTERNAL_FORECAST := 0;
	SELECT DECODE (p_AGGREGATE_ID, 0, 0, 1) INTO v_SERVICE_STATE.IS_AGGREGATE_ACCOUNT FROM DUAL;

	IF p_AGGREGATE_ID > 0 THEN
		GET_AGGREGATE_ACCOUNT_SERVICE(p_AGGREGATE_ID, v_SERVICE_DATE, p_AS_OF_DATE, v_SERVICE_STATE.SERVICE_ACCOUNTS, v_SERVICE_STATE.USAGE_FACTOR);
		IF NVL(p_POOL_ID,CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
			v_POOL_ID := GET_AGGREGATE_POOL_OF_RECORD(p_AGGREGATE_ID, v_SERVICE_DATE);
		ELSE
			v_POOL_ID := p_POOL_ID;
		END IF;
	ELSE
		v_SERVICE_STATE.SERVICE_ACCOUNTS := 1;
		v_SERVICE_STATE.USAGE_FACTOR := GET_USAGE_FACTOR(p_ACCOUNT_ID, p_METER_ID, v_SERVICE_DATE);
		IF NVL(p_POOL_ID,CONSTANTS.NOT_ASSIGNED) = CONSTANTS.NOT_ASSIGNED THEN
			v_POOL_ID := GET_POOL_OF_RECORD(p_ACCOUNT_ID, v_ESP_ID, v_SERVICE_DATE);
		ELSE
			v_POOL_ID := p_POOL_ID;
		END IF;
	END IF;

	v_SERVICE_ZONE_ID := GET_SERVICE_ZONE_ID(v_SERVICE_POINT_ID);
	v_SCHEDULE_GROUP_ID := GET_SCHEDULE_GROUP_ID(p_ACCOUNT_ID, p_METER_ID, v_SERVICE_DATE);
	v_SC_ID := GET_SC_ID(v_EDC_ID);
	v_SUPPLY_TYPE := GET_ESP_SUPPLY_TYPE(v_ESP_ID);
	v_IS_BUG := GET_PSE_IS_BUG(v_PSE_ID);
	v_IS_WHOLESALE := GET_IS_WHOLESALE(v_SERVICE_POINT_ID);
	v_IS_AGGREGATE_POOL := GET_IS_AGGREGATE_POOL(v_POOL_ID);

	SET_SERVICE_DELIVERY_ID(v_POOL_ID, v_SERVICE_POINT_ID, v_SERVICE_ZONE_ID, v_SCHEDULE_GROUP_ID, v_SC_ID, v_SUPPLY_TYPE, v_IS_BUG, v_IS_WHOLESALE, v_IS_AGGREGATE_POOL, v_SERVICE.SERVICE_DELIVERY_ID);


  IF p_APPLY_UFE_CODE = 0 THEN
    v_SERVICE_STATE.IS_UFE_PARTICIPANT := 0;
  ELSE
    v_SERVICE_STATE.IS_UFE_PARTICIPANT := GET_ACCOUNT_UFE_OVERRIDE(p_ACCOUNT_ID, p_SERVICE_CODE, p_REQUESTOR);
    IF v_SERVICE_STATE.IS_AGGREGATE_ACCOUNT = 1 AND v_SERVICE_STATE.METER_TYPE = 'I' THEN
      --use the accounts is_ufe_particpant setting for MDR accounts
      v_SERVICE_STATE.IS_UFE_PARTICIPANT := GET_ACCOUNT_IS_UFE_PARTICIPANT(p_ACCOUNT_ID);
    END IF;
    IF p_APPLY_UFE_CODE = 2 THEN
      IF p_SERVICE_CODE = GA.ACTUAL_SERVICE AND NOT GA.ENABLE_NON_AGG_UFE_SETTLEMENT AND v_SERVICE_STATE.METER_TYPE = 'I' THEN
        v_SERVICE_STATE.IS_UFE_PARTICIPANT := 0;
      ELSE
        v_SERVICE_STATE.IS_UFE_PARTICIPANT := GET_ACCOUNT_IS_UFE_PARTICIPANT(p_ACCOUNT_ID);
      END IF;
    END IF;
  END IF;

	PUT_SERVICE(p_SERVICE_CODE, v_SERVICE, v_SERVICE_STATE.SERVICE_ID);

	RETURN v_SERVICE_STATE;

END GET_SERVICE_STATE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_PROVIDER_SERVICE_STATE
	(
	p_MODEL_ID IN NUMBER,
	p_SCENARIO_ID IN NUMBER,
	p_ACCOUNT_SERVICE_ID IN NUMBER,
	p_PROVIDER_SERVICE_ID IN NUMBER,
	p_SERVICE_DATE IN DATE,
	p_AS_OF_DATE IN DATE
	) RETURN SERVICE_STATE%ROWTYPE IS

v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE;
v_PROVIDER_SERVICE PROVIDER_SERVICE%ROWTYPE;
v_POOL_ID NUMBER(9);
v_SERVICE_POINT_ID NUMBER(9);
v_SERVICE_ZONE_ID NUMBER(9);
v_SCHEDULE_GROUP_ID NUMBER(9);
v_SC_ID NUMBER(9);
v_SUPPLY_TYPE CHAR(1);
v_IS_BUG NUMBER(1);
v_IS_WHOLESALE NUMBER(1);
v_IS_AGGREGATE_POOL NUMBER(1);
v_SERVICE_DATE DATE := TRUNC(p_SERVICE_DATE);
v_SERVICE SERVICE%ROWTYPE;
v_SERVICE_STATE SERVICE_STATE%ROWTYPE;


BEGIN

	v_ACCOUNT_SERVICE := GET_ACCOUNT_SERVICE(p_ACCOUNT_SERVICE_ID);
	v_PROVIDER_SERVICE := GET_PROVIDER_SERVICE(p_PROVIDER_SERVICE_ID);

	v_SERVICE.MODEL_ID := p_MODEL_ID;
	v_SERVICE.SCENARIO_ID := p_SCENARIO_ID;
	v_SERVICE.AS_OF_DATE := p_AS_OF_DATE;
	v_SERVICE.ACCOUNT_SERVICE_ID := p_ACCOUNT_SERVICE_ID;
	v_SERVICE.PROVIDER_SERVICE_ID := p_PROVIDER_SERVICE_ID;

	v_SERVICE_STATE.SERVICE_CODE := GA.ACTUAL_SERVICE;
	v_SERVICE_STATE.SERVICE_DATE := v_SERVICE_DATE;
	v_SERVICE_STATE.METER_TYPE := GET_METER_TYPE(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.METER_ID);
	v_SERVICE_STATE.IS_EXTERNAL_FORECAST := 0;
	v_SERVICE_STATE.SERVICE_ACCOUNTS := 1;
	v_SERVICE_STATE.USAGE_FACTOR:= 1;

	SELECT DECODE(v_ACCOUNT_SERVICE.AGGREGATE_ID,0,0,1) INTO v_SERVICE_STATE.IS_AGGREGATE_ACCOUNT FROM DUAL;

	IF v_SERVICE_STATE.IS_AGGREGATE_ACCOUNT = 1 THEN
		v_POOL_ID := GET_AGGREGATE_POOL_OF_RECORD(v_ACCOUNT_SERVICE.AGGREGATE_ID, v_SERVICE_DATE);
	ELSE
		v_POOL_ID := GET_POOL_OF_RECORD(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_PROVIDER_SERVICE.ESP_ID, v_SERVICE_DATE, v_ACCOUNT_SERVICE.AGGREGATE_ID > 0);
	END IF;

	v_SERVICE_POINT_ID := GET_SERVICE_POINT_ID(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.SERVICE_LOCATION_ID, v_SERVICE_DATE);
	v_SERVICE_ZONE_ID := GET_SERVICE_ZONE_ID(v_SERVICE_ZONE_ID);
	v_SCHEDULE_GROUP_ID := GET_SCHEDULE_GROUP_ID(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.METER_ID, p_SERVICE_DATE);
	v_SC_ID := GET_SC_ID(v_PROVIDER_SERVICE.EDC_ID);
	v_SUPPLY_TYPE := GET_ESP_SUPPLY_TYPE(v_PROVIDER_SERVICE.ESP_ID);
	v_IS_BUG := GET_PSE_IS_BUG(v_PROVIDER_SERVICE.PSE_ID);
	v_IS_WHOLESALE := GET_IS_WHOLESALE(v_SERVICE_POINT_ID);
	v_IS_AGGREGATE_POOL := GET_IS_AGGREGATE_POOL(v_POOL_ID);

	SET_SERVICE_DELIVERY_ID(v_POOL_ID, v_SERVICE_POINT_ID, v_SERVICE_ZONE_ID, v_SCHEDULE_GROUP_ID, v_SC_ID, v_SUPPLY_TYPE, v_IS_BUG, v_IS_WHOLESALE, v_IS_AGGREGATE_POOL, v_SERVICE.SERVICE_DELIVERY_ID);

	v_SERVICE_STATE.IS_UFE_PARTICIPANT := 0;

	PUT_SERVICE(GA.ACTUAL_SERVICE, v_SERVICE, v_SERVICE_STATE.SERVICE_ID);

	RETURN v_SERVICE_STATE;

END GET_PROVIDER_SERVICE_STATE;
----------------------------------------------------------------------------------------------------
FUNCTION GET_PROFILE_AS_OF_DATE
	(
	p_PROFILE_ID IN NUMBER,
	p_AS_OF_DATE IN DATE
	) RETURN DATE IS

-- Answer the assigned profile version date for the specified profile.

v_AS_OF_DATE DATE := LOW_DATE;
v_USE_PRODUCTION_PROFILE NUMBER(1);

BEGIN

	IF GA.VERSION_PROFILE THEN
		IF GA.ENFORCE_PRODUCTION_PROFILE_USE THEN
			v_USE_PRODUCTION_PROFILE := 1;
		ELSE
			v_USE_PRODUCTION_PROFILE := 0;
		END IF;
		SELECT MAX(AS_OF_DATE)
		INTO v_AS_OF_DATE
		FROM LOAD_PROFILE_STATISTICS
		WHERE PROFILE_ID = p_PROFILE_ID
			AND AS_OF_DATE <= p_AS_OF_DATE
			AND UPPER(PROFILE_STATUS) = DECODE(v_USE_PRODUCTION_PROFILE,1,'PRODUCTION',UPPER(PROFILE_STATUS));
	END IF;

	RETURN v_AS_OF_DATE;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
		IF GA.ENFORCE_PRODUCTION_PROFILE_USE THEN
			RETURN NULL;
		ELSE
			RETURN LOW_DATE;
		END IF;

END GET_PROFILE_AS_OF_DATE;

--@@Begin Implementtation Override--
FUNCTION GET_PROVIDER_SERVICE_ID
   (
   p_EDC_ID IN NUMBER,
   p_ESP_ID IN NUMBER,
   p_PSE_ID IN NUMBER
   ) RETURN NUMBER IS

-- Answer the Service Id for the account service for the specified date.

v_PROVIDER_SERVICE_ID NUMBER;

BEGIN

   SELECT PROVIDER_SERVICE_ID
   INTO v_PROVIDER_SERVICE_ID
   FROM PROVIDER_SERVICE
   WHERE EDC_ID = p_EDC_ID
      AND ESP_ID = p_ESP_ID
      AND PSE_ID = p_PSE_ID;

RETURN v_PROVIDER_SERVICE_ID;

EXCEPTION
   WHEN NO_DATA_FOUND THEN
      ERRS.LOG_AND_CONTINUE(p_LOG_LEVEL => LOGS.c_LEVEL_DEBUG);
      RETURN CONSTANTS.NOT_ASSIGNED;
END GET_PROVIDER_SERVICE_ID;

FUNCTION GET_PROV_POOL_SERVICE_STATE
   (
   p_MODEL_ID IN NUMBER,
   p_SCENARIO_ID IN NUMBER,
   p_ACCOUNT_SERVICE_ID IN NUMBER,
   p_PROVIDER_SERVICE_ID IN NUMBER,
   p_POOL_ID IN NUMBER,
   p_SERVICE_DATE IN DATE,
   p_AS_OF_DATE IN DATE,
   p_UFE_FLAG IN BINARY_INTEGER
   ) RETURN SERVICE_STATE%ROWTYPE IS

v_ACCOUNT_SERVICE ACCOUNT_SERVICE%ROWTYPE;
v_PROVIDER_SERVICE PROVIDER_SERVICE%ROWTYPE;
v_POOL_ID NUMBER(9);
v_SERVICE_POINT_ID NUMBER(9);
v_SERVICE_ZONE_ID NUMBER(9);
v_SCHEDULE_GROUP_ID NUMBER(9);
v_SC_ID NUMBER(9);
v_SUPPLY_TYPE CHAR(1);
v_IS_BUG NUMBER(1);
v_IS_WHOLESALE NUMBER(1);
v_IS_AGGREGATE_POOL NUMBER(1);
v_SERVICE_DATE DATE := TRUNC(p_SERVICE_DATE);
v_SERVICE SERVICE%ROWTYPE;
v_SERVICE_STATE SERVICE_STATE%ROWTYPE;
BEGIN
   v_ACCOUNT_SERVICE := GET_ACCOUNT_SERVICE(p_ACCOUNT_SERVICE_ID);
   v_PROVIDER_SERVICE := GET_PROVIDER_SERVICE(p_PROVIDER_SERVICE_ID);
   v_SERVICE.MODEL_ID := p_MODEL_ID;
   v_SERVICE.SCENARIO_ID := p_SCENARIO_ID;
   v_SERVICE.AS_OF_DATE := p_AS_OF_DATE;
   v_SERVICE.ACCOUNT_SERVICE_ID := p_ACCOUNT_SERVICE_ID;
   v_SERVICE.PROVIDER_SERVICE_ID := p_PROVIDER_SERVICE_ID;
   v_SERVICE_STATE.SERVICE_CODE := GA.ACTUAL_SERVICE;
   v_SERVICE_STATE.SERVICE_DATE := v_SERVICE_DATE;
   v_SERVICE_STATE.METER_TYPE := GET_METER_TYPE(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.METER_ID);
   v_SERVICE_STATE.IS_EXTERNAL_FORECAST := 0;
   v_SERVICE_STATE.SERVICE_ACCOUNTS := 1;
   v_SERVICE_STATE.USAGE_FACTOR:= 1;

   SELECT DECODE(v_ACCOUNT_SERVICE.AGGREGATE_ID,0,0,1) INTO v_SERVICE_STATE.IS_AGGREGATE_ACCOUNT FROM DUAL;
   v_POOL_ID := p_POOL_ID;
   v_SERVICE_POINT_ID := GET_SERVICE_POINT_ID(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.SERVICE_LOCATION_ID, v_SERVICE_DATE);
   v_SERVICE_ZONE_ID := GET_SERVICE_ZONE_ID(v_SERVICE_ZONE_ID);
   v_SCHEDULE_GROUP_ID := GET_SCHEDULE_GROUP_ID(v_ACCOUNT_SERVICE.ACCOUNT_ID, v_ACCOUNT_SERVICE.METER_ID, p_SERVICE_DATE);
   v_SC_ID := GET_SC_ID(v_PROVIDER_SERVICE.EDC_ID);
   v_SUPPLY_TYPE := GET_ESP_SUPPLY_TYPE(v_PROVIDER_SERVICE.ESP_ID);
   v_IS_BUG := GET_PSE_IS_BUG(v_PROVIDER_SERVICE.PSE_ID);
   v_IS_WHOLESALE := GET_IS_WHOLESALE(v_SERVICE_POINT_ID);
   v_IS_AGGREGATE_POOL := GET_IS_AGGREGATE_POOL(v_POOL_ID);

   SET_SERVICE_DELIVERY_ID(v_POOL_ID, v_SERVICE_POINT_ID, v_SERVICE_ZONE_ID, v_SCHEDULE_GROUP_ID, v_SC_ID, v_SUPPLY_TYPE, v_IS_BUG, v_IS_WHOLESALE, v_IS_AGGREGATE_POOL, v_SERVICE.SERVICE_DELIVERY_ID);
   v_SERVICE_STATE.IS_UFE_PARTICIPANT := p_UFE_FLAG;
   PUT_SERVICE(GA.ACTUAL_SERVICE, v_SERVICE, v_SERVICE_STATE.SERVICE_ID);
   RETURN v_SERVICE_STATE;
END GET_PROV_POOL_SERVICE_STATE;
--@@End Implementtation Override--
END CS;
/

