CREATE OR REPLACE PACKAGE BODY CDI_ANCILLARY_SERVICE AS

TYPE t_COUNTER IS TABLE OF PLS_INTEGER INDEX BY VARCHAR2(100);

c_SET_INITIAL_BID_BLOCK_SIZE   CONSTANT VARCHAR2(64) := 'CDI: Set Initial Bid Block Size';
c_ACCEPT_PLC_NSPL_SCHEDULE     CONSTANT VARCHAR2(64) := 'CDI: Accept PLC/NSPL Schedule';
c_ACCEPT_FTR_NSPL_SCHEDULE     CONSTANT VARCHAR2(64) := 'CDI: Accept FTR NSPL Schedule';
c_POST_DEFAULT_PLC_NSPL        CONSTANT VARCHAR2(64) := 'CDI: Post Default PLC/NSPL';
c_LOAD_PLC_NSPL_INT_FROM_FILE  CONSTANT VARCHAR2(64) := 'CDI: Load PLC/NSPL Initial From File';
c_DATE_FORMAT                  CONSTANT VARCHAR2(16) := 'MM/DD/YYYY';
c_DATE_TIME_FORMAT             CONSTANT VARCHAR2(32) := 'MM/DD/YYYY HH24:MI:SS';
c_PLC_NAME                     CONSTANT VARCHAR2(16) := 'ICAP';
c_NSPL_NAME                    CONSTANT VARCHAR2(16) := 'Network Service';
c_DEFAULT_SUPPLIER_NAME        CONSTANT VARCHAR2(16) := 'DEFAULT';
c_FILE_LOAD_OPTION             CONSTANT NUMBER(1)    := 2;
c_ONE_SECOND                   CONSTANT NUMBER       := 1/86400;
c_COMMA                        CONSTANT CHAR(1)      := ',';

l_MESSAGE VARCHAR2(1000);
l_STATUS  NUMBER;

PROCEDURE PARSE_FILE_CONTENT(p_CLOB IN CLOB, p_LINES IN OUT NOCOPY CDI_PLC_NSPL_INIT_VALUE_LIST ) AS
v_LINE_COUNT PLS_INTEGER := 0;
v_BEGIN_POS NUMBER := 1;
v_END_POS NUMBER;
v_END_POS2 NUMBER;
v_END_CHAR VARCHAR2(1);
v_CHAR VARCHAR2(1);
v_LENGTH NUMBER;
v_RECORD VARCHAR2(4000);
v_RECORD_SIZE NUMBER := 0;
v_CHUNK VARCHAR2(32767);
v_CHUNK_SIZE NUMBER := 32767;
v_CHUNK_OFFSET NUMBER := 1;
v_CHUNK_COUNT NUMBER := 1;
v_LAST_LINE BOOLEAN := FALSE;
v_FIELDS PARSE_UTIL.STRING_TABLE;
v_MARK_TIME  PLS_INTEGER := DBMS_UTILITY.GET_TIME;
   PROCEDURE POST_RECORD AS
   v_DATE DATE;
   BEGIN
      PARSE_UTIL.PARSE_DELIMITED_STRING(v_RECORD, c_COMMA, v_FIELDS);
      BEGIN
         v_DATE := TO_DATE(TRIM(v_FIELDS(4)), c_DATE_TIME_FORMAT);
      EXCEPTION WHEN OTHERS
         THEN v_DATE := CONSTANTS.LOW_DATE;
      END; 
      v_LINE_COUNT := v_LINE_COUNT + 1;
      p_LINES.EXTEND;
      p_LINES(p_LINES.LAST) := CDI_PLC_NSPL_INIT_VALUE_TYPE(TRIM(v_FIELDS(1)), TRIM(v_FIELDS(2)), TRIM(v_FIELDS(3)), v_DATE, TRIM(v_FIELDS(5)));
      v_RECORD := NULL;
   END POST_RECORD;
BEGIN
   v_LENGTH := DBMS_LOB.GETLENGTH(p_CLOB);
   WHILE v_CHUNK_OFFSET <= v_LENGTH AND NOT v_LAST_LINE LOOP
      v_CHUNK_SIZE := 32767;
      DBMS_LOB.READ(p_CLOB,v_CHUNK_SIZE,v_CHUNK_OFFSET,v_CHUNK);
      v_LAST_LINE := (v_CHUNK_SIZE + v_CHUNK_OFFSET) > v_LENGTH;
-- Cleanup, Get Ready For Next Chunk
      v_BEGIN_POS := 1;
      v_END_POS := NULL;
      v_END_POS2 := NULL;
      v_RECORD := NULL;
      WHILE v_BEGIN_POS <= v_CHUNK_SIZE LOOP
-- Check To Make Sure We Did Not Split The CRLF During The 32K Last Chunk --
         IF v_CHUNK_COUNT > 1 AND v_BEGIN_POS = 1 THEN
            v_END_POS := INSTR(v_CHUNK, CHR(10), v_BEGIN_POS);
            IF v_END_POS = 1 AND v_END_CHAR IS NOT NULL AND v_END_CHAR <> CHR(10) THEN
-- Remove The First Character From The Chunk --
               v_CHUNK := SUBSTR(v_CHUNK, 2);
-- Change The Size Of The Chunk When The First Character Removed --
               v_CHUNK_SIZE := LENGTH(v_CHUNK);
            END IF;
         END IF;
         v_END_CHAR := NULL;
         v_END_POS := INSTR(v_CHUNK, CHR(10), v_BEGIN_POS);
         v_END_POS2 := INSTR(v_CHUNK, CHR(13), v_BEGIN_POS);
         v_RECORD_SIZE := 0;
-- Determine Which Character Was Found - Or, If We Found Both, Which One Comes First --
          IF v_END_POS2 BETWEEN v_BEGIN_POS AND v_END_POS OR v_BEGIN_POS BETWEEN v_END_POS AND v_END_POS2 THEN
             v_END_POS := v_END_POS2;
             v_END_CHAR := CHR(13);
          ELSE
             v_END_CHAR := CHR(10);
          END IF;
         IF v_END_POS < v_BEGIN_POS THEN
            v_RECORD := SUBSTR(v_CHUNK, v_BEGIN_POS, 4000);
            v_RECORD_SIZE := LENGTH(v_RECORD);
            v_RECORD := TRIM(v_RECORD);
            v_END_POS := v_CHUNK_SIZE;
            IF v_LAST_LINE THEN
               POST_RECORD;
            END IF;
         ELSE
            v_RECORD := TRIM(SUBSTR(v_CHUNK, v_BEGIN_POS, v_END_POS - v_BEGIN_POS));
            v_CHAR := SUBSTR(v_CHUNK, v_END_POS+1, 1);
             IF v_CHAR IN (CHR(10),CHR(13)) AND v_CHAR <> v_END_CHAR THEN
                v_END_POS := v_END_POS+1;
             END IF;
            POST_RECORD;
         END IF;
         v_BEGIN_POS := v_END_POS + 1;
      END LOOP;
      IF v_RECORD IS NOT NULL THEN
         v_CHUNK_OFFSET := v_CHUNK_OFFSET + 32767 - v_RECORD_SIZE;
      ELSE
         v_CHUNK_OFFSET := v_CHUNK_OFFSET + 32767;
      END IF;
      v_CHUNK_COUNT := v_CHUNK_COUNT + 1;
   END LOOP;
   LOGS.LOG_INFO('Parse File Content Complete. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100)));
EXCEPTION
    WHEN VALUE_ERROR THEN
       ERRS.RAISE(MSGCODES.c_ERR_GENERAL, 'VALUE_ERROR: Counter: ' || v_LINE_COUNT || ', Record: ' || v_RECORD || ', Begin Position: ' || TO_CHAR(v_BEGIN_POS) || ', End Position: ' || TO_CHAR(v_END_POS)|| ', Length: ' || TO_CHAR(v_LENGTH));
END PARSE_FILE_CONTENT;

PROCEDURE POST_COMPETITIVE_POLR
   (
   p_BEGIN_DATE IN DATE,
   p_END_DATE   IN DATE,
   p_ICAP_ID    IN PLS_INTEGER,
   p_NSPL_ID    IN PLS_INTEGER
   ) AS
v_ICAP_VALUE   NUMBER;
v_NET_VALUE    NUMBER;
v_COUNT        PLS_INTEGER := 0;
v_SERVICE_DATE DATE := TRUNC(p_BEGIN_DATE);
BEGIN
   WHILE v_SERVICE_DATE <= p_END_DATE LOOP
      MERGE INTO CDI_PLC_NSPL_STAGE T
      USING
         (SELECT PLC_DATE, ESP_ID, PSE_ID, CONTRACT_NAME "PJM_SHORT_NAME", POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, TICKET_NUMBER, PLC_BAND, SUM(ICAP_VALUE) "ICAP_VALUE", SUM(NSPL_VALUE) "NSPL_VALUE"
         FROM
            (SELECT v_SERVICE_DATE "PLC_DATE", B.ESP_ID, F.PSE_ID, G.CONTRACT_NAME, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, NULL "TICKET_NUMBER", D.PLC_BAND,
               SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_ICAP_ID THEN A.SERVICE_VAL END) "ICAP_VALUE",
               SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_NSPL_ID THEN A.SERVICE_VAL END) "NSPL_VALUE"
            FROM AGGREGATE_ANCILLARY_SERVICE  A
               JOIN AGGREGATE_ACCOUNT_ESP     B ON B.AGGREGATE_ID = A.AGGREGATE_ID AND v_SERVICE_DATE BETWEEN B.BEGIN_DATE AND NVL(B.END_DATE, v_SERVICE_DATE)
               JOIN ENERGY_SERVICE_PROVIDER   C ON C.ESP_ID = B.ESP_ID
               JOIN POOL                      D ON D.POOL_ID = B.POOL_ID
               JOIN PSE_ESP                   H ON H.ESP_ID = B.ESP_ID AND v_SERVICE_DATE BETWEEN H.BEGIN_DATE AND NVL(H.END_DATE, v_SERVICE_DATE)
               JOIN PURCHASING_SELLING_ENTITY F ON F.PSE_ID = H.PSE_ID
               JOIN INTERCHANGE_CONTRACT      E ON E.CONTRACT_NAME = F.PSE_NAME AND v_SERVICE_DATE BETWEEN E.BEGIN_DATE AND NVL(E.END_DATE, v_SERVICE_DATE)
               JOIN TP_CONTRACT_NUMBER        G ON G.CONTRACT_ID = E.CONTRACT_ID AND v_SERVICE_DATE BETWEEN G.BEGIN_DATE AND NVL(G.END_DATE, v_SERVICE_DATE)
               JOIN ACCOUNT_EDC               I ON I.ACCOUNT_ID = B.ACCOUNT_ID AND v_SERVICE_DATE BETWEEN I.BEGIN_DATE AND NVL(I.END_DATE, v_SERVICE_DATE)
            WHERE A.SERVICE_DATE = v_SERVICE_DATE
            GROUP BY B.ESP_ID, F.PSE_ID, G.CONTRACT_NAME, D.POLR_TYPE, G.CONTRACT_NAME, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND
            UNION
            SELECT v_SERVICE_DATE "PLC_DATE", B.ESP_ID, F.PSE_ID, G.CONTRACT_NAME, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, NULL "TICKET_NUMBER", D.PLC_BAND,
               SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_ICAP_ID THEN A.SERVICE_VAL END) "ICAP_VALUE",
               SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_NSPL_ID THEN A.SERVICE_VAL END) "NSPL_VALUE"
            FROM ACCOUNT_ANCILLARY_SERVICE    A
               JOIN ACCOUNT_ESP               B ON B.ACCOUNT_ID = A.ACCOUNT_ID AND v_SERVICE_DATE BETWEEN B.BEGIN_DATE AND NVL(B.END_DATE, v_SERVICE_DATE)
               JOIN ENERGY_SERVICE_PROVIDER   C ON C.ESP_ID = B.ESP_ID
               JOIN POOL                      D ON D.POOL_ID = B.POOL_ID
               JOIN PSE_ESP                   H ON H.ESP_ID = B.ESP_ID
               JOIN PURCHASING_SELLING_ENTITY F ON F.PSE_ID = H.PSE_ID
               JOIN INTERCHANGE_CONTRACT      E ON E.CONTRACT_NAME = F.PSE_NAME AND v_SERVICE_DATE BETWEEN E.BEGIN_DATE AND NVL(E.END_DATE, v_SERVICE_DATE)
               JOIN TP_CONTRACT_NUMBER        G ON G.CONTRACT_ID = E.CONTRACT_ID AND v_SERVICE_DATE BETWEEN G.BEGIN_DATE AND NVL(G.END_DATE, v_SERVICE_DATE)
               JOIN ACCOUNT_EDC               I ON I.ACCOUNT_ID = B.ACCOUNT_ID AND v_SERVICE_DATE BETWEEN I.BEGIN_DATE AND NVL(I.END_DATE, v_SERVICE_DATE)
            WHERE v_SERVICE_DATE BETWEEN A.BEGIN_DATE AND NVL(A.END_DATE, v_SERVICE_DATE)
            GROUP BY B.ESP_ID, F.PSE_ID, G.CONTRACT_NAME, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND)
         GROUP BY PLC_DATE, ESP_ID, PSE_ID, CONTRACT_NAME, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, TICKET_NUMBER, PLC_BAND) S
      ON(TRUNC(T.PLC_DATE) = TRUNC(S.PLC_DATE) AND T.ESP_ID = S.ESP_ID AND T.PSE_ID = S.PSE_ID AND NVL(T.PJM_SHORT_NAME,'X') = NVL(S.PJM_SHORT_NAME, 'X') AND NVL(T.POLR_TYPE,'X') = NVL(S.POLR_TYPE, 'X') AND NVL(T.VOLTAGE_CLASS, 'X') = NVL(S.VOLTAGE_CLASS, 'X') AND NVL(T.REPORTED_SEGMENT, 'X') = NVL(S.REPORTED_SEGMENT, 'X') AND NVL(T.RFT_TICKET_NUMBER, 'X') = NVL(S.TICKET_NUMBER, 'X') AND NVL(T.PLC_BAND, 'X') = NVL(S.PLC_BAND, 'X'))
      WHEN MATCHED THEN
         UPDATE SET T.ICAP_VALUE = S.ICAP_VALUE, T.NSPL_VALUE = S.NSPL_VALUE
      WHEN NOT MATCHED THEN
         INSERT(PLC_DATE, ESP_ID, PSE_ID, PJM_SHORT_NAME, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, RFT_TICKET_NUMBER, PLC_BAND, ICAP_VALUE, NSPL_VALUE)
         VALUES(S.PLC_DATE, S.ESP_ID, S.PSE_ID, S.PJM_SHORT_NAME, S.POLR_TYPE, S.VOLTAGE_CLASS, S.REPORTED_SEGMENT, NULL, S.PLC_BAND, S.ICAP_VALUE, S.NSPL_VALUE);
      v_COUNT := v_COUNT + SQL%ROWCOUNT;
      v_SERVICE_DATE := v_SERVICE_DATE + 1;
   END LOOP;
   LOGS.LOG_INFO('Number Of Records Posted To The CDI_PLC_NSPL_LOAD_STAGE Table: ' || TO_CHAR(v_COUNT));
END POST_COMPETITIVE_POLR;

PROCEDURE POST_NON_COMPETITIVE_POLR
   (
   p_BEGIN_DATE    IN  DATE,
   p_END_DATE      IN  DATE,
   p_ICAP_VALUE    IN  NUMBER,
   p_NSPL_VALUE    IN  NUMBER
   ) AS
v_ICAP_VALUE   NUMBER;
v_NET_VALUE    NUMBER;
v_SERVICE_DATE DATE := TRUNC(p_BEGIN_DATE);
v_COUNT        PLS_INTEGER := 0;
CURSOR c_SELECT(p_SERVICE_DATE IN DATE) IS
   SELECT ESP_ID, PSE_ID, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, PLC_BAND, SUM(ICAP_VALUE) "ICAP_VALUE", SUM(NSPL_VALUE) "NSPL_VALUE"
   FROM
      (SELECT B.ESP_ID, F.PSE_ID, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND,
         SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_ICAP_VALUE THEN A.SERVICE_VAL END) "ICAP_VALUE",
         SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_NSPL_VALUE THEN A.SERVICE_VAL END) "NSPL_VALUE"
      FROM AGGREGATE_ANCILLARY_SERVICE  A
         JOIN AGGREGATE_ACCOUNT_ESP     B ON B.AGGREGATE_ID = A.AGGREGATE_ID AND p_SERVICE_DATE BETWEEN B.BEGIN_DATE AND NVL(B.END_DATE, p_SERVICE_DATE)
         JOIN ENERGY_SERVICE_PROVIDER   C ON C.ESP_ID = B.ESP_ID AND UPPER(C.ESP_NAME) = c_DEFAULT_SUPPLIER_NAME
         JOIN POOL                      D ON D.POOL_ID = B.POOL_ID
         JOIN PSE_ESP                   E ON E.ESP_ID = B.ESP_ID AND p_SERVICE_DATE BETWEEN E.BEGIN_DATE AND NVL(E.END_DATE, p_SERVICE_DATE)
         JOIN PURCHASING_SELLING_ENTITY F ON F.PSE_ID = E.PSE_ID
         JOIN ACCOUNT_EDC               G ON G.ACCOUNT_ID = B.ACCOUNT_ID AND p_SERVICE_DATE BETWEEN G.BEGIN_DATE AND NVL(G.END_DATE, p_SERVICE_DATE)
      WHERE A.SERVICE_DATE = p_SERVICE_DATE
      GROUP BY B.ESP_ID, F.PSE_ID, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND
      UNION
      SELECT B.ESP_ID, F.PSE_ID, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND,
         SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_ICAP_VALUE THEN A.SERVICE_VAL END) "ICAP_VALUE",
         SUM(CASE WHEN A.ANCILLARY_SERVICE_ID = p_NSPL_VALUE THEN A.SERVICE_VAL END) "NSPL_VALUE"
      FROM ACCOUNT_ANCILLARY_SERVICE    A
         JOIN ACCOUNT_ESP               B ON B.ACCOUNT_ID = A.ACCOUNT_ID AND p_SERVICE_DATE BETWEEN B.BEGIN_DATE AND NVL(B.END_DATE, p_SERVICE_DATE)
         JOIN ENERGY_SERVICE_PROVIDER   C ON C.ESP_ID = B.ESP_ID AND UPPER(C.ESP_NAME) = c_DEFAULT_SUPPLIER_NAME
         JOIN POOL                      D ON D.POOL_ID = B.POOL_ID
         JOIN PSE_ESP                   H ON H.ESP_ID = B.ESP_ID AND p_SERVICE_DATE BETWEEN H.BEGIN_DATE AND NVL(H.END_DATE, p_SERVICE_DATE)
         JOIN PURCHASING_SELLING_ENTITY F ON F.PSE_ID = H.PSE_ID
         JOIN ACCOUNT_EDC               I ON I.ACCOUNT_ID = A.ACCOUNT_ID AND p_SERVICE_DATE BETWEEN I.BEGIN_DATE AND NVL(I.END_DATE, p_SERVICE_DATE)
      WHERE p_SERVICE_DATE BETWEEN A.BEGIN_DATE AND NVL(A.END_DATE, p_SERVICE_DATE)
      GROUP BY B.ESP_ID, F.PSE_ID, D.POLR_TYPE, D.VOLTAGE_CLASS, D.REPORTED_SEGMENT, D.PLC_BAND)
   GROUP BY ESP_ID, PSE_ID, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, PLC_BAND;
BEGIN
   WHILE v_SERVICE_DATE <= p_END_DATE LOOP
      FOR v_SELECT IN c_SELECT(v_SERVICE_DATE) LOOP
         INSERT INTO CDI_PLC_NSPL_STAGE(PLC_DATE,ESP_ID,PSE_ID,PJM_SHORT_NAME,POLR_TYPE,VOLTAGE_CLASS,REPORTED_SEGMENT,RFT_TICKET_NUMBER,PLC_BAND,ICAP_VALUE,NSPL_VALUE)
         SELECT v_SERVICE_DATE, v_SELECT.ESP_ID, v_SELECT.PSE_ID, A.PJM_SHORT, v_SELECT.POLR_TYPE, v_SELECT.VOLTAGE_CLASS, v_SELECT.REPORTED_SEGMENT, A.SUPPLIER_ID, v_SELECT.PLC_BAND, SUM(v_SELECT.ICAP_VALUE * A.SHARE_OF_LOAD), SUM(v_SELECT.NSPL_VALUE* A.SHARE_OF_LOAD)
         FROM BGE_SUPPLIER_VIEW A
         WHERE A.POLR_TYPE = v_SELECT.POLR_TYPE
            AND v_SERVICE_DATE BETWEEN A.POWER_FLOW_START AND A.POWER_FLOW_END
         GROUP BY v_SELECT.ESP_ID, v_SELECT.PSE_ID, A.PJM_SHORT, v_SELECT.POLR_TYPE, v_SELECT.VOLTAGE_CLASS, v_SELECT.REPORTED_SEGMENT, A.SUPPLIER_ID, v_SELECT.PLC_BAND;
         v_COUNT := v_COUNT + SQL%ROWCOUNT;
      END LOOP;
      v_SERVICE_DATE := v_SERVICE_DATE + 1;
   END LOOP;
   LOGS.LOG_INFO('Number Of Records Posted To The CDI_PLC_NSPL_LOAD_STAGE Table: ' || TO_CHAR(v_COUNT));
END POST_NON_COMPETITIVE_POLR;

PROCEDURE POST_INCREMENT_DECREMENT(p_BEGIN_DATE IN DATE, p_END_DATE IN DATE) AS
v_BASE_SIZE_PRL   NUMBER(14,3);
v_BASE_SIZE_PRX   NUMBER(14,3);
v_BASE_SIZE       NUMBER(14,3);
v_TOLERANCE       NUMBER(14,3);
v_BASE_PERCENT    NUMBER(9,7);
v_INC_PERCENT     NUMBER(9,7);
v_DEDUCTION       NUMBER(14,3);
v_DEDUCTION_PRL   NUMBER(14,3);
v_DEDUCTION_PRX   NUMBER(14,3);
v_AGG_BLOCK_ADJUSTMENT     NUMBER(14,3);
v_PRL_AGG_BLOCK_ADJUSTMENT NUMBER(14,3);
v_PRX_AGG_BLOCK_ADJUSTMENT NUMBER(14,3);
v_INC_LABEL       CHAR(1);
v_DEC_LABEL       CHAR(1);
v_PRX_PERCENT_DEC NUMBER;
v_PRL_PERCENT_DEC NUMBER;
v_INC_MW          NUMBER := TO_NUMBER(GET_DICTIONARY_VALUE('INC_MW',0,'Settlement','INC_DEC','?','?',0));
v_DEC_MW          NUMBER := TO_NUMBER(GET_DICTIONARY_VALUE('DEC_MW',0,'Settlement','INC_DEC','?','?',0));
v_INDEX           VARCHAR2(100);
v_COUNTER         t_COUNTER;
CURSOR c_SELECT_POLR IS
   SELECT PLC_DATE, CASE POLR_TYPE WHEN 'PRL' THEN 'PRC' WHEN 'PRX' THEN 'PRC' ELSE POLR_TYPE END "POLR_TYPE",
      SUM(ICAP_VALUE) "ICAP_VALUE",
      SUM(NSPL_VALUE) "NSPL_VALUE",
      SUM(CASE WHEN POLR_TYPE IN ('PRL') THEN ICAP_VALUE ELSE 0 END) "ICAP_VALUE_PRL",
      SUM(CASE WHEN POLR_TYPE IN ('PRL') THEN NSPL_VALUE ELSE 0 END) "NSPL_VALUE_PRL",
      SUM(CASE WHEN POLR_TYPE IN ('PRX') THEN ICAP_VALUE ELSE 0 END) "ICAP_VALUE_PRX",
      SUM(CASE WHEN POLR_TYPE IN ('PRX') THEN NSPL_VALUE ELSE 0 END) "NSPL_VALUE_PRX"
   FROM CDI_PLC_LOAD
   WHERE PLC_DATE BETWEEN p_BEGIN_DATE AND p_END_DATE
      AND RFT_TICKET_NUMBER IS NOT NULL
   GROUP BY PLC_DATE, CASE POLR_TYPE WHEN 'PRL' THEN 'PRC' WHEN 'PRX' THEN 'PRC' ELSE POLR_TYPE END
   ORDER BY PLC_DATE, POLR_TYPE;
CURSOR c_SELECT(p_PLC_DATE IN DATE, p_POLR_TYPE IN VARCHAR2) IS
SELECT DISTINCT
   CASE WHEN POLR_TYPE = 'PRC' THEN BASE_BLOCK_SIZE_PRL/ DECODE(NUMBER_OF_BLOCKS_PRL,0,1,NUMBER_OF_BLOCKS_PRL) + (BASE_BLOCK_SIZE_PRX/ DECODE(NUMBER_OF_BLOCKS_PRX,0,1,NUMBER_OF_BLOCKS_PRX) ) + INC_MW ELSE BASE_BLOCK_SIZE END "BASE_BLOCK_SIZE2",
   CASE WHEN POLR_TYPE = 'PRC' THEN SHARE_OF_LOAD_PRL/ DECODE(NUMBER_OF_BLOCKS_PRL,0,1,NUMBER_OF_BLOCKS_PRL) + (SHARE_OF_LOAD_PRX/ DECODE(NUMBER_OF_BLOCKS_PRX,0,1,NUMBER_OF_BLOCKS_PRX) ) ELSE SHARE_OF_LOAD2 END "SHARE_OF_LOAD",
   X.*
FROM
   (SELECT
      A.RFT_TICKET_NUMBER RFP_TICKET_NUMBER,
      DECODE(B.POLR_TYPE,'PRL','PRC','PRX', 'PRC',B.POLR_TYPE) "POLR_TYPE",
      A.PLC_DATE              "PLC_DATE",
      A.PJM_SHORT_NAME        "PJM_SHORT_NAME",
      B.PJM_INC_SHORT         "PJM_INC_SHORT",
      SUM(B.NUMBER_OF_BLOCKS) "NUMBER_OF_BLOCKS",
      MAX(B.NUMBER_OF_BLOCKS) "MAX_NUMBER_OF_BLOCKS",
      AVG(B.INC_MW * 1000)    "INC_MW",
      AVG(B.DEC_MW * 1000)    "DEC_MW",
      SUM(C.BASE_BLOCK_SIZE)  "BASE_BLOCK_SIZE",
      C.EFFECTIVE_DATE        "EFFECTIVE_DATE",
      B.POWER_FLOW_END        "POWER_FLOW_END",
      SUM(B.SHARE_OF_LOAD)    "SHARE_OF_LOAD2",
      SUM(A.ICAP_VALUE)       "ICAP_VALUE",
      SUM(A.NSPL_VALUE)       "NSPL_VALUE",
      SUM(CASE WHEN C.POLR_TYPE = 'PRL' THEN  C.BASE_BLOCK_SIZE ELSE 0 END )                   "BASE_BLOCK_SIZE_PRL",
      SUM(CASE WHEN C.POLR_TYPE = 'PRX' THEN  C.BASE_BLOCK_SIZE ELSE 0 END )                   "BASE_BLOCK_SIZE_PRX",
      SUM(CASE WHEN B.POLR_TYPE = 'PRL' THEN  B.NUMBER_OF_BLOCKS ELSE 0 END )                  "NUMBER_OF_BLOCKS_PRL",
      SUM(CASE WHEN B.POLR_TYPE = 'PRX' THEN  B.NUMBER_OF_BLOCKS ELSE 0 END )                  "NUMBER_OF_BLOCKS_PRX",
      SUM(CASE WHEN B.POLR_TYPE = 'PRL' THEN  B.SHARE_OF_LOAD ELSE 0 END )                     "SHARE_OF_LOAD_PRL",
      SUM(CASE WHEN B.POLR_TYPE = 'PRX' THEN  B.SHARE_OF_LOAD ELSE 0 END )                     "SHARE_OF_LOAD_PRX",
      AVG(CASE WHEN B.POLR_TYPE = 'PRX' THEN  B.DEC_MW * 1000  ELSE NULL END )                 "DEC_MW_PRX",
      AVG(CASE WHEN B.POLR_TYPE = 'PRL' THEN  B.DEC_MW * 1000  ELSE NULL END )                 "DEC_MW_PRL",
      AVG(CASE WHEN B.POLR_TYPE = 'PRX' THEN  B.INC_MW * 1000  ELSE NULL END)                  "INC_MW_PRX",
      AVG(CASE WHEN B.POLR_TYPE = 'PRL' THEN  B.INC_MW * 1000  ELSE NULL END )                 "INC_MW_PRL",
      SUM(CASE WHEN C.POLR_TYPE = 'PRL' THEN  C.BASE_BLOCK_SIZE - B.DEC_MW * 1000 ELSE 0 END ) "DEC_BASE_BLOCK_SIZE_PRL",
      SUM(CASE WHEN C.POLR_TYPE = 'PRX' THEN  C.BASE_BLOCK_SIZE - B.DEC_MW * 1000 ELSE 0 END ) "DEC_BASE_BLOCK_SIZE_PRX"
   FROM
      (SELECT PLC_DATE, DECODE(POLR_TYPE,'PRL','PRC','PRX','PRC', POLR_TYPE) POLR_TYPE, RFT_TICKET_NUMBER, PJM_SHORT_NAME, SUM(ICAP_VALUE) "ICAP_VALUE", SUM(NSPL_VALUE) "NSPL_VALUE"
      FROM CDI_PLC_LOAD WHERE DECODE(POLR_TYPE,'PRL','PRC','PRX','PRC', POLR_TYPE) = p_POLR_TYPE
      GROUP BY PLC_DATE, DECODE(POLR_TYPE,'PRL','PRC','PRX','PRC', POLR_TYPE), RFT_TICKET_NUMBER, PJM_SHORT_NAME) A,
         BGE_SUPPLIER_VIEW B, CDI_BID_BLOCK_HIST C
   WHERE A.PLC_DATE = p_PLC_DATE
      AND A.POLR_TYPE = p_POLR_TYPE
      AND DECODE(B.POLR_TYPE,'PRL','PRC','PRX', 'PRC', B.POLR_TYPE) = A.POLR_TYPE
      AND DECODE(C.POLR_TYPE,'PRL','PRC','PRX', 'PRC', C.POLR_TYPE) = A.POLR_TYPE
      AND A.RFT_TICKET_NUMBER = B.SUPPLIER_ID
      AND B.PJM_INC_SHORT IS NOT NULL
      AND C.RFP_ID = B.SUPPLIER_ID
      AND C.POLR_TYPE = B.POLR_TYPE
      AND p_PLC_DATE BETWEEN B.INC_DEC_START AND NVL(B.POWER_FLOW_END, p_PLC_DATE)
      AND p_PLC_DATE BETWEEN C.EFFECTIVE_DATE /*+ 1/(24*60) */  AND NVL(C.STOP_DATE, p_PLC_DATE)
   GROUP BY A.RFT_TICKET_NUMBER, DECODE(B.POLR_TYPE,'PRL','PRC','PRX', 'PRC', B.POLR_TYPE), A.PLC_DATE, A.PJM_SHORT_NAME, B.PJM_INC_SHORT, C.EFFECTIVE_DATE, B.POWER_FLOW_END) X
ORDER BY RFP_TICKET_NUMBER, POLR_TYPE;

BEGIN
   LOGS.LOG_INFO('System Settings INC_MW: ' || TO_CHAR(v_INC_MW) || ', DEC_MW: ' || TO_CHAR(v_DEC_MW));
   v_COUNTER('CDI_BID_BLOCK_HIST') := 0;
   v_COUNTER('CDI_BID_BLOCK_HIST-PRL') := 0;
   v_COUNTER('CDI_BID_BLOCK_HIST-PRX') := 0;
   v_COUNTER('CDI_INC_DEC_EVENT_HIST') := 0;
   v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRL') := 0;
   v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRX') := 0;
   v_COUNTER('CDI_BASE_LOAD_ALLOC') := 0;
   FOR v_SELECT_POLR IN c_SELECT_POLR LOOP
      LOGS.LOG_DEBUG('PLC_DATE: ' || TO_CHAR(v_SELECT_POLR.PLC_DATE, c_DATE_FORMAT) || ', POLR_TYPE: ' || v_SELECT_POLR.POLR_TYPE || ', ICAP_VALUE: ' || TO_CHAR(v_SELECT_POLR.ICAP_VALUE) || ', NSPL_VALUE: ' || TO_CHAR(v_SELECT_POLR.NSPL_VALUE) || ', ICAP_VALUE_PRL: ' || TO_CHAR(v_SELECT_POLR.ICAP_VALUE_PRL) || ', NSPL_VALUE_PRL: ' || TO_CHAR(v_SELECT_POLR.NSPL_VALUE_PRL) || ', ICAP_VALUE_PRX: ' || TO_CHAR(v_SELECT_POLR.ICAP_VALUE_PRX) || ', NSPL_VALUE_PRX: ' || TO_CHAR(v_SELECT_POLR.NSPL_VALUE_PRX));
      FOR v_SELECT IN c_SELECT(v_SELECT_POLR.PLC_DATE, v_SELECT_POLR.POLR_TYPE) LOOP
         IF v_SELECT.POLR_TYPE = 'PRC' THEN
            BEGIN
               SELECT SUM(AGG_BLOCK_ADJUSTMENT),
                  SUM(CASE WHEN POLR_TYPE = 'PRL' THEN AGG_BLOCK_ADJUSTMENT ELSE 0 END),
                  SUM(CASE WHEN POLR_TYPE = 'PRX' THEN AGG_BLOCK_ADJUSTMENT ELSE 0 END)
               INTO v_AGG_BLOCK_ADJUSTMENT, v_PRL_AGG_BLOCK_ADJUSTMENT, v_PRX_AGG_BLOCK_ADJUSTMENT
               FROM CDI_PLC_ANNUAL_ADJUST
               WHERE RFP_TICKET = v_SELECT.RFP_TICKET_NUMBER
                  AND PJM_SHORT_NAME = v_SELECT.PJM_SHORT_NAME
                  AND POLR_TYPE IN ('PRL','PRX')
                  AND v_SELECT.PLC_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, v_SELECT.PLC_DATE);
            EXCEPTION
               WHEN OTHERS THEN
                  v_AGG_BLOCK_ADJUSTMENT := 0;
                  v_PRL_AGG_BLOCK_ADJUSTMENT :=0;
                  v_PRX_AGG_BLOCK_ADJUSTMENT:=0;
            END;
            v_TOLERANCE := v_SELECT.BASE_BLOCK_SIZE_PRL + v_SELECT.BASE_BLOCK_SIZE_PRX;
            v_DEDUCTION := v_SELECT.BASE_BLOCK_SIZE_PRL + v_SELECT.BASE_BLOCK_SIZE_PRX;
            v_PRX_PERCENT_DEC := CASE WHEN v_TOLERANCE <> 0 THEN v_SELECT.BASE_BLOCK_SIZE_PRX / v_TOLERANCE ELSE 0 END;
            v_PRL_PERCENT_DEC := CASE WHEN v_TOLERANCE <> 0 THEN v_SELECT.BASE_BLOCK_SIZE_PRL / v_TOLERANCE ELSE 0 END;
            v_BASE_SIZE_PRL   := CASE WHEN v_SELECT.NUMBER_OF_BLOCKS_PRL <> 0 THEN ((v_SELECT_POLR.ICAP_VALUE_PRL * v_SELECT.SHARE_OF_LOAD_PRL)) + NVL(v_PRL_AGG_BLOCK_ADJUSTMENT,0) ELSE 0 END;
            v_BASE_SIZE_PRX   := CASE WHEN v_SELECT.NUMBER_OF_BLOCKS_PRX <> 0 THEN ((v_SELECT_POLR.ICAP_VALUE_PRX * v_SELECT.SHARE_OF_LOAD_PRX)) + NVL(v_PRX_AGG_BLOCK_ADJUSTMENT,0) ELSE 0 END;
            v_BASE_SIZE := v_BASE_SIZE_PRL + v_BASE_SIZE_PRX;
        ELSE
            BEGIN
               SELECT AGG_BLOCK_ADJUSTMENT INTO v_AGG_BLOCK_ADJUSTMENT
               FROM CDI_PLC_ANNUAL_ADJUST
               WHERE RFP_TICKET = v_SELECT.RFP_TICKET_NUMBER
                  AND PJM_SHORT_NAME = v_SELECT.PJM_SHORT_NAME
                  AND POLR_TYPE = v_SELECT.POLR_TYPE
                  AND v_SELECT.PLC_DATE BETWEEN BEGIN_DATE AND NVL(END_DATE, v_SELECT.PLC_DATE);
            EXCEPTION
               WHEN OTHERS THEN
                  v_AGG_BLOCK_ADJUSTMENT := 0;
            END;
            v_TOLERANCE := v_SELECT.BASE_BLOCK_SIZE;
            v_DEDUCTION := v_SELECT.BASE_BLOCK_SIZE ;
            v_BASE_SIZE := (v_SELECT.ICAP_VALUE * v_SELECT.SHARE_OF_LOAD) + v_AGG_BLOCK_ADJUSTMENT;
         END IF;
         v_BASE_SIZE     := CASE WHEN v_SELECT.MAX_NUMBER_OF_BLOCKS <> 0 THEN v_BASE_SIZE     / v_SELECT.MAX_NUMBER_OF_BLOCKS ELSE 0 END;
         v_BASE_SIZE_PRX := CASE WHEN v_SELECT.MAX_NUMBER_OF_BLOCKS <> 0 THEN v_BASE_SIZE_PRX / v_SELECT.MAX_NUMBER_OF_BLOCKS ELSE 0 END;
         v_BASE_SIZE_PRL := CASE WHEN v_SELECT.MAX_NUMBER_OF_BLOCKS <> 0 THEN v_BASE_SIZE_PRL / v_SELECT.MAX_NUMBER_OF_BLOCKS ELSE 0 END;

         LOGS.LOG_DEBUG('TOLERANCE: ' || TO_CHAR(v_TOLERANCE) || ', DEDUCTION: ' || TO_CHAR(v_DEDUCTION) || ', PRX_PERCENT_DEC: ' || TO_CHAR(ROUND(v_PRX_PERCENT_DEC,6)) || ', PRL_PERCENT_DEC: ' || TO_CHAR(ROUND(v_PRL_PERCENT_DEC,6)) || ', BASE_SIZE_PRL: ' || TO_CHAR(v_BASE_SIZE_PRL) || ', BASE_SIZE_PRX: ' || TO_CHAR(v_BASE_SIZE_PRX) || ', BASE_SIZE: ' || TO_CHAR(v_BASE_SIZE) || ', BASE_PERCENT: ' || TO_CHAR(v_BASE_PERCENT) || ', INC_PERCENT: ' || TO_CHAR(v_INC_PERCENT) || ', INC_LABEL: ' || v_INC_LABEL || ', DEC_LABEL: ' || v_DEC_LABEL);

         IF v_BASE_SIZE > (v_TOLERANCE + v_INC_MW) THEN
            LOGS.LOG_DEBUG('Increment Mode');
            v_TOLERANCE := v_TOLERANCE + v_INC_MW ;
            v_BASE_PERCENT :=  CASE WHEN v_BASE_SIZE <> 0 THEN v_TOLERANCE / v_BASE_SIZE ELSE 0 END;
            v_INC_PERCENT  :=  1 - v_BASE_PERCENT;
            INSERT INTO CDI_PLC_LOAD(PLC_DATE, ESP_ID, PSE_ID, PJM_SHORT_NAME, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, RFT_TICKET_NUMBER, PLC_BAND, ICAP_VALUE, NSPL_VALUE)
            SELECT DISTINCT PLC_DATE, ESP_ID, PSE_ID, v_SELECT.PJM_INC_SHORT, POLR_TYPE, VOLTAGE_CLASS, REPORTED_SEGMENT, RFT_TICKET_NUMBER, PLC_BAND, ICAP_VALUE * v_INC_PERCENT, NSPL_VALUE * v_INC_PERCENT
            FROM CDI_PLC_LOAD
            WHERE PLC_DATE = v_SELECT.PLC_DATE
               AND RFT_TICKET_NUMBER = v_SELECT.RFP_TICKET_NUMBER
               AND PJM_SHORT_NAME    = v_SELECT.PJM_SHORT_NAME
               AND CASE POLR_TYPE WHEN 'PRL' THEN 'PRC' WHEN 'PRX' THEN 'PRC' ELSE POLR_TYPE END = v_SELECT.POLR_TYPE;
            UPDATE CDI_PLC_LOAD SET ICAP_VALUE = ICAP_VALUE * v_BASE_PERCENT, NSPL_VALUE = NSPL_VALUE * v_BASE_PERCENT
            WHERE PLC_DATE = v_SELECT.PLC_DATE
               AND RFT_TICKET_NUMBER = v_SELECT.RFP_TICKET_NUMBER
               AND NVL(PJM_SHORT_NAME,'X') = NVL(v_SELECT.PJM_SHORT_NAME,'X')
               AND CASE NVL(POLR_TYPE,'X') WHEN 'PRL' THEN 'PRC' WHEN 'PRX' THEN 'PRC' ELSE NVL(POLR_TYPE,'X') END = NVL(v_SELECT.POLR_TYPE,'X');
            v_INC_LABEL := 'Y';
            v_DEC_LABEL := 'N';
         ELSIF v_BASE_SIZE < (v_DEDUCTION - v_DEC_MW) THEN
            LOGS.LOG_DEBUG('Decrement Mode');
            v_DEDUCTION := v_DEDUCTION - v_DEC_MW * FLOOR((v_DEDUCTION - v_BASE_SIZE)/v_DEC_MW);
            UPDATE CDI_BID_BLOCK_HIST SET STOP_DATE = v_SELECT.PLC_DATE - 1, EFFECTIVE_DATE = LEAST(v_SELECT.EFFECTIVE_DATE, v_SELECT.PLC_DATE - 2)
            WHERE RFP_ID = v_SELECT.RFP_TICKET_NUMBER
               AND CASE POLR_TYPE WHEN 'PRL' THEN 'PRC' WHEN 'PRX' THEN 'PRC' ELSE POLR_TYPE END = v_SELECT.POLR_TYPE
               AND EFFECTIVE_DATE = v_SELECT.EFFECTIVE_DATE;
            IF v_SELECT.POLR_TYPE = 'PRC' THEN
               INSERT INTO CDI_BID_BLOCK_HIST(RFP_ID, POLR_TYPE, BASE_BLOCK_SIZE, EFFECTIVE_DATE, STOP_DATE)
               VALUES(v_SELECT.RFP_TICKET_NUMBER, 'PRL', v_DEDUCTION * v_PRL_PERCENT_DEC, v_SELECT.PLC_DATE, GREATEST(v_SELECT.PLC_DATE + 1, v_SELECT.POWER_FLOW_END));
               v_COUNTER('CDI_BID_BLOCK_HIST-PRL') := v_COUNTER('CDI_BID_BLOCK_HIST-PRL') + SQL%ROWCOUNT;
               INSERT INTO CDI_BID_BLOCK_HIST(RFP_ID, POLR_TYPE, BASE_BLOCK_SIZE, EFFECTIVE_DATE, STOP_DATE)
               VALUES(v_SELECT.RFP_TICKET_NUMBER, 'PRX', v_DEDUCTION * v_PRX_PERCENT_DEC, v_SELECT.PLC_DATE, GREATEST(v_SELECT.PLC_DATE + 1, v_SELECT.POWER_FLOW_END));
               v_COUNTER('CDI_BID_BLOCK_HIST-PRX') := v_COUNTER('CDI_BID_BLOCK_HIST-PRX') + SQL%ROWCOUNT;
            ELSE
               INSERT INTO CDI_BID_BLOCK_HIST(RFP_ID, POLR_TYPE, BASE_BLOCK_SIZE, EFFECTIVE_DATE, STOP_DATE)
               VALUES(v_SELECT.RFP_TICKET_NUMBER, v_SELECT.POLR_TYPE, v_DEDUCTION, v_SELECT.PLC_DATE, GREATEST(v_SELECT.PLC_DATE + 1, v_SELECT.POWER_FLOW_END));
               v_COUNTER('CDI_BID_BLOCK_HIST') := v_COUNTER('CDI_BID_BLOCK_HIST') + SQL%ROWCOUNT;
            END IF;
            v_BASE_PERCENT := 1;
            v_INC_PERCENT  := 0;
            v_INC_LABEL := 'N';
            v_DEC_LABEL := 'Y';
         ELSE
            LOGS.LOG_DEBUG('Normal Mode');
            v_BASE_PERCENT := 1;
            v_INC_PERCENT  := 0;
            v_INC_LABEL := 'N';
            v_DEC_LABEL := 'N';
         END IF;

         IF v_SELECT.POLR_TYPE = 'PRC' THEN
            IF v_SELECT.BASE_BLOCK_SIZE_PRX <> 0 AND v_BASE_SIZE_PRX <> 0 THEN
               INSERT INTO CDI_INC_DEC_EVENT_HIST(RFP_TICKET, PJM_SHORT_NAME, POLR_TYPE, BASE_BLOCK_SIZE, NEW_BLOCK_SIZE, INC, DEC, PLC_DATE)
               VALUES(v_SELECT.RFP_TICKET_NUMBER, v_SELECT.PJM_SHORT_NAME, 'PRX', v_SELECT.BASE_BLOCK_SIZE_PRX, v_BASE_SIZE_PRX, v_INC_LABEL, v_DEC_LABEL, v_SELECT.PLC_DATE);
               v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRX') := v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRX') + SQL%ROWCOUNT;
            END IF;
            IF v_SELECT.BASE_BLOCK_SIZE_PRL <> 0 AND v_BASE_SIZE_PRL <> 0 THEN
               INSERT INTO CDI_INC_DEC_EVENT_HIST(RFP_TICKET, PJM_SHORT_NAME, POLR_TYPE, BASE_BLOCK_SIZE, NEW_BLOCK_SIZE, INC, DEC, PLC_DATE)
               VALUES(v_SELECT.RFP_TICKET_NUMBER, v_SELECT.PJM_SHORT_NAME, 'PRL', v_SELECT.BASE_BLOCK_SIZE_PRL, v_BASE_SIZE_PRL, v_INC_LABEL, v_DEC_LABEL, v_SELECT.PLC_DATE);
               v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRL') := v_COUNTER('CDI_INC_DEC_EVENT_HIST-PRL') + SQL%ROWCOUNT;
            END IF;
         ELSE
            INSERT INTO CDI_INC_DEC_EVENT_HIST(RFP_TICKET, PJM_SHORT_NAME, POLR_TYPE, BASE_BLOCK_SIZE, NEW_BLOCK_SIZE, INC, DEC, PLC_DATE)
            VALUES(v_SELECT.RFP_TICKET_NUMBER, v_SELECT.PJM_SHORT_NAME, v_SELECT.POLR_TYPE, v_SELECT.BASE_BLOCK_SIZE, v_BASE_SIZE, v_INC_LABEL, v_DEC_LABEL, v_SELECT.PLC_DATE);
            v_COUNTER('CDI_INC_DEC_EVENT_HIST') := v_COUNTER('CDI_INC_DEC_EVENT_HIST') + SQL%ROWCOUNT;
         END IF;

         MERGE INTO CDI_BASE_LOAD_ALLOC T
         USING
         (SELECT v_SELECT.PLC_DATE "PLC_DATE", v_SELECT.RFP_TICKET_NUMBER "RFP_TICKET", v_SELECT.POLR_TYPE "POLR_TYPE", CASE WHEN v_INC_LABEL = 'N' THEN 1 ELSE CASE WHEN v_BASE_SIZE <> 0 THEN v_TOLERANCE / v_BASE_SIZE ELSE 0 END END "BASE_LOAD_FACTOR" FROM DUAL WHERE v_SELECT.POLR_TYPE <> 'PRC'
         UNION ALL
         SELECT v_SELECT.PLC_DATE "PLC_DATE", v_SELECT.RFP_TICKET_NUMBER "RFP_TICKET",              'PRX' "POLR_TYPE", CASE WHEN v_INC_LABEL = 'N' THEN 1 ELSE CASE WHEN v_BASE_SIZE <> 0 THEN v_TOLERANCE / v_BASE_SIZE ELSE 0 END END "BASE_LOAD_FACTOR" FROM DUAL WHERE v_SELECT.POLR_TYPE = 'PRC'
         UNION ALL
         SELECT v_SELECT.PLC_DATE "PLC_DATE", v_SELECT.RFP_TICKET_NUMBER "RFP_TICKET",              'PRL' "POLR_TYPE", CASE WHEN v_INC_LABEL = 'N' THEN 1 ELSE CASE WHEN v_BASE_SIZE <> 0 THEN v_TOLERANCE / v_BASE_SIZE ELSE 0 END END "BASE_LOAD_FACTOR" FROM DUAL WHERE v_SELECT.POLR_TYPE = 'PRC') S
         ON (T.PLC_DATE = S.PLC_DATE AND T.RFP_TICKET = S.RFP_TICKET AND T.POLR_TYPE = S.POLR_TYPE)
         WHEN MATCHED THEN
            UPDATE SET T.BASE_LOAD_FACTOR = S.BASE_LOAD_FACTOR
         WHEN NOT MATCHED THEN
            INSERT(PLC_DATE, RFP_TICKET, POLR_TYPE, BASE_LOAD_FACTOR)
            VALUES(S.PLC_DATE, S.RFP_TICKET, S.POLR_TYPE, S.BASE_LOAD_FACTOR);
         v_COUNTER('CDI_BASE_LOAD_ALLOC') := v_COUNTER('CDI_BASE_LOAD_ALLOC') + SQL%ROWCOUNT;
      END LOOP;
   END LOOP;
   v_INDEX := v_COUNTER.FIRST;
   WHILE v_INDEX IS NOT NULL LOOP
      LOGS.LOG_INFO('Number Of Records Posted To ' || v_INDEX || ': ' || TO_CHAR(v_COUNTER(v_INDEX)));
      v_INDEX := v_COUNTER.NEXT(v_INDEX);
   END LOOP;
END POST_INCREMENT_DECREMENT;

PROCEDURE POST_PJM_ECAPACITY(p_BEGIN_DATE IN DATE, p_END_DATE IN DATE) AS
v_PRIOR_DATE DATE := CONSTANTS.LOW_DATE;
v_PLC_ID  PLS_INTEGER;
v_NSPL_ID PLS_INTEGER;
v_NSPL_VALUE NUMBER;
v_NSPL_TOTAL NUMBER;
v_NSPL_RATIO NUMBER;
v_PLC_TOTAL  NUMBER;
v_PLC_VALUE  NUMBER;
v_PLC_RATIO  NUMBER;
v_ALLOCATE BOOLEAN;
v_NSPL_ROUND_SHORT_NAME VARCHAR2(100);
v_PLC_ALLOCATION_VAL ANCILLARY_SERVICE_ALLOCATION.ALLOCATION_VAL%TYPE;
v_NSPL_ALLOCATION_VAL ANCILLARY_SERVICE_ALLOCATION.ALLOCATION_VAL%TYPE;
CURSOR c_SELECT_PLC_NSPL IS
   SELECT PLC_DATE, PJM_SHORT_NAME, SUM(ICAP_VALUE) "PLC_VALUE", SUM(NSPL_VALUE) "NSPL_VALUE"
   FROM CDI_PLC_LOAD
   WHERE PLC_DATE <= p_END_DATE
      AND PLC_DATE >= p_BEGIN_DATE
   GROUP BY PLC_DATE, PJM_SHORT_NAME
   ORDER BY PLC_DATE, PJM_SHORT_NAME;
CURSOR c_SELECT_ADJUSTMENT IS
   SELECT PLC_DATE, SUM(NSPL_VAL) "NSPL_VAL" FROM CDI_PJM_ECAPACITY_ADJ GROUP BY PLC_DATE;
BEGIN
   EXECUTE IMMEDIATE 'TRUNCATE TABLE CDI_PJM_ECAPACITY_ADJ';
   SELECT  NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_PLC_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_PLC_NAME;
   ASSERT(v_PLC_ID <> CONSTANTS.NOT_ASSIGNED, 'Ancillary Service For Peak Load Capacity Is Not Defined');
   SELECT  NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_NSPL_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_NSPL_NAME;
   ASSERT(v_NSPL_ID <> CONSTANTS.NOT_ASSIGNED, 'Ancillary Service For Network Service Peak Load Is Not Defined');
   SELECT MAX(ALLOCATION_VAL)*1000 INTO v_PLC_ALLOCATION_VAL FROM ANCILLARY_SERVICE_ALLOCATION WHERE ANCILLARY_SERVICE_ID = v_PLC_ID AND ALLOCATION_NAME = c_PLC_NAME AND p_BEGIN_DATE BETWEEN BEGIN_DATE AND END_DATE;
   LOGS.LOG_INFO('Peak Load Capacity Allocation Value: ' || TO_CHAR(v_PLC_ALLOCATION_VAL));
   IF NVL(v_PLC_ALLOCATION_VAL,0) <= 0 THEN
      ERRS.LOG_AND_RAISE('Peak Load Capacity Allocation Value Is Not Defined For ' || TO_CHAR(p_BEGIN_DATE, c_DATE_FORMAT));
   END IF;
   SELECT  MAX(ALLOCATION_VAL)*1000 INTO v_NSPL_ALLOCATION_VAL FROM ANCILLARY_SERVICE_ALLOCATION WHERE ANCILLARY_SERVICE_ID = v_NSPL_ID AND ALLOCATION_NAME = c_NSPL_NAME AND p_BEGIN_DATE BETWEEN BEGIN_DATE AND END_DATE;
   LOGS.LOG_INFO('Network Service Peak Load Allocation Value: ' || TO_CHAR(v_NSPL_ALLOCATION_VAL));
   IF NVL(v_NSPL_ALLOCATION_VAL,0) <= 0 THEN
      ERRS.LOG_AND_RAISE('Network Service Peak Load Allocation Value Is Not Defined For ' || TO_CHAR(p_BEGIN_DATE, c_DATE_FORMAT));
   END IF;
   v_NSPL_ROUND_SHORT_NAME := GET_DICTIONARY_VALUE('NSPL Rounding Correction Short Name', GA.ELECTRIC_MODEL, 'Scheduling', 'PJM Export');
   v_ALLOCATE := v_NSPL_ROUND_SHORT_NAME IS NOT NULL;
   LOGS.LOG_INFO('NSPL Allocation Enabled: ' || CASE WHEN v_ALLOCATE THEN 'Yes' ELSE 'No' END);

   FOR v_SELECT IN c_SELECT_PLC_NSPL LOOP
      IF NOT (v_PRIOR_DATE = v_SELECT.PLC_DATE) THEN
         SELECT SUM(NVL(NSPL_VALUE,0)) INTO v_NSPL_TOTAL FROM CDI_PLC_LOAD WHERE PLC_DATE = v_SELECT.PLC_DATE;
         SELECT SUM(NVL(ICAP_VALUE,0)) INTO v_PLC_TOTAL  FROM CDI_PLC_LOAD WHERE PLC_DATE = v_SELECT.PLC_DATE;
         v_NSPL_RATIO := CASE WHEN v_NSPL_TOTAL <> 0 THEN v_NSPL_ALLOCATION_VAL / v_NSPL_TOTAL ELSE 0 END;
         v_PLC_RATIO :=  CASE WHEN v_PLC_TOTAL  <> 0 THEN v_PLC_ALLOCATION_VAL  / v_PLC_TOTAL  ELSE 0 END;
         v_PRIOR_DATE := v_SELECT.PLC_DATE;
         UPDATE CDI_NETWORK_RECON_FACTOR SET FACTOR = v_NSPL_RATIO, ICAP_FACTOR = v_PLC_RATIO WHERE PLC_DATE = TRUNC(v_PRIOR_DATE);
         IF SQL%NOTFOUND THEN
            INSERT INTO CDI_NETWORK_RECON_FACTOR(PLC_DATE, FACTOR, ICAP_FACTOR) VALUES(TRUNC(v_PRIOR_DATE), v_NSPL_RATIO, v_PLC_RATIO);
         END IF;
      END IF;
      v_NSPL_VALUE := ROUND((v_SELECT.NSPL_VALUE*v_NSPL_RATIO)/1000,1);
      v_PLC_VALUE  := ROUND(v_SELECT.PLC_VALUE/1000,1);
      UPDATE CDI_PJM_ECAPACITY_ADJ SET NSPL_VAL = v_NSPL_VALUE, ICAP_VAL = v_PLC_VALUE WHERE PLC_DATE = v_SELECT.PLC_DATE AND PJM_SHORT_NAME = v_SELECT.PJM_SHORT_NAME;
      IF SQL%NOTFOUND THEN
         INSERT INTO CDI_PJM_ECAPACITY_ADJ(PLC_DATE, PJM_SHORT_NAME, ICAP_VAL, NSPL_VAL)
         VALUES(v_SELECT.PLC_DATE, v_SELECT.PJM_SHORT_NAME, v_PLC_VALUE, v_NSPL_VALUE);
      END IF;
      v_PRIOR_DATE := v_SELECT.PLC_DATE;
   END LOOP;

-- Allocate The Daily Difference--
   IF v_ALLOCATE THEN
      v_NSPL_TOTAL := ROUND((v_NSPL_ALLOCATION_VAL/1000),1);
      FOR v_SELECT IN c_SELECT_ADJUSTMENT LOOP
         v_NSPL_VALUE := v_NSPL_TOTAL - v_SELECT.NSPL_VAL;
         UPDATE CDI_PJM_ECAPACITY_ADJ SET NSPL_VAL = NSPL_VAL + v_NSPL_VALUE WHERE PLC_DATE = v_SELECT.PLC_DATE AND UPPER(PJM_SHORT_NAME) = UPPER(v_NSPL_ROUND_SHORT_NAME);
      END LOOP;
   END IF;
   COMMIT;
END POST_PJM_ECAPACITY;

FUNCTION GET_PLC_NSPL_CONTRACT
   (
   p_CONTRACT_NAME  IN VARCHAR2,
   p_CONTRACT_ALIAS IN VARCHAR2,
   p_CONTRACT_TYPE  IN VARCHAR2,
   p_BEGIN_DATE     IN DATE,
   p_END_DATE       IN DATE
   ) RETURN PLS_INTEGER AS
v_CONTRACT_ID PLS_INTEGER;
BEGIN
   SELECT MAX(CONTRACT_ID) INTO v_CONTRACT_ID FROM INTERCHANGE_CONTRACT WHERE CONTRACT_NAME = p_CONTRACT_NAME;
   IF v_CONTRACT_ID IS NULL THEN
      IO.PUT_INTERCHANGE_CONTRACT(
         o_OID                  => v_CONTRACT_ID,
         p_CONTRACT_NAME        => p_CONTRACT_NAME,
         p_CONTRACT_ALIAS       => p_CONTRACT_ALIAS,
         p_CONTRACT_DESC        => 'Retail Operations Generated Contract On '  || TO_CHAR(SYSDATE, c_DATE_TIME_FORMAT),
         p_CONTRACT_ID          => v_CONTRACT_ID,
         p_CONTRACT_STATUS      => 'Active',
         p_BEGIN_DATE           => p_BEGIN_DATE,
         p_END_DATE             => p_END_DATE,
         p_IS_EVERGREEN         => CONSTANTS.NOT_ASSIGNED,
         p_CONTRACT_TYPE        => p_CONTRACT_TYPE,
         p_BILLING_ENTITY_ID    => CONSTANTS.NOT_ASSIGNED,
         p_PURCHASER_ID         => CONSTANTS.NOT_ASSIGNED,
         p_SELLER_ID            => CONSTANTS.NOT_ASSIGNED,
         p_SOURCE_ID            => CONSTANTS.NOT_ASSIGNED,
         p_SINK_ID              => CONSTANTS.NOT_ASSIGNED,
         p_POR_ID               => CONSTANTS.NOT_ASSIGNED,
         p_POD_ID               => CONSTANTS.NOT_ASSIGNED,
         p_SC_ID                => CONSTANTS.NOT_ASSIGNED,
         p_AGREEMENT_TYPE       => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_APPROVAL_TYPE        => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_MARKET_TYPE          => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_LOSS_OPTION          => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_CONTRACT_FILE_NAME   => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_PIPELINE_ID          => CONSTANTS.NOT_ASSIGNED,
         p_PIPELINE_TARIFF_TYPE => CONSTANTS.UNDEFINED_ATTRIBUTE);
      INSERT INTO TP_CONTRACT_NUMBER(CONTRACT_ID, TP_ID, BEGIN_DATE, END_DATE, CONTRACT_NAME, CONTRACT_NUMBER, ENTRY_DATE)
      VALUES(v_CONTRACT_ID, CONSTANTS.NOT_ASSIGNED, p_BEGIN_DATE, p_END_DATE, p_CONTRACT_ALIAS, CONSTANTS.UNDEFINED_ATTRIBUTE, SYSDATE);   
      LOGS.LOG_INFO('New Ancillary Service Transaction Contract: ' || p_CONTRACT_NAME);
   ELSE
      UPDATE INTERCHANGE_CONTRACT SET BEGIN_DATE = LEAST(BEGIN_DATE, p_BEGIN_DATE), END_DATE = GREATEST(END_DATE, p_END_DATE) WHERE CONTRACT_ID = v_CONTRACT_ID;
      UPDATE TP_CONTRACT_NUMBER   SET BEGIN_DATE = LEAST(BEGIN_DATE, p_BEGIN_DATE), END_DATE = GREATEST(END_DATE, p_END_DATE) WHERE CONTRACT_ID = v_CONTRACT_ID;
   END IF;
RETURN v_CONTRACT_ID;
END GET_PLC_NSPL_CONTRACT;

FUNCTION GET_PLC_NSPL_TRANSACTION
   (
   p_TRANSACTION_NAME  IN VARCHAR2,
   p_TRANSACTION_ALIAS IN VARCHAR2,
   p_TRANSACTION_CODE  IN CHAR,
   p_BEGIN_DATE        IN DATE,
   p_END_DATE          IN DATE
   ) RETURN PLS_INTEGER AS
v_TRANSACTION_ID    PLS_INTEGER;
v_COMMODITY_ID      PLS_INTEGER;
v_SCHEDULE_GROUP_ID PLS_INTEGER;
v_CONTRACT_ID       PLS_INTEGER;
BEGIN
   SELECT MAX(TRANSACTION_ID) INTO v_TRANSACTION_ID FROM INTERCHANGE_TRANSACTION WHERE TRANSACTION_NAME = p_TRANSACTION_NAME;
   IF v_TRANSACTION_ID IS NULL THEN
      SELECT MAX(COMMODITY_ID) INTO v_COMMODITY_ID FROM IT_COMMODITY WHERE COMMODITY_NAME = CASE WHEN p_TRANSACTION_CODE = 'C' THEN 'Capacity' ELSE 'Transmission' END;
      SELECT MAX(SCHEDULE_GROUP_ID) INTO v_SCHEDULE_GROUP_ID FROM SCHEDULE_GROUP WHERE SCHEDULE_GROUP_NAME = 'EGS';
      v_CONTRACT_ID := GET_PLC_NSPL_CONTRACT(p_TRANSACTION_NAME, p_TRANSACTION_ALIAS, CASE WHEN p_TRANSACTION_CODE = 'C' THEN 'PLC' ELSE 'NSPL' END, p_BEGIN_DATE, p_END_DATE);
      IO.PUT_TRANSACTION(
         o_OID                      => v_TRANSACTION_ID,
         p_TRANSACTION_NAME         => p_TRANSACTION_NAME,
         p_TRANSACTION_ALIAS        => p_TRANSACTION_ALIAS,
         p_TRANSACTION_DESC         => 'Retail Operations Generated Ancillary Transaction On '  || TO_CHAR(SYSDATE, c_DATE_TIME_FORMAT),
         p_TRANSACTION_ID           => v_TRANSACTION_ID,
         p_TRANSACTION_TYPE         => 'Ancillary',
         p_TRANSACTION_CODE         => p_TRANSACTION_CODE,
         p_TRANSACTION_IDENTIFIER   => CASE WHEN p_TRANSACTION_CODE = 'C' THEN 'Peak Load Capacity' ELSE 'Network Service Peak Load' END,
         p_IS_FIRM                  => CONSTANTS.NOT_ASSIGNED,
         p_IS_IMPORT_SCHEDULE       => CONSTANTS.NOT_ASSIGNED,
         p_IS_EXPORT_SCHEDULE       => CONSTANTS.NOT_ASSIGNED,
         p_IS_BALANCE_TRANSACTION   => CONSTANTS.NOT_ASSIGNED,
         p_IS_BID_OFFER             => CONSTANTS.NOT_ASSIGNED,
         p_IS_EXCLUDE_FROM_POSITION => CONSTANTS.NOT_ASSIGNED,
         p_IS_IMPORT_EXPORT         => CONSTANTS.NOT_ASSIGNED,
         p_IS_DISPATCHABLE          => CONSTANTS.NOT_ASSIGNED,
         p_TRANSACTION_INTERVAL     => CONSTANTS.INTERVAL_DAY,
         p_EXTERNAL_INTERVAL        => CONSTANTS.INTERVAL_DAY,
         p_ETAG_CODE                => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_BEGIN_DATE               => p_BEGIN_DATE,
         p_END_DATE                 => p_END_DATE,
         p_PURCHASER_ID             => CONSTANTS.NOT_ASSIGNED,
         p_SELLER_ID                => CONSTANTS.NOT_ASSIGNED,
         p_CONTRACT_ID              => v_CONTRACT_ID,
         p_SC_ID                    => CONSTANTS.NOT_ASSIGNED,
         p_POR_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_POD_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_COMMODITY_ID             => v_COMMODITY_ID,
         p_SERVICE_TYPE_ID          => CONSTANTS.NOT_ASSIGNED,
         p_TX_TRANSACTION_ID        => CONSTANTS.NOT_ASSIGNED,
         p_PATH_ID                  => CONSTANTS.NOT_ASSIGNED,
         p_LINK_TRANSACTION_ID      => CONSTANTS.NOT_ASSIGNED,
         p_EDC_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_PSE_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_ESP_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_POOL_ID                  => CONSTANTS.NOT_ASSIGNED,
         p_SCHEDULE_GROUP_ID        => v_SCHEDULE_GROUP_ID,
         p_MARKET_PRICE_ID          => CONSTANTS.NOT_ASSIGNED,
         p_ZOR_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_ZOD_ID                   => CONSTANTS.NOT_ASSIGNED,
         p_SOURCE_ID                => CONSTANTS.NOT_ASSIGNED,
         p_SINK_ID                  => CONSTANTS.NOT_ASSIGNED,
         p_RESOURCE_ID              => CONSTANTS.NOT_ASSIGNED,
         p_AGREEMENT_TYPE           => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_APPROVAL_TYPE            => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_LOSS_OPTION              => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_TRAIT_CATEGORY           => CONSTANTS.UNDEFINED_ATTRIBUTE,
         p_TP_ID                    => CONSTANTS.NOT_ASSIGNED);
      LOGS.LOG_INFO('New Ancillary Service Transaction: ' || p_TRANSACTION_NAME);
   ELSE
      UPDATE INTERCHANGE_TRANSACTION SET BEGIN_DATE = LEAST(BEGIN_DATE, p_BEGIN_DATE), END_DATE = GREATEST(END_DATE, p_END_DATE) WHERE TRANSACTION_ID = v_TRANSACTION_ID;
      v_CONTRACT_ID := GET_PLC_NSPL_CONTRACT(p_TRANSACTION_NAME, p_TRANSACTION_ALIAS, CASE WHEN p_TRANSACTION_CODE = 'C' THEN 'PLC' ELSE 'NSPL' END, p_BEGIN_DATE, p_END_DATE);
   END IF;
   RETURN v_TRANSACTION_ID;
END GET_PLC_NSPL_TRANSACTION;

PROCEDURE POST_PLC_NSPL_SCHEDULE(p_BEGIN_DATE IN DATE, p_END_DATE IN DATE) AS
CURSOR c_SELECT_ANCILLARY IS SELECT ANCILLARY_SERVICE_ALIAS, SUBSTR(ANCILLARY_SERVICE_TYPE,1,1) "ANCILLARY_SERVICE_TYPE" FROM ANCILLARY_SERVICE WHERE SUBSTR(ANCILLARY_SERVICE_TYPE,1,1) IN ('C','N');
CURSOR c_SELECT_TRANSACTION IS SELECT DISTINCT PJM_SHORT_NAME FROM CDI_PJM_ECAPACITY_ADJ ORDER BY PJM_SHORT_NAME;
v_TRANSACTION_NAME VARCHAR2(128);
v_TRANSACTION_ID   PLS_INTEGER;
v_COUNT            PLS_INTEGER := 0;
v_BEGIN_DATE DATE;
v_END_DATE DATE;
BEGIN
   UT.CUT_DATE_RANGE(p_BEGIN_DATE, p_END_DATE, GA.LOCAL_TIME_ZONE, v_BEGIN_DATE, v_END_DATE);
   DELETE IT_SCHEDULE
   WHERE TRANSACTION_ID IN (SELECT TRANSACTION_ID FROM INTERCHANGE_TRANSACTION WHERE TRANSACTION_TYPE = 'Ancillary' AND TRANSACTION_CODE IN ('C','N'))
      AND SCHEDULE_TYPE = CONSTANTS.SCHEDULE_TYPE_FINAL 
      AND SCHEDULE_STATE = CONSTANTS.INTERNAL_STATE
      AND SCHEDULE_DATE BETWEEN v_BEGIN_DATE AND v_END_DATE
      AND AS_OF_DATE = CONSTANTS.LOW_DATE;
      LOGS.LOG_INFO('Number Of Ancillary Records Deleted From The IT_SCHEDULE Table: ' || TO_CHAR(SQL%ROWCOUNT));
   FOR v_SELECT_ANCILLARY IN c_SELECT_ANCILLARY LOOP
      FOR v_SELECT_TRANSACTION IN c_SELECT_TRANSACTION LOOP
         v_TRANSACTION_NAME := 'PJM-' || v_SELECT_TRANSACTION.PJM_SHORT_NAME || '_' || v_SELECT_ANCILLARY.ANCILLARY_SERVICE_ALIAS;
         v_TRANSACTION_ID := GET_PLC_NSPL_TRANSACTION(v_TRANSACTION_NAME, v_SELECT_TRANSACTION.PJM_SHORT_NAME, v_SELECT_ANCILLARY.ANCILLARY_SERVICE_TYPE, p_BEGIN_DATE, p_END_DATE);
         INSERT INTO IT_SCHEDULE(TRANSACTION_ID, SCHEDULE_TYPE, SCHEDULE_STATE, SCHEDULE_DATE, AS_OF_DATE, AMOUNT)
         SELECT v_TRANSACTION_ID, CONSTANTS.SCHEDULE_TYPE_FINAL, CONSTANTS.INTERNAL_STATE, PLC_DATE + c_ONE_SECOND, CONSTANTS.LOW_DATE, CASE WHEN v_SELECT_ANCILLARY.ANCILLARY_SERVICE_TYPE = 'C' THEN ICAP_VAL ELSE NSPL_VAL END
         FROM CDI_PJM_ECAPACITY_ADJ
         WHERE PJM_SHORT_NAME = v_SELECT_TRANSACTION.PJM_SHORT_NAME;
         v_COUNT := v_COUNT + SQL%ROWCOUNT;
      END LOOP;
      LOGS.LOG_INFO('Number Of ' || CASE WHEN v_SELECT_ANCILLARY.ANCILLARY_SERVICE_TYPE = 'C' THEN 'PLC' ELSE 'NSPL' END || ' Records Posted To The IT_SCHEDULE Table: ' || TO_CHAR(v_COUNT));
   END LOOP;
END POST_PLC_NSPL_SCHEDULE;

PROCEDURE ACCEPT_PLC_NSPL_SCHEDULE
   (
   p_BEGIN_DATE IN DATE,
   p_END_DATE   IN DATE,
   p_ENABLE     IN BOOLEAN,
   p_STATUS    OUT NUMBER,
   p_MESSAGE   OUT VARCHAR2
   ) AS
v_BEGIN_DATE DATE := TRUNC(p_BEGIN_DATE);
v_END_DATE   DATE := TRUNC(GREATEST(p_BEGIN_DATE, p_END_DATE));
v_STATUS     NUMBER;
v_ICAP_ID    PLS_INTEGER;
v_NSPL_ID    PLS_INTEGER;
v_MARK_TIME  PLS_INTEGER := DBMS_UTILITY.GET_TIME;
BEGIN
-- Start The Process Log --
   LOGS.START_PROCESS(c_ACCEPT_PLC_NSPL_SCHEDULE);
   LOGS.LOG_INFO('Begin Date: ' || TO_CHAR(v_BEGIN_DATE, c_DATE_FORMAT) || ', End Date: ' || TO_CHAR(v_END_DATE, c_DATE_FORMAT));
   SELECT NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_ICAP_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_PLC_NAME;
   ASSERT(v_ICAP_ID <> CONSTANTS.NOT_ASSIGNED, 'Peak Load Capacity Ancillary Service "' || c_PLC_NAME || '" Is Not Defined.');
   SELECT NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_NSPL_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_NSPL_NAME;
   ASSERT(v_NSPL_ID <> CONSTANTS.NOT_ASSIGNED, 'Network Service Ancillary Service "' || c_NSPL_NAME || '" Is Not Defined.');
-- Call The Legacy Code To Process --  
   CDI_STORE_PLC_DETAIL.MAIN(v_BEGIN_DATE, v_END_DATE, CASE WHEN p_ENABLE THEN 2 ELSE 1 END, v_STATUS);
-- Post The Associated Transaction Schedules --
   POST_PJM_ECAPACITY(v_BEGIN_DATE, v_END_DATE);
   POST_PLC_NSPL_SCHEDULE(v_BEGIN_DATE, v_END_DATE);   
-- Stop The Process Log --
   p_MESSAGE := c_ACCEPT_PLC_NSPL_SCHEDULE || ' Complete. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100));
   LOGS.LOG_INFO(p_MESSAGE);
   LOGS.STOP_PROCESS(p_MESSAGE, p_STATUS);
   COMMIT;
EXCEPTION
   WHEN OTHERS THEN
      p_STATUS := SQLCODE;
      p_MESSAGE := SQLERRM;
      ERRS.ABORT_PROCESS;
END ACCEPT_PLC_NSPL_SCHEDULE;

PROCEDURE ACCEPT_PLC_NSPL_OPTION_LIST(p_LABEL OUT VARCHAR2, p_CURSOR OUT GA.REFCURSOR) AS
BEGIN
   p_LABEL := 'Option';
   OPEN p_CURSOR FOR
      SELECT 'Forward Looking'      "OPTION_NAME", 1 "OPTION_ID" FROM DUAL UNION 
      SELECT 'eCapacity Submission' "OPTION_NAME", 2 "OPTION_ID" FROM DUAL
      ORDER BY OPTION_ID;
END ACCEPT_PLC_NSPL_OPTION_LIST;

PROCEDURE DEX_ACCEPT_PLC_NSPL_SCHEDULE
   (
   p_ENTITY_LIST IN VARCHAR2,  --Accept PLC Option Id--
   p_BEGIN_DATE  IN DATE,
   p_END_DATE    IN DATE,
   p_STATUS     OUT NUMBER,
   p_MESSAGE    OUT VARCHAR2
   ) AS
BEGIN
   ACCEPT_PLC_NSPL_SCHEDULE(p_BEGIN_DATE, p_END_DATE, TRIM(p_ENTITY_LIST) = '2', p_STATUS, p_MESSAGE);
END DEX_ACCEPT_PLC_NSPL_SCHEDULE;

PROCEDURE SET_INITIAL_BID_BLOCK_SIZE
   (
   p_BEGIN_DATE IN DATE,
   p_END_DATE   IN DATE,
   p_STATUS    OUT NUMBER,
   p_MESSAGE   OUT VARCHAR2
   ) AS
v_MARK_TIME  PLS_INTEGER := DBMS_UTILITY.GET_TIME;
BEGIN
-- Start The Process Log --
   LOGS.START_PROCESS(c_SET_INITIAL_BID_BLOCK_SIZE);
   LOGS.LOG_INFO('Begin Date: ' || TO_CHAR(p_BEGIN_DATE, c_DATE_FORMAT) || ', End Date: ' || TO_CHAR(p_END_DATE, c_DATE_FORMAT));
-- Call The Legacy Code To Process --  
   CDI_STORE_PLC_DETAIL.GET_INIT_BID_BLOCK_SIZE(p_BEGIN_DATE, p_END_DATE);
-- Stop The Process Log --
   LOGS.LOG_INFO(c_SET_INITIAL_BID_BLOCK_SIZE || ' Complete. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100)));
   LOGS.STOP_PROCESS(p_MESSAGE, p_STATUS);
   COMMIT;
EXCEPTION
   WHEN OTHERS THEN
      ERRS.ABORT_PROCESS;
END SET_INITIAL_BID_BLOCK_SIZE;


PROCEDURE POST_DEFAULT_PLC_NSPL(p_PLAN_YEAR IN DATE, p_STATUS OUT NUMBER, p_MESSAGE OUT VARCHAR2) AS
CURSOR c_PLC_NSPL IS
   SELECT ANCILLARY_SERVICE_ID, ANCILLARY_SERVICE_NAME, CASE ANCILLARY_SERVICE_NAME WHEN 'ICAP' THEN 'C' WHEN 'Network Service' THEN 'T' ELSE '?' END  ANCILLARY_SERVICE_TYPE
   FROM ANCILLARY_SERVICE
   WHERE ANCILLARY_SERVICE_NAME IN ('ICAP','Network Service');
CURSOR c_DEFAULT(p_ANCILLARY_SERVICE_ID IN NUMBER, p_TAG_TYPE IN CHAR) IS
   SELECT DISTINCT A.BILL_ACCOUNT, A.SERVICE_POINT, A.PREMISE_NUMBER, TO_CHAR(B.BEGIN_DATE,'YYYY') || p_TAG_TYPE "TAG_ID", B.BEGIN_DATE, B.END_DATE, B.DEFAULT_VAL,
      TO_CHAR(A.BILL_ACCOUNT)||','||TO_CHAR(A.SERVICE_POINT)||','||TO_CHAR(A.PREMISE_NUMBER)||','||TO_CHAR(B.BEGIN_DATE,'YYYY')||p_TAG_TYPE||','||TO_CHAR(B.BEGIN_DATE,c_DATE_FORMAT)||','||TO_CHAR(B.END_DATE,c_DATE_FORMAT)||','||TO_CHAR(B.DEFAULT_VAL) "RECORD"
   FROM CDI_MISSING_PLC_NSPL_TAG_CACHE A
      JOIN ANCILLARY_SERVICE_ALLOCATION B ON B.ANCILLARY_SERVICE_ID = p_ANCILLARY_SERVICE_ID AND B.ALLOCATION_NAME =  A.RATE_CLASS || '-' || A.VOLTAGE_LEVEL AND B.END_DATE >= A.EFFECTIVE_DATE
   WHERE A.SOURCE_BILL_ACCOUNT IS NULL
      AND A.EFFECTIVE_DATE <> A.TERMINATION_DATE
      AND RATE_CLASS IS NOT NULL
      AND VOLTAGE_LEVEL IS NOT NULL;     
v_TAG_ID VARCHAR2(16);
v_PLAN_YEAR NUMBER(4) := TO_NUMBER(TO_CHAR(p_PLAN_YEAR,'YYYY'));
v_BEGIN_DATE DATE;
v_END_DATE DATE;
v_TAG_TYPE CHAR(1);
v_COUNT PLS_INTEGER := 0;
v_TOTAL PLS_INTEGER := 0;
v_MARK_TIME PLS_INTEGER := DBMS_UTILITY.GET_TIME;
BEGIN
-- Start The Process Log --
   LOGS.START_PROCESS(c_POST_DEFAULT_PLC_NSPL);
   LOGS.LOG_INFO('Plan Year Date: ' || TO_CHAR(p_PLAN_YEAR, c_DATE_FORMAT));
   FOR v_PLC_NSPL IN c_PLC_NSPL LOOP 
-- Utilize Premise Inheritance To Assign The Default PLC/NSPL Value --
      v_TAG_TYPE := v_PLC_NSPL.ANCILLARY_SERVICE_TYPE;    
      v_TAG_ID := v_PLAN_YEAR || v_TAG_TYPE;    
      v_BEGIN_DATE := CASE WHEN v_PLC_NSPL.ANCILLARY_SERVICE_TYPE = 'C' THEN TO_DATE('06/01/' || TO_CHAR(v_PLAN_YEAR),   c_DATE_FORMAT) ELSE TO_DATE('01/01/' || TO_CHAR(v_PLAN_YEAR), c_DATE_FORMAT) END;
      v_END_DATE   := CASE WHEN v_PLC_NSPL.ANCILLARY_SERVICE_TYPE = 'C' THEN TO_DATE('05/31/' || TO_CHAR(v_PLAN_YEAR+1), c_DATE_FORMAT) ELSE TO_DATE('12/31/' || TO_CHAR(v_PLAN_YEAR), c_DATE_FORMAT) END; 
      LOGS.LOG_INFO('Ancillary Service: ' || v_PLC_NSPL.ANCILLARY_SERVICE_NAME || ', Plan Year Begin Date: ' || TO_CHAR(v_BEGIN_DATE, c_DATE_FORMAT) || ', End Date: ' || TO_CHAR(v_END_DATE, c_DATE_FORMAT));
      EXECUTE IMMEDIATE 'TRUNCATE TABLE CDI_MISSING_PLC_NSPL_TAG_CACHE';
-- Cache Accounts Without An Assignment --
      INSERT INTO CDI_MISSING_PLC_NSPL_TAG_CACHE(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, EFFECTIVE_DATE, TERMINATION_DATE, RATE_CLASS, VOLTAGE_LEVEL)
      SELECT DISTINCT BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, EFFECTIVE_DATE, TERMINATION_DATE, RATE_CLASS, VOLTAGE_LEVEL
      FROM BGE_MASTER_ACCOUNT X
      WHERE TERMINATION_DATE >= p_PLAN_YEAR
         AND NOT EXISTS (SELECT NULL FROM CDI_PLC_ICAP_TX WHERE BILL_ACCOUNT = X.BILL_ACCOUNT AND SERVICE_POINT = X.SERVICE_POINT AND PREMISE_NUMBER = X.PREMISE_NUMBER AND TAG_ID = v_TAG_ID);
      LOGS.LOG_INFO('Number Of "' || v_PLC_NSPL.ANCILLARY_SERVICE_NAME || '" Ancillary Service Accounts Missing An Assignment: ' || TO_CHAR(SQL%ROWCOUNT));
-- Select A Source Bill Account/Service Point Candidate Based On Premise Inheritance --      
      MERGE INTO CDI_MISSING_PLC_NSPL_TAG_CACHE T
      USING
         (SELECT * 
         FROM
            (SELECT A.BILL_ACCOUNT, A.SERVICE_POINT, B.BILL_ACCOUNT "SOURCE_BILL_ACCOUNT", A.PREMISE_NUMBER, B.SERVICE_POINT "SOURCE_SERVICE_POINT", ROW_NUMBER() OVER(PARTITION BY B.PREMISE_NUMBER ORDER BY B.PREMISE_NUMBER, B.BEGIN_DATE DESC) "TARGET_ROW"
            FROM CDI_MISSING_PLC_NSPL_TAG_CACHE A
               JOIN CDI_PLC_ICAP_TX B ON B.BILL_ACCOUNT <> A.BILL_ACCOUNT AND B.SERVICE_POINT <> A.SERVICE_POINT AND B.PREMISE_NUMBER = A.PREMISE_NUMBER AND SUBSTR(B.TAG_ID,5,1) = v_TAG_TYPE AND TO_NUMBER(SUBSTR(B.TAG_ID,1,4)) >= TO_NUMBER(TO_CHAR(EFFECTIVE_DATE,'YYYY')))
         WHERE TARGET_ROW = 1) S
      ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER)
      WHEN MATCHED THEN
         UPDATE SET T.SOURCE_BILL_ACCOUNT = S.SOURCE_BILL_ACCOUNT, T.SOURCE_SERVICE_POINT = S.SOURCE_SERVICE_POINT;
      LOGS.LOG_INFO('Number Of "' || v_PLC_NSPL.ANCILLARY_SERVICE_NAME || '" Premise Inheritance Source Assignments: ' || TO_CHAR(SQL%ROWCOUNT));
-- Utilize Premise Inheritance To Assign The Default PLC/NSPL Value --
      MERGE INTO CDI_PLC_ICAP_TX T
      USING
      (SELECT DISTINCT A.BILL_ACCOUNT, A.SERVICE_POINT, A.PREMISE_NUMBER, B.TAG_ID, B.BEGIN_DATE, B.END_DATE, B.TAG_VAL
      FROM CDI_MISSING_PLC_NSPL_TAG_CACHE A
         JOIN CDI_PLC_ICAP_TX B ON B.BILL_ACCOUNT = A.SOURCE_BILL_ACCOUNT AND B.SERVICE_POINT = A.SOURCE_SERVICE_POINT AND B.PREMISE_NUMBER = A.PREMISE_NUMBER AND SUBSTR(B.TAG_ID,5,1) = v_TAG_TYPE AND TO_NUMBER(SUBSTR(B.TAG_ID,1,4)) >= TO_NUMBER(TO_CHAR(EFFECTIVE_DATE,'YYYY'))) S
      ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER AND T.TAG_ID = S.TAG_ID)
      WHEN NOT MATCHED THEN
         INSERT(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, TAG_ID, BEGIN_DATE, END_DATE, TAG_VAL)
         VALUES(S.BILL_ACCOUNT, S.SERVICE_POINT, S.PREMISE_NUMBER, S.TAG_ID, S.BEGIN_DATE, S.END_DATE, S.TAG_VAL);
      LOGS.LOG_INFO('Number Of "' || v_PLC_NSPL.ANCILLARY_SERVICE_NAME || '" Premise Inheritance Default Assignment Records: ' || TO_CHAR(SQL%ROWCOUNT));
-- For Any Default Account Without A Premise Inheritance Assignment Use Rate Class/Voltage Assignment --
      v_COUNT := 0;
      FOR v_DEFAULT IN c_DEFAULT(v_PLC_NSPL.ANCILLARY_SERVICE_ID, v_TAG_TYPE) LOOP
         BEGIN
            LOGS.LOG_DEBUG('Default Assignment: ' || v_DEFAULT.RECORD);
            INSERT INTO CDI_PLC_ICAP_TX(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, TAG_ID, BEGIN_DATE, END_DATE, TAG_VAL)
            VALUES(v_DEFAULT.BILL_ACCOUNT, v_DEFAULT.SERVICE_POINT, v_DEFAULT.PREMISE_NUMBER, v_DEFAULT.TAG_ID, v_DEFAULT.BEGIN_DATE, v_DEFAULT.END_DATE, v_DEFAULT.DEFAULT_VAL);
            v_COUNT := v_COUNT + 1;
         EXCEPTION
            WHEN OTHERS THEN
               ERRS.LOG_AND_CONTINUE('Error In Default Assignment: ' || v_DEFAULT.RECORD);
         END;
      END LOOP;
      LOGS.LOG_INFO('Number Of "' || v_PLC_NSPL.ANCILLARY_SERVICE_NAME || '" Ancillary Service Allocation Default Assignments: ' || TO_CHAR(v_COUNT));
   END LOOP;
-- Stop The Process Log --
   p_MESSAGE := c_POST_DEFAULT_PLC_NSPL || ' Complete. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100));
   LOGS.LOG_INFO(p_MESSAGE);
   LOGS.STOP_PROCESS(p_MESSAGE, p_STATUS);
   COMMIT;
EXCEPTION
   WHEN OTHERS THEN
      p_STATUS := SQLCODE;
      p_MESSAGE := SQLERRM;
      ERRS.ABORT_PROCESS;
END POST_DEFAULT_PLC_NSPL;

PROCEDURE DEX_POST_DEFAULT_PLC(p_BEGIN_DATE IN DATE, p_STATUS OUT NUMBER, p_MESSAGE OUT VARCHAR2) AS
BEGIN
   POST_DEFAULT_PLC_NSPL(TRUNC(p_BEGIN_DATE), p_STATUS, p_MESSAGE);
END DEX_POST_DEFAULT_PLC;

PROCEDURE SERVICE_TYPE_LIST(p_LABEL OUT VARCHAR2, p_CURSOR OUT GA.REFCURSOR) AS
BEGIN
   p_LABEL := 'Service Type';
   OPEN p_CURSOR FOR
      SELECT 'PLC' "SERVICE_TYPE", 1 "SERVICE_TYPE_ID" FROM DUAL
      UNION
      SELECT 'NSPL' "SERVICE_TYPE", 2 "SERVICE_TYPE_ID" FROM DUAL
      ORDER BY SERVICE_TYPE_ID; 
END SERVICE_TYPE_LIST;

PROCEDURE LOAD_PLC_NSPL_INT_FROM_FILE
   (
   p_ENTITY_LIST      IN VARCHAR2,  --Service Type--
   p_IMPORT_FILE      IN CLOB,
   p_IMPORT_FILE_PATH IN VARCHAR2,
   p_STATUS          OUT NUMBER,
   p_MESSAGE         OUT VARCHAR2
   ) AS
v_BILL_ACCOUNT   NUMBER(10);
v_SERVICE_POINT  NUMBER(10);
v_PREMISE_NUMBER NUMBER(10);
v_POINT_VAL      NUMBER;
v_PEAK_DATE      DATE;
v_COUNT          PLS_INTEGER := 0;
v_ERROR_COUNT    PLS_INTEGER := 0;
v_ENTITY_SELECTION VARCHAR2(8) := CASE WHEN p_ENTITY_LIST IN ('1','8') THEN 'PLC' WHEN p_ENTITY_LIST IN ('2','9') THEN 'NSPL' ELSE '?' END;
v_CONTAINER      PARSE_UTIL.BIG_STRING_TABLE_MP;
v_TOKENS         PARSE_UTIL.STRING_TABLE;
v_FILE_CONTAINER CDI_PLC_NSPL_INIT_VALUE_LIST := CDI_PLC_NSPL_INIT_VALUE_LIST();
v_MARK_TIME  PLS_INTEGER := DBMS_UTILITY.GET_TIME;
BEGIN
-- Start The Process Log --
   LOGS.START_PROCESS(c_LOAD_PLC_NSPL_INT_FROM_FILE);
   LOGS.LOG_INFO('Entity List Selection: ' || v_ENTITY_SELECTION);
-- Clear Target Tables Prior To Loading --
   LOGS.LOG_INFO('');
   IF v_ENTITY_SELECTION = 'PLC' THEN
      DELETE RTO_STAGING.PLC_ICAP_INT_VALUE;
   ELSIF v_ENTITY_SELECTION = 'NSPL' THEN
      DELETE RTO_STAGING.PLC_TX_INT_VALUE;
   END IF;
   LOGS.LOG_INFO('Records Deleted From ' || v_ENTITY_SELECTION || ' Table: ' || TO_CHAR(SQL%ROWCOUNT) || '. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100)));
   COMMIT;
   v_MARK_TIME := DBMS_UTILITY.GET_TIME;
   IF c_FILE_LOAD_OPTION = 1 THEN
-- Parse The File Into Records --   
      PARSE_UTIL.PARSE_CLOB_INTO_LINES(p_IMPORT_FILE, v_CONTAINER);
      LOGS.LOG_INFO('Input File Container Count: ' || TO_CHAR(v_CONTAINER.COUNT));
-- Process Each Line --
      FOR v_INDEX IN v_CONTAINER.FIRST..v_CONTAINER.LAST LOOP
         PARSE_UTIL.PARSE_DELIMITED_STRING(v_CONTAINER(v_INDEX), c_COMMA, v_TOKENS);
         IF v_TOKENS.COUNT = 5 THEN
            v_BILL_ACCOUNT   := TRIM(v_TOKENS(1));
            v_SERVICE_POINT  := TRIM(v_TOKENS(2));
            v_PREMISE_NUMBER := TRIM(v_TOKENS(3));
            v_PEAK_DATE      := TO_DATE(TRIM(v_TOKENS(4)), c_DATE_TIME_FORMAT);
            v_POINT_VAL      := TRIM(v_TOKENS(5));
            IF v_ENTITY_SELECTION = 'PLC' THEN
               MERGE INTO RTO_STAGING.PLC_ICAP_INT_VALUE T
               USING (SELECT v_BILL_ACCOUNT "BILL_ACCOUNT", v_SERVICE_POINT "SERVICE_POINT", v_PREMISE_NUMBER "PREMISE_NUMBER", v_PEAK_DATE "PEAK_DATE", v_POINT_VAL "POINT_VAL" FROM DUAL) S
               ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER AND T.PEAK_DATE = S.PEAK_DATE)
               WHEN MATCHED THEN
                  UPDATE SET T.POINT_VAL = S.POINT_VAL
               WHEN NOT MATCHED THEN
                  INSERT(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL)
                  VALUES(S.BILL_ACCOUNT, S.SERVICE_POINT, S.PREMISE_NUMBER, S.PEAK_DATE, S.POINT_VAL);
               v_COUNT := v_COUNT + 1;
            ELSIF v_ENTITY_SELECTION = 'NSPL' THEN
               MERGE INTO RTO_STAGING.PLC_TX_INT_VALUE T
               USING (SELECT v_BILL_ACCOUNT "BILL_ACCOUNT", v_SERVICE_POINT "SERVICE_POINT", v_PREMISE_NUMBER "PREMISE_NUMBER", v_PEAK_DATE "PEAK_DATE", v_POINT_VAL "POINT_VAL" FROM DUAL) S
               ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER AND T.PEAK_DATE = S.PEAK_DATE)
               WHEN MATCHED THEN
                  UPDATE SET T.POINT_VAL = S.POINT_VAL
               WHEN NOT MATCHED THEN
                  INSERT(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL)
                  VALUES(S.BILL_ACCOUNT, S.SERVICE_POINT, S.PREMISE_NUMBER, S.PEAK_DATE, S.POINT_VAL);
               v_COUNT := v_COUNT + 1;
            END IF;
         ELSE
            LOGS.LOG_ERROR('Invalid Field Count, Expected: 5, Observed: ' || TO_CHAR(v_TOKENS.COUNT) || ', Record: ' || v_CONTAINER(v_INDEX));
            v_ERROR_COUNT := v_ERROR_COUNT + 1;
         END IF;
      END LOOP;
      p_MESSAGE := 'Load Complete. Number Of Records Posted To ' || v_ENTITY_SELECTION || ' Initial Table: ' || TO_CHAR(v_COUNT) || ', Errors Detected: ' || TO_CHAR(v_ERROR_COUNT) || '. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100)) || '.';
   ELSE
-- Parse The File Content Into Records --   
      PARSE_FILE_CONTENT(p_IMPORT_FILE, v_FILE_CONTAINER);
      LOGS.LOG_INFO('Input File Container Count: ' || TO_CHAR(v_FILE_CONTAINER.COUNT));
      IF v_ENTITY_SELECTION = 'PLC' THEN
         MERGE INTO RTO_STAGING.PLC_ICAP_INT_VALUE T
         USING (SELECT BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL FROM TABLE(CAST(v_FILE_CONTAINER AS CDI_PLC_NSPL_INIT_VALUE_LIST))) S
         ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER AND T.PEAK_DATE = S.PEAK_DATE)
         WHEN MATCHED THEN
            UPDATE SET T.POINT_VAL = S.POINT_VAL
         WHEN NOT MATCHED THEN
            INSERT(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL)
            VALUES(S.BILL_ACCOUNT, S.SERVICE_POINT, S.PREMISE_NUMBER, S.PEAK_DATE, S.POINT_VAL);
      ELSIF v_ENTITY_SELECTION = 'NSPL' THEN
         MERGE INTO RTO_STAGING.PLC_TX_INT_VALUE T
         USING (SELECT BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL FROM TABLE(CAST(v_FILE_CONTAINER AS CDI_PLC_NSPL_INIT_VALUE_LIST))) S
         ON (T.BILL_ACCOUNT = S.BILL_ACCOUNT AND T.SERVICE_POINT = S.SERVICE_POINT AND T.PREMISE_NUMBER = S.PREMISE_NUMBER AND T.PEAK_DATE = S.PEAK_DATE)
         WHEN MATCHED THEN
            UPDATE SET T.POINT_VAL = S.POINT_VAL
         WHEN NOT MATCHED THEN
            INSERT(BILL_ACCOUNT, SERVICE_POINT, PREMISE_NUMBER, PEAK_DATE, POINT_VAL)
            VALUES(S.BILL_ACCOUNT, S.SERVICE_POINT, S.PREMISE_NUMBER, S.PEAK_DATE, S.POINT_VAL);
      END IF;
      v_COUNT := SQL%ROWCOUNT;
      p_MESSAGE := 'Load Complete. Number Of Records Posted To ' || v_ENTITY_SELECTION || ' Initial Table: ' || TO_CHAR(v_COUNT) || '. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100)) || '.';
   END IF;
   p_STATUS := 0;
   LOGS.LOG_INFO(p_MESSAGE);
   LOGS.STOP_PROCESS(p_MESSAGE, p_STATUS);
   COMMIT;
EXCEPTION
   WHEN OTHERS THEN
      p_STATUS := SQLCODE;
      p_MESSAGE := SQLERRM;
      ERRS.ABORT_PROCESS;
END LOAD_PLC_NSPL_INT_FROM_FILE;

PROCEDURE POST_FTR_NSPL_SCHEDULE(p_SCHEDULE_DATE IN DATE) AS
CURSOR c_SELECT IS
   SELECT PJM_SHORT_NAME, SUM(NSPL_VALUE) "FTR_NSPL_VALUE"
   FROM CDI_PLC_NSPL_STAGE
   WHERE PLC_DATE = p_SCHEDULE_DATE
   GROUP BY PJM_SHORT_NAME
   ORDER BY PJM_SHORT_NAME;
v_TRANSACTION_NAME VARCHAR2(128);
v_TRANSACTION_ID   PLS_INTEGER;
v_COUNT            PLS_INTEGER := 0;
BEGIN
   FOR v_SELECT IN c_SELECT LOOP
      v_TRANSACTION_NAME := 'PJM-' || v_SELECT.PJM_SHORT_NAME || '_Network Service';
      v_TRANSACTION_ID := GET_PLC_NSPL_TRANSACTION(v_TRANSACTION_NAME, v_SELECT.PJM_SHORT_NAME, 'N', p_SCHEDULE_DATE, p_SCHEDULE_DATE);
      DELETE IT_SCHEDULE
      WHERE TRANSACTION_ID = v_TRANSACTION_ID
         AND SCHEDULE_TYPE = CONSTANTS.SCHEDULE_TYPE_FINAL 
         AND SCHEDULE_STATE = CONSTANTS.INTERNAL_STATE
         AND SCHEDULE_DATE = p_SCHEDULE_DATE + c_ONE_SECOND
         AND AS_OF_DATE = CONSTANTS.LOW_DATE;
      INSERT INTO IT_SCHEDULE(TRANSACTION_ID, SCHEDULE_TYPE, SCHEDULE_STATE, SCHEDULE_DATE, AS_OF_DATE, AMOUNT)
      VALUES(v_TRANSACTION_ID, CONSTANTS.SCHEDULE_TYPE_FINAL, CONSTANTS.INTERNAL_STATE, p_SCHEDULE_DATE + c_ONE_SECOND, CONSTANTS.LOW_DATE, v_SELECT.FTR_NSPL_VALUE);
      v_COUNT := v_COUNT + SQL%ROWCOUNT;
   END LOOP;
   LOGS.LOG_INFO('Number Of FTR-NSPL Records Posted To The IT_SCHEDULE Table: ' || TO_CHAR(v_COUNT));
END POST_FTR_NSPL_SCHEDULE;

PROCEDURE ACCEPT_FTR_NSPL_SCHEDULE(p_BEGIN_DATE IN DATE, p_STATUS OUT NUMBER, p_MESSAGE OUT VARCHAR2) AS
v_SCHEDULE_DATE DATE := TRUNC(p_BEGIN_DATE);
v_ICAP_ID    PLS_INTEGER;
v_NSPL_ID    PLS_INTEGER;
v_MARK_TIME  PLS_INTEGER := DBMS_UTILITY.GET_TIME;
BEGIN
-- Start The Process Log --
   LOGS.START_PROCESS(c_ACCEPT_FTR_NSPL_SCHEDULE);
   LOGS.LOG_INFO('Schedule Date: ' || TO_CHAR(v_SCHEDULE_DATE, c_DATE_FORMAT));
   SELECT NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_ICAP_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_PLC_NAME;
   ASSERT(v_ICAP_ID <> CONSTANTS.NOT_ASSIGNED, 'Peak Load Capacity Ancillary Service "' || c_PLC_NAME || '" Is Not Defined.');
   SELECT NVL(MAX(ANCILLARY_SERVICE_ID), CONSTANTS.NOT_ASSIGNED) INTO v_NSPL_ID FROM ANCILLARY_SERVICE WHERE ANCILLARY_SERVICE_NAME = c_NSPL_NAME;
   ASSERT(v_NSPL_ID <> CONSTANTS.NOT_ASSIGNED, 'Network Service Ancillary Service "' || c_NSPL_NAME || '" Is Not Defined.');
   EXECUTE IMMEDIATE 'TRUNCATE TABLE CDI_PLC_NSPL_STAGE';
   POST_COMPETITIVE_POLR(v_SCHEDULE_DATE, v_SCHEDULE_DATE, v_ICAP_ID, v_NSPL_ID);
   POST_NON_COMPETITIVE_POLR(v_SCHEDULE_DATE, v_SCHEDULE_DATE, v_ICAP_ID, v_NSPL_ID);
   POST_FTR_NSPL_SCHEDULE(v_SCHEDULE_DATE);   
-- Stop The Process Log --
   p_MESSAGE := c_ACCEPT_FTR_NSPL_SCHEDULE || ' Complete. Elapsed Seconds: ' || TO_CHAR(ROUND((DBMS_UTILITY.GET_TIME-v_MARK_TIME)/100));
   LOGS.LOG_INFO(p_MESSAGE);
   LOGS.STOP_PROCESS(p_MESSAGE, p_STATUS);
   COMMIT;
EXCEPTION
   WHEN OTHERS THEN
      p_STATUS := SQLCODE;
      p_MESSAGE := SQLERRM;
      ERRS.ABORT_PROCESS;
END ACCEPT_FTR_NSPL_SCHEDULE;

END CDI_ANCILLARY_SERVICE;
/
