CREATE OR REPLACE PROCEDURE ADD_TABLE_PARTITIONS
   (
   p_TABLE      IN VARCHAR2,
   p_TABLESPACE IN VARCHAR2,
   p_YEAR       IN DATE,
   p_IS_DST     IN BOOLEAN DEFAULT TRUE,
   p_MODE       IN VARCHAR2 DEFAULT 'IMMEDIATE'
   ) AS
v_BEGIN_DATE DATE := TRUNC(p_YEAR,'YEAR');
v_END_DATE DATE := LAST_DAY(ADD_MONTHS(v_BEGIN_DATE,11));
v_SERVICE_DATE DATE;
v_PARTITION_DATE DATE;
v_PARTITION VARCHAR2(4000);
v_STATEMENT VARCHAR2(64) := 'ALTER TABLE ' || p_TABLE || ' ADD ';
-- Do Not Create A Sub-Partition For "Forecast"--
v_TEMPLATE VARCHAR2(4000) := 'PARTITION P<yyyymmdd> VALUES LESS THAN (TO_DATE('' yyyy-mm-dd hh:mi:ss'', ''SYYYY-MM-DD HH24:MI:SS'')) TABLESPACE <tablespace> (SUBPARTITION P<yyyymmdd>B VALUES(''B'') TABLESPACE <tablespace>, SUBPARTITION P<yyyymmdd>A VALUES (''A'') TABLESPACE <tablespace>)';
BEGIN
   IF p_MODE NOT IN ('DBMS_OUTPUT','IMMEDIATE') THEN
      RAISE_APPLICATION_ERROR(-20101, 'Invalid Mode: "' || p_MODE || '". Mode Must Be Specified As "IMMEDIATE" Or "DBMS_OUTPUT".');
   END IF;
   v_TEMPLATE := v_TEMPLATE || CASE WHEN p_MODE = 'DBMS_OUTPUT' THEN ';' ELSE '' END;
   v_SERVICE_DATE := v_BEGIN_DATE;
   WHILE v_SERVICE_DATE <= v_END_DATE LOOP
      v_PARTITION_DATE := CASE WHEN p_IS_DST THEN ADD_SECONDS_TO_DATE(TO_CUT(v_SERVICE_DATE + 1, LOCAL_TIME_ZONE),1) ELSE v_SERVICE_DATE + 1 END; 
      v_PARTITION := v_STATEMENT || REPLACE(REPLACE(v_TEMPLATE, '<yyyymmdd>', TO_CHAR(v_SERVICE_DATE,'YYYYMMDD')), 'yyyy-mm-dd hh:mi:ss', TO_CHAR(v_PARTITION_DATE,'YYYY-MM-DD HH24:MI:SS'));
      v_PARTITION := REPLACE(v_PARTITION, '<tablespace>', p_TABLESPACE);
      IF p_MODE = 'IMMEDIATE' THEN
         EXECUTE IMMEDIATE v_PARTITION;
      ELSIF p_MODE = 'DBMS_OUTPUT' THEN    
         DBMS_OUTPUT.PUT_LINE(v_PARTITION);
      END IF;
      v_SERVICE_DATE := v_SERVICE_DATE + 1;
   END LOOP;
END ADD_TABLE_PARTITIONS;
/
