CREATE OR REPLACE FUNCTION MM_IS_MARKET_CLOSED_TXN
	(
	p_CLOSING_TYPE IN VARCHAR2,
	p_SCHEDULE_DATE IN DATE,
	p_TRANSACTION_ID IN NUMBER
	) RETURN BOOLEAN AS

-- return TRUE if the market is already closed for that date.
-- get market closing information from MARKET_CLOSINGS table.
-- if an entry does not exist in the table, then assume the
-- market never closes as far as MM is concerned.
v_MARKET_CLOSE_DATE DATE;
BEGIN

-- 	UT.DEBUG_TRACE('MM_IS_MARKET_CLOSED_TXN');
--  	UT.DEBUG_TRACE('CLOSING_TYPE=' || p_CLOSING_TYPE || '.');
-- 	UT.DEBUG_TRACE('TRANSACTION_ID=' || p_TRANSACTION_ID || '.');
-- 	UT.DEBUG_TRACE('SCHEDULE_DATE=' || UT.TRACE_DATE(p_SCHEDULE_DATE));
-- 	UT.DEBUG_TRACE('SYSDATE=' || UT.TRACE_DATE(SYSDATE));

	SELECT
		CASE TRUNCATE_TO
			WHEN 'Day' THEN 
				TRUNC(p_SCHEDULE_DATE - 1/86400)
			WHEN 'Hour' THEN
				TRUNC(p_SCHEDULE_DATE - 1/86400, 'HH') + 1/24
			ELSE
				p_SCHEDULE_DATE
			END
		+ NVL(DAYS_ADDED,0) + (NVL(HOURS_ADDED,0)/24) + (NVL(MINUTES_ADDED,0)/1440)
	INTO v_MARKET_CLOSE_DATE
	FROM MARKET_CLOSINGS A, INTERCHANGE_TRANSACTION B 
	WHERE B.TRANSACTION_ID = p_TRANSACTION_ID
		AND A.CLOSING_TYPE = p_CLOSING_TYPE
		AND A.SC_ID = B.SC_ID
		AND A.COMMODITY_ID = B.COMMODITY_ID
		AND A.TRANSACTION_TYPE = B.TRANSACTION_TYPE
		AND A.IS_IMPORT_EXPORT = B.IS_IMPORT_EXPORT;
		
-- 	UT.DEBUG_TRACE('MARKET_CLOSE_DATE=' || UT.TRACE_DATE(v_MARKET_CLOSE_DATE));
-- 	UT.DEBUG_TRACE('MARKET CLOSED=' || UT.TRACE_BOOLEAN(SYSDATE > v_MARKET_CLOSE_DATE));
	
	RETURN SYSDATE > v_MARKET_CLOSE_DATE;	

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		RETURN FALSE; --Assume market does not close if it is not in table.
	WHEN OTHERS THEN
		RAISE;
END MM_IS_MARKET_CLOSED_TXN;
/

