CREATE OR REPLACE TRIGGER STATION_PARM_PROJECTION_UPDATE
	BEFORE INSERT OR UPDATE ON STATION_PARAMETER_PROJECTION
	FOR EACH ROW

DECLARE
v_PROJECTION_PERIOD CHAR(1);
v_STATUS NUMBER;
v_HISTORICAL_BEGIN_DATE DATE;
v_HISTORICAL_END_DATE DATE;
v_BEGIN_DATE DATE;
v_END_DATE DATE;

BEGIN

	SELECT UPPER(SUBSTR(PROJECTION_PERIOD,1,1))
	INTO v_PROJECTION_PERIOD
	FROM WEATHER_PARAMETER
	WHERE PARAMETER_ID = :new.PARAMETER_ID;
	
	SELECT MIN(PARAMETER_DATE), MAX(PARAMETER_DATE)
	INTO v_BEGIN_DATE, v_END_DATE
	FROM STATION_PARAMETER_VALUE
	WHERE STATION_ID = :new.STATION_ID
		AND PARAMETER_ID = :new.PARAMETER_ID
		AND PARAMETER_CODE = 'A';
		
	:new.PARAMETER_DATE := TRUNC(:new.PARAMETER_DATE);
	IF GA.DEFAULT_MODEL = GA.ELECTRIC_MODEL THEN
		v_BEGIN_DATE := TRUNC(FROM_CUT(v_BEGIN_DATE, LOCAL_TIME_ZONE));
		v_END_DATE := TRUNC(FROM_CUT(v_END_DATE - 0.00001, LOCAL_TIME_ZONE));
	ELSE
		v_BEGIN_DATE := TRUNC(v_BEGIN_DATE);
		v_END_DATE := TRUNC(v_END_DATE);
	END IF;
	
	FS.SIMILAR_DAY_TYPE(0, :new.PARAMETER_DATE, v_BEGIN_DATE, v_END_DATE, v_HISTORICAL_BEGIN_DATE, v_STATUS);

	IF v_PROJECTION_PERIOD = 'M' THEN
		v_HISTORICAL_BEGIN_DATE := TO_DATE(TO_CHAR(:new.PARAMETER_DATE, 'DD-MON-') || TO_CHAR(v_HISTORICAL_BEGIN_DATE, 'YYYY'), 'DD-MON-YYYY');
		v_HISTORICAL_END_DATE := LAST_DAY(v_HISTORICAL_BEGIN_DATE);
	ELSIF v_PROJECTION_PERIOD = 'W' THEN
		v_HISTORICAL_END_DATE := v_HISTORICAL_BEGIN_DATE + 6;
	ELSE
		v_HISTORICAL_END_DATE := v_HISTORICAL_BEGIN_DATE;
	END IF;

	UT.CUT_DAY_INTERVAL_RANGE(GA.DEFAULT_MODEL, v_HISTORICAL_BEGIN_DATE, v_HISTORICAL_END_DATE, LOCAL_TIME_ZONE, :new.HISTORICAL_BEGIN_DATE, :new.HISTORICAL_END_DATE);

	IF GA.DEFAULT_MODEL = GA.GAS_MODEL THEN
		:new.HISTORICAL_END_DATE := TRUNC(:new.HISTORICAL_END_DATE);
	END IF;
		
	SELECT MIN(PARAMETER_VAL), MAX(PARAMETER_VAL), AVG(PARAMETER_VAL), SUM(PARAMETER_VAL), COUNT(*)
	INTO  :new.HISTORICAL_MIN, :new.HISTORICAL_MAX, :new.HISTORICAL_AVG, :new.HISTORICAL_SUM, :new.HISTORICAL_CNT 
	FROM STATION_PARAMETER_VALUE
	WHERE STATION_ID = :new.STATION_ID
		AND PARAMETER_ID = :new.PARAMETER_ID
		AND PARAMETER_CODE = 'A'
		AND PARAMETER_DATE BETWEEN :new.HISTORICAL_BEGIN_DATE AND :new.HISTORICAL_END_DATE;

	IF :new.HISTORICAL_CNT > 0 AND :new.HISTORICAL_SUM > 0 THEN
		:new.HISTORICAL_FACTOR := (:new.HISTORICAL_CNT * :new.PARAMETER_AVG) / :new.HISTORICAL_SUM;
	ELSE
		:new.HISTORICAL_FACTOR := 1.0;
	END IF;
	
	:new.ENTRY_DATE := SYSDATE;

EXCEPTION
	WHEN NO_DATA_FOUND THEN
		NULL;
	WHEN OTHERS THEN
		RAISE;
END STATION_PARM_PROJECTION_UPDATE;
/
